ASMB,R,L,B
      NAM SBITF,7 
      ENT SBITF 
      EXT .ENTR 
      SPC 1 
IWD   NOP 
IST   NOP 
ILEN  NOP 
IVAL  NOP 
      SPC 1 
*     THIS IS AN ALGOL/FORTRAN CALLABLE SUBROUTINE WHICH
*     WILL SET A BIT FIELD IN A WORD PAIR ACCORDING TO THE VALUE
*     TRANSFERRED IN LOCATION IVAL. 
      SPC 1 
*     THE VARIABLES HAVE THE FOLLOWING SIGNIFICANCE 
*       IWD = THE DESTINATION ADDRESS 
*       IST = THE STARTING BIT OF THE FIELD 
*       ILEN = LENGTH OF BIT FIELD
*       IVAL = VALUE TO BE INCLUDED IN IWD
      SPC 3 
SBITF NOP           ENTRY/EXIT
      JSB .ENTR     LINK THE PARAMETERS 
      DEF IWD 
      LDA ILEN,I    GET THE BIT LENGTH
      ADA TABLE     ADD STARTING ADDRESS
      LDA 0,I       GET DESIRED MASK
      STA MASK      SAVE FOR LATER USE
      LDA IST,I     GET STARTING BIT LOCATION 
      AND =B20      CHECK FOR A/B REG 
      STA FLAG
      SPC 1 
      JSB INST
      JSB SWAP
      DLD IWD,I 
* .SWP  SWP 
.SWP  OCT 101100
* SW.1  RRR 8 
SW.1  OCT 101110
      AND MASK
      IOR IVAL,I    INCLUDE THE DATA
* SWP.  SWP 
SWP.  OCT 101100
* L.INS RRL 4         TO STARTING POSITION
L.INS OCT 100104
      DST IWD,I 
      JMP SBITF,I 
      SPC 1 
INST  NOP           ENTRY/EXIT
      LDA IST,I 
      AND =B17
      STA 1 
      STA FL2 
      IOR RRL 
      STA L.INS 
      IOR =B1000
      STA SW.1
      JMP INST,I
      SPC 1 
SWAP  NOP           ENTRY 
      LDA FLAG
      LDB FL2 
      SZA 
      JMP .SWAP 
      STA .SWP
      STA SWP.
      SZB 
      JMP .FIN
      STA SW.1
      STA L.INS 
.FIN  JMP SWAP,I
.SWAP LDA .SWP. 
      SZB,RSS 
      CLA 
      STA .SWP
      STA SWP.
      JMP SWAP,I
      SPC 1 
* .SWP. SWP 
.SWP. OCT 101100
FLAG  NOP 
RRL   OCT 100100
FL2   NOP 
MASK  OCT 0 
TABLE DEF * 
      OCT 177776,177774,177770,177760,177740
      OCT 177700,177600,177400,177000,176000
      OCT 174000,170000,160000,140000,100000
      SPC 1 
      END 
