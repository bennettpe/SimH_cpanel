ASMB,R,L,B
************************************************************* 
*     SCOP1 DISPLAYS ON A X-Y SCOPE ISIZE POINTS OF 
*           BUFFER DATA 
*     SCOP1 - ENTERS THROUGH    SCOP1 ---- INITIATES
*                     AND       SCP1T ---- TERMINATES 
*     FORTRAN CALL
*                 DIMENSION IDATA(ID) 
*                    .
*                    .
*                 CALL SCOP1(ISIZE,IDATA) 
*                           ISIZE=NUMBER OF PLOTTED POINTS
*                                 LESS THAN ID
*                           IDATA=BUFFERED DATA (PACKED)
*                   . 
*                   . 
*                 CALL SCP1T    (TURN OFF DISPLAY)
********************************************************* 
      NAM SCOP1,6 
      ENT SCOP1,SCP1T 
      EXT .ENTR 
LINK  EQU 31B 
IO    EQU 17B 
ND    NOP 
IDATA NOP 
SCOP1 NOP 
      JSB .ENTR 
      DEF ND
      LDA ND,I
      CMA,INA 
      STA LOOPS 
      LDA LK
      STA LINK
      LDA BUSY
      SZA 
      JMP *-2 
      ISZ BUSY
      LDA IDATA 
      STA DATA
      STC IO,C
      LDA IJSB
      STA IO
      JMP SCOP1,I 
SCP1T NOP 
      JSB .ENTR 
      DEF SCP1T 
      CLC IO
      LDA SAVE
      STA IO
      JMP SCP1T,I 
BUSY  OCT 0 
END   BSS 1 
DATA  BSS 1 
LK    DEF CONT
IJSB  JSB LINK,I
CONT  NOP 
      CLF 0 
      STA SAVEA 
      STB SAVEB 
      ERA,ALS 
      SOC 
      INA 
      STA SAVEO 
      LDB LOOPS 
      STB CNTR
      LDB DATA
LOOP  LDA 1,I 
      OTA IO
      INB 
      ISZ CNTR
      JMP LOOP
      CLA 
      STA BUSY
      LDA SAVEO 
      CLO 
      SLA,ELA 
      STO 
      LDA SAVEA 
      LIB 1 
      SSB 
      JMP JEND
      LDB SAVEB 
      CLF IO
      STF 0 
      JMP CONT,I
JEND  CLC IO
      LDB SAVE
      STB IO
      LDB SAVEB 
      JMP *-6 
SAVE  OCT 114532
SAVEA BSS 1 
SAVEB BSS 1 
SAVEO BSS 1 
CNTR  BSS 1 
LOOPS BSS 1 
      END 
