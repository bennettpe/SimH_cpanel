ASMB,R,L,B
********************************************************* 
*     SCOP3  DISPLAYS ON A X-Y SCOPE 256 POINTS WHICH 
*            START (ISKIP) FROM THE FIRST WORD OF THE 
*            BUFFER IDATA, AND DISPLAYS A CURSER (ICURS)
*            CHANNELS FROM THE LEFT EDGE OF THE SCREEN
* 
*     SCOP3 - ENTERS THROUGH  SCOP3 -- INITIATES
*                   AND       SCP3T -- TERMINATES 
* 
*     FORTRAN CALL
*          DIMENSION IDATA
*            .
*            .
*          CALL SCOP3(ISKIP,ICURS,IDATA)
*                     ISKIP=STARTING POINT FOR DISPLAY
*                     ICURS=LOCATION OF CURSER
*                           CURSER IS TURNED OFF WITH ICURS=-1
*                     IDATA=BUFFERED DATA 
*            .
*            .
*         CALL SCP3T    TURNS OFF DISPLAY 
******************************************************* 
      NAM SCOP3,7 
      ENT SCOP3,SCP3T 
      EXT .ENTR 
LINK  EQU 31B 
IO    EQU 17B 
ISKIP NOP 
CURS  NOP 
IDATA NOP 
SCOP3 NOP 
      JSB .ENTR 
      DEF ISKIP 
* 
      LDA LK
      STA LINK
* 
      STC IO,C
      LDA IJSB
      STA IO
      JMP SCOP3,I 
* 
SCP3T NOP 
      JSB .ENTR 
      DEF SCP3T 
      CLC IO
      LDA SAVE
      STA IO
      JMP SCP3T,I 
* 
LK    DEF CONT
IJSB  JSB LINK,I
* 
CONT  NOP 
      CLF 0 
* 
      STA SAVEA 
      STB SAVEB 
      ERA,ALS 
      SOC 
      INA 
      STA SAVEO 
* 
      LDA IDATA 
      ADA ISKIP,I 
      STA PNTR
* 
      LDA M255
      STA CNTR
* 
      CLA 
      STA X 
* 
J1    LDA PNTR,I
      ALF,ALF 
      ADA X 
      OTA IO
      ISZ X 
      ISZ PNTR
      ISZ CNTR
      JMP J1
* 
      LDB CURS,I
      SSB 
      JMP J3
      CLA 
      STA X 
      LDA M255
      STA CNTR
J2    LDA X 
      ALF,ALF 
      ADA 1 
      OTA IO
      ISZ X 
      ISZ CNTR
      JMP J2
* 
J3    LDA SAVEO 
      CLO 
      SLA,ELA 
      STO 
      LDA SAVEA 
      LIB 1 
      SSB 
      JMP JEND
      LDB SAVEB 
      CLF IO
J4    STF 0 
      JMP CONT,I
JEND  CLC IO
      LDB SAVE
      STB IO
      LDB SAVEB 
      JMP J4
* 
* 
SAVE  OCT 114532
X     BSS 1 
SAVEA BSS 1 
SAVEB BSS 1 
SAVEO BSS 1 
PNTR  BSS 1 
CNTR  BSS 1 
M255  DEC -255
      END 
