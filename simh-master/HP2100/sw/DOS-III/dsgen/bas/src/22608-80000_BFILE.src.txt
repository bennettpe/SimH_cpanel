ASMB,R,L,X
      NAM BFILE,3 
      SPC 2 
      EXT EXEC
      SPC 2 
START JSB EXEC     CHECK EXISTENCE OF $FILS 
      DEF *+4 
      DEF R18 
      DEF FILA
      DEF NSECT 
      LDA NSECT    GET # OF SECTORS 
      SZA          DOES FILE EXIST? 
      JMP CON0     YES. CONTINUE
TERM0 JSB EXEC     NO. PRINT ERROR MESSAGE
      DEF *+5 
      DEF R2
      DEF C1
MESA  DEF MES0
      DEF ML0 
      SPC 1 
TERM1 JSB EXEC     TERMINATE
      DEF *+2 
      DEF R6
      SPC 1 
CON0  ADA =D-3     CHECK FOR $FILS < 3 SECTORS
      SSA,RSS      TOO SMALL? 
      JMP CON1     NO. CONTINUE 
      LDA MS1      YES. PRINT ERROR MESSAGE 
      STA MESA       AND TERMINATE
      JMP TERM0 
      SPC 1 
CON1  JSB EXEC     READ FIRST DIRECTORY ENTRY OF $FILS
      DEF *+7        TO CHECK FOR 
      DEF R14          ALREADY INITIALIZED
      DEF C2
      DEF DBUF
      DEF DELEN 
      DEF FILA
      DEF ZER 
      SPC 1 
      LDA DBUF     GET WD 0 OF FIRST ENTRY
      CPA FILA     ="$F"? 
      RSS          YES
      JMP CON2     NO. CONTINUE 
      LDA DBUF+1   GET WD 1 
      CPA FILA+1   ="IL"? 
      RSS          YES
      JMP CON2     NO. CONTINUE 
      LDA DBUF+2   GET WD 2 
      CPA FILA+2   ="S "? 
      RSS          YES
      JMP CON2     NO. CONTINUE 
      LDA DBUF+3   GET WD 3 
      CPA NSECT    = # OF SECTORS IN $FILS? 
      RSS          YES. ALREADY INITIALIZED 
      JMP CON2     NO. CONTINUE 
      SPC 1 
L0    JSB EXEC     PRINT "OK TO PURGE BASIC FILES?" 
      DEF *+5 
      DEF R2
      DEF C1
      DEF MES2
      DEF ML2 
      SPC 1 
      JSB EXEC     INPUT REPLY
      DEF *+5        ("YES" OR "NO")
      DEF R1
      DEF C1
      DEF INP 
      DEF DM3 
      SPC 1 
      LDA INP      GET FIRST CHARACTER OF REPLY 
      ALF,ALF 
      AND =B177 
      CPA Y        IS IT "Y"? 
      JMP CON2     YES. CONTINUE
      CPA N        NO. IS IT "N"? 
      JMP TERM1    YES. TERMINATE 
      JMP L0       NO. TRY AGAIN
      SPC 1 
CON2  JSB EXEC     WRITE INITIAL DIRECTORY
      DEF *+7      WD 0 = "$F"
      DEF R15      WD 1 = "IL"
      DEF C2       WD 2 = "S "
      DEF FILA     WD 3 = # OF SECTORS IN $FILS 
      DEF D129     WD 4 = 1 (INITIAL LAST SECTOR USED)
      DEF FILA     WD 5 = 1 (INITIAL INDEX OF FINAL DIR. ENTRY) 
      DEF ZER      WD 6,7= UNUSED 
      SPC 1 
      JMP TERM1    TERMINATE
      SPC 2 
FILA  ASC 3,$FILS 
NSECT NOP          # OF SECTORS IN $FILS
R1    OCT 1        LAST SECTOR USED 
      OCT 1        INDEX OF FINAL DIR. ENTRY
ZER   OCT 0 
      SUP 
      REP 122 
      OCT 0 
      SPC 2 
R18   DEC 18
R2    DEC 2 
C1    OCT 401 
MES0  ASC 8,$FILS UNDEFINED 
ML0   DEC -15 
R6    DEC 6 
MES1  ASC 8,$FILS TOO SMALL 
MS1   DEF MES1
R14   DEC 14
DBUF  BSS 8 
DELEN DEC 8 
MES2  ASC 12,OK TO PURGE BASIC FILES? 
ML2   DEC -24 
R15   DEC 15
C2    OCT 102 
D129  DEC 129 
INP   BSS 2 
DM3   DEC -3
Y     OCT 131 
N     OCT 116 
      END START 

