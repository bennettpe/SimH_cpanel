ASMB,R,B,L,X
      NAM BASIC,3 
      SPC 2 
      ENT SCHEQ,SCHIQ,LCDLP 
      ENT SCHIN,SCHLB 
      ENT GETCR,S14SC,ABCHK 
      ENT BLANK,MLINK,MLINB,INF 
      ENT CW0 
      ENT CW2,OUTCH 
      ENT ATERM,ABFLG 
      ENT PRISF 
      ENT #LDFG 
      SPC 2 
      COM PBPTR,NEG(26),.(52),MAIN,SPROG,SYMTB,ERRCT
      COM SBPTR,SBUFA,SSTAK,SYNTQ,SERRS(36),RERRS(50) 
      COM FERRS(15),WERRS(10),MAXCR,MSFLG,PFLAG,SFLAG 
      COM DFLAG,TEMP(3),CDFLG,ARYAD,GFLAG,SIGN,UFLAG
      COM PINTG(2),TABLE,LNGTH,SMBGN,SLENG,COUNT
      COM TBLPT,TSPTR,SBT0
      COM EXP,MANT1,MANT2,EXPON,DPFLG,NT0,ENOUF 
      COM LNAME,CHRCT,OCTMP,SPTR,FILTB,NSPTR,INFST
      COM DCLC1,DEST,DCLC2,STEND,DFILT,FILPT,COMSN
      COM MVEND 
      COM STYPE,USESN,FILCT,.LNUM 
      COM PRGCT,STCT1,STCT2,NUMPT,STCT3,FLINK 
      COM STMP1,STMP2,VLFLG,VALTB,FCORE,COMPT,VTMP1 
      COM VTMP2,VTMP3,VTMP4,VTMP5,IFSS
      COM CU1,APTR,DCFLG,TEMP2,LT1,LT2,FCNTR,RTNST
      COM ASINP,RTRNQ,FORQ,TEMP1,FILE#
      COM INTMP,TMPST,TPRME,TNULL,PS1,FSCHA,TEMP5 
      COM PRIST,FBASE,RQ3,EOL,GTMP,FORST,EFN0,FVT 
      COM EFN1,EFN2,EFN3,MCNT,INITF,IFCNT,OPDST 
      COM FFLG,TEMP4,EPRFL,NCH,TEMP6,STRLN,ATIM,LT5 
      COM ASTYP,ASBFP,ORDNO,ATMP(2),RETCD 
      COM DCCNT,NXTDT,VL0,RCRD#,PMASK,ITEMP 
      COM OPTRQ,EST3,EST2,EST1,TT1,TT2,BINO1
      COM BINO2,TT3,TT4,CP0,CP1,PS0,MPT,NQT,RT0 
      COM RT1,TRFCH,TRS0,A1,A2,C1,EOPF,NUMBF(6),FD0 
      COM OT1,OT4,OT3,LBTMP,REPCT,RNDX1,RNDX2 
      COM X2TMP(2),CTMP,DTMP(2),BTMP(2),XTEMP(2),YTEMP(2) 
      COM UTEMP(2),LT0,LT8,LT3,LT6,LT7,LT4,DIGCT
      COM NMPTR,NMTMP,FFLAG,DGCNT 
      COM IFSTR,ZZ(23),IHB,ZZZ(43)
      COM SOURC,TAP0,TAP1 
      COM STIME(5)
      COM LTEMP(16),LTYP1,LTYP2,LCHCR,MOVES,MOVED 
      COM ERSEC(77),DRFLG,DIREC(256),DTEMP,DCNT 
      COM DTRAK,DSECT,FLSTS(4)
      SPC 2 
      SPC 2 
      EXT STDAT 
      EXT EXEC,SYNTX,CMPLE,LISTB
      EXT PBUFF,LWAUS,LW200 
      EXT EOST
      EXT SAIR
      EXT DLOOK 
      EXT FSCHB 
      EXT PUNCH,XPNCH 
      EXT TAPER 
      EXT OUTST,LTYPR 
      SPC 2 
HB    DEF ERSEC+5 
A     EQU 0 
B     EQU 1 
B177  OCT 177 
BKS   OCT 160023
MNEG  OCT 100000
GTC   OCT 40000 
RBP   OCT 140000
SBP   OCT 120000
ALI   OCT 160014
B377  OCT 377 
IWT   OCT 160003
UNR   OCT 160002
UIR   OCT 160001
STE   OCT 20000 
TAPEF EQU .+8 
TERR  EQU .+1 
.140  OCT 140 
.72B  OCT 72
FLSTA DEF FLSTS 
      SPC 2 
SCHIQ NOP          INPUT A LINE 
SCHIN EQU SCHIQ 
      JSB S14SC 
      JMP SCHIQ,I 
      ISZ SCHIQ 
      JMP SCHIQ,I 
      SPC 2 
LCDLP NOP          DUMMY FILE "LAST-CHANGE- 
      JMP LCDLP,I       DATE" UPDATER 
      SPC 2 
SCHLB NOP          DUMMY LIBRARY CALL PROCESSOR 
      LDA SCHLB,I 
      ISZ SCHLB 
      STA SEG 
      JSB EXEC
      DEF *+3 
      DEF .+8 
SEG   NOP 
      JMP SCHLB,I 
      SPC 5 
START LDA B,I 
      SZA,RSS 
      JMP STRT
      STA INCWA 
      IOR =B100 
      STA CW0 
      STA CW2 
      IOR =B400 
      STA INCW
      IOR =B20000 
      STA INCWR 
STRT  LDA PINA
      RAL 
      STA PINA
      LDA HB        GET HOLD BUFFER ADDR. 
      STA IHB         AND SAVE
      LDA BEND     SET INP. BUF. END ADDD.
      RAL 
      STA BEND
      LDA LBBEG    SET LP BUFFER ADDR.
      RAL 
      INA 
      STA LBBEG 
      LDA LBEND 
      RAL 
      STA LBEND 
      LDA FLSTA 
      STA DFILT 
      LDB .ADM     SET CONSTANTS IN COMMON
      LDA =D-26 
L1    STA B,I 
      INA 
      INB 
      CPB .END
      RSS 
      JMP L1
      JSB EXEC     GET START TIME 
      DEF *+3 
      DEF .+11
      DEF STIME 
      JSB STDAT 
      LDA LW200    NEGATE LW200 
      CMA,INA 
      STA LW200 
      LDA MANE     SET POINTER TO TTY TABLE 
      STA MAIN
      LDA PBUFF    SET USER PROGRAM POINTER 
      STA SPROG 
      STA PBPTR 
      LDA FSCHB 
      STA FSCHA 
      LDA SAIR     SET ERROR LINKS
      LDB SA
L2    STA B,I 
      INB 
      CPB SAEND 
      RSS 
      JMP L2
      CLA          INITIALIZE FLAGS 
      STA MSFLG 
      STA PFLAG 
      STA SFLAG 
      STA UFLAG 
      STA DFLAG 
      STA CDFLG 
      STA ARYAD 
      STA GFLAG 
      STA CHRCT 
      STA PRIST 
      STA DRFLG 
      OTA 1 
      LDA DA
      STA DCLC1 
      STA DCLC2 
      LDA NAMEA    SET NAME POINTER 
      STA LNAME 
      JSB DLOOK    INITIALIZE DIRECTORY 
      SPC 1 
      CLA           DISABLE 
      LDB B111        COLON 
      JSB EXEC          CHECK 
      DEF *+2 
      DEF .-19
      SPC 1 
      JSB EXEC      PRINT "READY" 
      DEF *+5 
      DEF .+2 
      DEF CW0 
      DEF RDY 
      DEF .-8 
      SPC 1 
      JMP RESET 
      SPC 2 
B111  OCT 111 
CW0   OCT 101 
.ADM  DEF .-26
.END  DEF .+52
SA    DEF SYNTQ+1 
SAEND DEF MAXCR 
DA    DEF DCLC2 
RDY   ASC 2,READ
      OCT 54415 
      OCT 5012
      SPC 5 
GSTAT NOP 
      JSB EXEC     GET TTY STATUS 
      DEF *+5 
      DEF .+13
      DEF INCWA 
      DEF STATS 
      DEF TLOG
      JMP GSTAT,I 
      SPC 2 
XINST JMP 225B,I
CLTTY NOP          CLEAR PENDING INPUT ON TTY 
      LDA XINST 
      JSB EXEC
      DEF *+2 
      DEF .-22
      LDA 121B
      ADA INCWA 
      ADA .-1 
      LDA A,I 
      ADA .-1 
      MPY .+17
      ADA 117B
      ADA .+2 
      LDA A,I 
      AND =B77
      IOR OTAB
      STA OTAA
      AND =B77
      IOR STFB
      STA STFA
      CLA,INA 
OTAA  OTA 0        FORCE INTERRUPT ON TTY 
STFA  STF 0 
      STF 0 
      STC 5 
      JMP CLTTY,I 
OTAB  OTA 0 
STFB  STF 0 
      SPC 2 
RESET CLA 
      STA ABFLG 
      JSB LININ 
      JMP CR
      SPC 2 
AIDPT NOP 
AIDTL NOP 
MD132 DEC -132
LINT  NOP 
INCW  OCT 501 
INCWR OCT 20501 
INCWA OCT 1 
LININ NOP          READ A LINE FROM TTY 
      STA LINT        (A#0 => "ENTER")
      LDA PINA     RESET INPUT BUFFER POINTER 
      STA PNTR
      RAR 
      CLB          CLEAR BUFFER 
L9    STB A,I 
      INA 
      CPA BEND2 
      RSS 
      JMP L9
      LDA #LDFG      GET "LOAD" FLAG
      SZA,RSS        "LOAD"?
      JMP LINI7      NO 
      JSB EXEC       YES. READ RECORD FROM LOAD DEVICE
      DEF *+5 
      DEF .+1 
      DEF #LDFG 
AIDBA DEF LPBUF 
      DEF MD132 
      SPC 1 
      SZB            EOF? 
      JMP LINI6      NO 
      STB #LDFG      YES. CLEAR "LOAD" FLAG 
      JMP SCH31 
      SPC 1 
LINI6 STB AIDTL      SAVE RECORD LENGTH 
      LDA AIDBA      GET BUFFER ADDR. 
      RAL            FORM BYTE ADDR.
      STA AIDPT        AND SAVE 
      JMP GCHR
      SPC 1 
LINI7 LDA INCW
      LDB LINT
      SZB          "ENTER"? 
      LDA INCWR    YES. 
      STA CW1      NO. SET INPUT CONTROL WORD 
GCHR  LDA #LDFG      GET "LOAD" FLAG
      SZA,RSS        "LOAD"?
      JMP GCHR7      NO 
      LDA AIDTL      YES. ALL OF RECORD 
      SZA            PROCESSED? 
      JMP *+4 
      LDA .+15B      YES. FAKE CR 
      STA TEMP
      JMP L85 
      SPC 1 
      LDB AIDPT      GET BUF. POINTER 
      CLE,ERB        FORM FLAG,ADDR.
      LDA B,I        GET WORD 
      SEZ,RSS        RIGHT/LEFT?
      ALF,ALF        LEFT 
      ELB            BUMP POINTER 
      INB 
      STB AIDPT 
      LDB AIDTL      DECR. COUNTER
      ADB .-1 
      STB AIDTL 
      JMP L81 
      SPC 1 
GCHR7 JSB EXEC       READ A CHARACTER 
      DEF *+5 
      DEF .+1 
      DEF CW1 
      DEF BIN 
      DEF .-1 
      LDA LINT     "ENTER"? 
      SZA,RSS 
      JMP L8       NO 
L7    JSB GSTAT    YES.GET TTY STATS
      SPC 1 
      JSB EXEC     NO. GET CURRENT TIME 
      DEF *+3 
      DEF .+11
      DEF TIME1 
      SPC 1 
      CLA 
      LDB TIME1+3  GET CURRENT HOURS
      CPB TIME0+3  = INITIAL HOURS? 
      RSS          YES. RELATIVE CUR. MIN. =0 
      LDA =D60     NO.  RELATIVE CUR. MIN. = 60 
      ADA TIME1+2  FORM REL. CUR. MINS. 
      MPY =D60          MAKE SECONDS
      ADA TIME1+1  FORM CUR. REL. SECS. 
      ADA TIME0+4  GET CUR. TIME - INITIAL TIME 
      LDB STATS 
      SSB,RSS 
      JMP L75 
      ADA S14T     ADD ALLOWABLE WAIT 
      SSA          TOO LONG?
      JMP L7       NO. GO CHECK TTY AGAIN 
      STA LPTMP 
      JSB CLTTY 
      ISZ LININ    YES. 
      JMP LININ,I  TIMEOUT RETURN 
      SPC 1 
L75   STA RTIME 
L8    LDA BIN      GET CHARACTER
      ALF,ALF 
L81   AND B177
      STA TEMP     SAVE 
      SZA,RSS      =NULL? 
      JMP GCHR     YES. IGNORE IT 
      CPA .+23B    IS IT X-OFF? 
      JMP GCHR     YES. IGNORE IT 
      CPA .+12B      IS IT A LINEFEED?
      JMP GCHR       YES. IGNORE IT.
      CPA .+30B    IS IT CNTL/X?
      JMP DELL     YES. DELETE LINE 
      CPA .+32B    IS IT CNTL/Z?
      JMP ETAPE    YES. END-OF-TAPE 
      CPA .+20B    IS IT CNTL/P?
      LDA .72B     YES. MAKE IT =":"
      CPA =B137    IS IT A BACKARROW? 
      JMP DELC     YES. DELETE A CHAR.
      CPA .+21B    NO. IS IT CNTL/Q 
      JMP CNTLQ    YES. 
      CPA .+27B    NO. IS IT CNTL/W?
      JMP CNTLW    YES
L85   LDB PNTR     NO. PLACE CHAR. IN BUFFER
      CPB BEND     END OF BUFFER? 
      JMP L87       YES. IGNORE CHAR. UNLESS CR 
L86   CLE,ERB       NO. PLACE CHAR. IN BUF. 
      SEZ,RSS 
      ALF,ALF 
      IOR B,I 
      STA B,I 
      ELB 
      INB          INCR. CHAR. POINTER
      STB PNTR
      LDA TEMP     GET CHAR.
      CPA .+15B    CARRIAGE RETURN? 
      RSS          YES
      JMP GCHR     NO. GET ANOTHER CHAR.
      STA LPTMP 
      LDA PINA     RESET POINTER
      STA PNTR
      JMP LININ,I  RETURN 
      SPC 1 
L87   CPA .+15B     CR? 
      JMP L86       YES 
      JMP GCHR      NO
      SPC 1 
CNTLQ LDB PRIST    GET LP INDICATOR 
      SZB,RSS      LP ASSIGNED? 
      JMP GCHR     NO 
      STB PRISF    YES. SET IGNORE LP FLAG
      JMP GCHR
      SPC 2 
CNTLW CLA          CLEAR IGNORE LP FLAG 
      STA PRISF 
      JMP GCHR
PRISF OCT 0 
      SPC 2 
      SPC 2 
DELL  JSB EXEC     PRINT BACKSLASH
      DEF *+5 
      DEF .+2 
      DEF INCWA 
      DEF DCON
      DEF .-1 
      JMP LININ+2 
      SPC 2 
ETAPE LDA MAIN,I
      AND TAPEF 
      SZA,RSS      TAPE MODE? 
      JMP L85      NO. ACCEPT CHAR. 
      LDA TAPEF    YES
      CMA 
      AND MAIN,I   CLEAR TAPE MODE FLAG BIT 
      STA MAIN,I
      LDA =B500    RESET TTY INPUT
      IOR INCWA 
      STA INCW
      LDA MAIN,I
      AND TERR
      SZA           TAPE ERRORS?
      JMP TAPER    YES. 
      LDA .+12B    NO.
      JSB OUTCH    PRINT LF 
      JMP LININ+2 
      SPC 2 
DELC  LDB PNTR     GET BUFFER POINTER 
      CPB PINA     AT START OF BUF.?
      JMP GCHR     YES. CAN'T DELETE ANYMORE
      ADB .-1      NO. DECREMENT CHAR. POINTER
      STB PNTR
      LDA B377     CLEAR CHAR. IN BUFFER
      CLE,ERB 
      SEZ 
      ALF,ALF 
      AND B,I 
      STA B,I 
      JMP GCHR     GET ANOTHER CHAR.
      SPC 2 
CR    CLA          SET FLAG FOR COMMAND INTERP. 
      STA SCHP
      JSB SCOM     EXAMINE LINE 
      JMP SCH30    EMPTY LINE 
      JMP SYNTX    FIRST CHAR. A DIGIT
      JMP EHERR    INVALID COMMAND
      JMP SCH11    COMMAND IS OK
      SPC 2 
SCH30 LDA MAIN,I
      AND TAPEF 
      SZA          TAPE MODE? 
      JMP SCHEQ    YES
SCH31 LDA .+12B    NO. OUTPUT A LINEFEED. 
      JSB OUTCH 
      JMP SCHEQ    TRY AGAIN
      SPC 2 
EHERR JSB EXEC     PRINT "???"
      DEF *+5 
      DEF .+2 
      DEF INCWA 
      DEF QM
      DEF .-4 
      JMP SCHEQ    TRY AGAIN
      SPC 2 
MCOM6 DEF COM6-1
MCOM3 DEF COM3-1
SCH11 LDA #LDFG     GET LOAD FLAG 
      SZA,RSS       LOAD? 
      JMP SC115     NO
      CLA           YES 
      STA #LDFG 
SC115 CMB,INB 
      ADB MCOM3 
      SSB 
      JMP SCH12 
      CMB,INB 
      ADB MCOM6 
      ADB MNEG
      JMP B,I      EXECUTE COMMAND
      SPC 2 
LPAD  DEF * 
      OCT 5114     LF-L 
      ASC 3,P FREE
      OCT 6412     CR-LF
SCHEQ LDA PRIST    GET LP INDICATOR 
      CLB           CLEAR "LIST"
      STB LFG2        FLAG
      SZA,RSS      ASSIGNED?
      JMP RESET    NO 
      LDA LFLAG    YES. GET LPR FLAG
      SZA          LPR BEING TERMINATED?
      JMP SCHQ2    YES
      STA PRIST     RELEASE LP
      STA PRISF 
      LDA LPAD       AND PRINT MESSAGE
      STA LT1 
      CLB 
      LDA .-5 
      JSB OUTST 
      JMP RESET 
SCHQ2 CLA          RESET LPR FLAG 
      STA LFLAG 
      JMP RESET 
      SPC 2 
SCH12 CMB 
      STB SCOM
      ADB SCOM
      ADB SCOM
      ADB MCOM6 
      INB 
      STB SEGAD 
      STB LCHCR     RESET LIB. LAST CHAR. FLAG
      SPC 1 
      JSB SCHLB 
SEGAD NOP 
      SPC 2 
OUTCH NOP          OUTPUT A CHAR. TO TTY
      LDB ABFLG 
      SZB,RSS      ARE ABORTS ALLOWED?
      JMP OUTC2    NO 
      LIB 1 
      SSB 
      JMP ATERM 
OUTC2 LDB PRISF    GET IGNORE LP FLAG 
      SZB          IGNORE LP? 
      JMP OUTTY    YES. 
      LDB MAIN     IS LP
      INB            ASSIGNED?
      CPB PRIST 
      JMP LPOUT    YES
*                                      TTY OUTPUT AFTER "LPR" 
OUTTY ALF,ALF 
      STA TEMP
      JSB EXEC
      DEF *+5 
      DEF .+2 
      DEF CW2 
      DEF TEMP
      DEF .-1 
      JMP OUTCH,I 
LPOUT LDB LPTMP    IS LP OUTPUT TEMPORARILY 
      SSB            SUSPENDED? 
      JMP OUTTY    YES. PRINT ON TTY
      CPA .+23B    NO.IS CHAR. "X-OFF"? 
      JMP LPOT0    YES
      CPA .+15B    NO. IS IT CR?
      JMP LPCR     YES
      CPA .+12B    NO. IS IT LF?
      JMP LPLF     YES
      CPA .+14B     NO. IS IT TOF?
      JMP LPTOF     YES 
      STA B        NO. SAVE IT IN B 
      AND .140     IS IT A
      SZA            CONTROL
      CPA .140         CHARACTER? 
      JMP OUTCH,I 
      LDA B        NO. RESTORE CHAR.
LPOT  STA LPTMP    SAVE FOR LP SUSPEND CHECK
      LDB LBPNT    GET LP BUFFER POINTER
      CPB LBEND    BUFFER FULL? 
      JMP LPEND    YES
      CLE,ERB      NO. SHIFT CAR. BIT TO E
      SEZ,RSS      RIGHT OR LEFT HALF?
      ALF,ALF      LEFT 
      IOR B,I      RIGHT.MERGE PREVIOUS CHAR. 
      STA B,I      PUT IN BUFFFER 
      ELB 
      INB          INCR. LP BUFFER POINTER
      STB LBPNT 
      ISZ LPCNT    INCR. LP CHAR. COUNT 
      JMP OUTCH,I 
      SPC 1 
LPEND LDA SSPAC    SET FOR SINGLE SPACE 
      IOR LPBUF 
      STA LPBUF 
      LDA LPCNT    SET CHAR. COUNT FOR OUTPUT 
      CMA 
      STA LPCNT 
      JSB EXEC
      DEF *+5 
      DEF .+2 
      DEF .+6 
      DEF LPBUF 
      DEF LPCNT 
      CLA          RESET CHAR. COUNT
      STA LPCNT 
      LDB LBBEG    RESET LP BUFFER POINTER
      STB LBPNT 
      CLE,ERB 
OUTL0 STA B,I      CLEAR LP BUFFER
      INB 
      CPB LBEN2    DONE?
      RSS          YES
      JMP OUTL0    NO 
      LDA LPTMP    YES. GET OVERFLOW FLAG 
      SSA,RSS      BUFFER OVERFLOW? 
      JMP LPOT     YES. PUT IN BUFFER 
      CLA          NO.RESET FLAG
      STA LPTMP 
      JMP OUTCH,I 
      SPC 1 
LPCR  LDA LFG2      GET LIST FLAG 
      SZA           LIST? 
      JMP OUTCH,I   YES. IGNORE CR
      CCA           NO. SET NO
      STA LPTMP       OVERFLOW
      LDA NSPAC    GET "SUPPRESS SPACING" CHAR. 
      JMP LPEND+1 
      SPC 1 
LPLF  CCA          SET NO OVERFLOW
      STA LPTMP 
      JMP LPEND 
      SPC 1 
LPTOF JSB TOFRM     TP OF FORM
      JMP OUTCH,I 
      SPC 1 
LPOT0 CPA LPTMP    WAS LAST CHAR. ALSO "X/OFF"
      JMP LPOT1    YES. 
      STA LPTMP    NO.
      JMP OUTCH,I 
LPOT1 CCA          SET TEMP. LP OUTPUT
      STA LPTMP      SUSPEND FLAG 
      JMP OUTCH,I 
      SPC 2 
SSPAC OCT 20000 
NSPAC OCT 25000 
LPTMP OCT 0 
LPBUF BSS 66
LBEND DEF * 
LBBEG DEF LPBUF 
LBEN2 DEF LBEND 
LBPNT NOP 
CW2   OCT 101 
LPCNT NOP 
      SPC 2 
#SCR  LDA PBUFF    SCRATCH
      STA PBPTR      RESET USER PROG. POINTER 
      JMP SCH31 
      SPC 2 
LIST  CLA,INA 
      STA ABFLG 
      STA LFG2     SET LIST FLAG
      JMP LISTB 
LFG2  NOP 
      SPC 1 
CMPLB JMP CMPLE    RUN
      SPC 2 
PUNCB CLA,INA 
      STA ABFLG 
      JMP PUNCH 
      SPC 1 
XPNCB CLA,INA 
      STA ABFLG 
      JMP XPNCH 
      SPC 2 
#TAP  LDA =B105 
      STA INCW
      LDA MAIN,I
      IOR TAPEF 
      STA MAIN,I
      JMP SCH31 
      SPC 2 
#KEY  JMP SCH31 
      SPC 2 
TOFRM NOP 
      JSB EXEC     OUTPUT TOF 
      DEF *+4 
      DEF .+3 
      DEF B1106 
      DEF .-1 
      JMP TOFRM,I 
      SPC 2 
B1106 OCT 1106
#LPR  JSB TOFRM    TOP OF FORM TO LPR 
      LDA .+12B 
      JSB OUTCH 
      LDA MAIN
      INA 
      STA PRIST 
      STA LFLAG    SET LPR FLAG 
      CLB 
      STB LPCNT    CLEAR LP CHAR. COUNT 
      STB PRISF    CLEAR IGNORE LP FLAG 
      LDA LBBEG    RESET LP BUFFER POINTER
      STA LBPNT 
      CLE,ERA 
LPR0  STB A,I      CLEAR LP BUFFER
      INA 
      CPA LBEN2    DONE?
      JMP SCHEQ     YEC 
      JMP LPR0     NO 
      SPC 2 
#LDFG OCT 0 
#LOD  JSB SCHAR    GET 1ST DIGIT OF LU #
      JMP #LOD0    CR 
      AND .+17B    CONVERT TO BINARY
      STA #LDFG      AND SAVE 
      JSB SCHAR    GET 2ND DIGIT
      JMP #LOD1   CR. ONLY ONE DIGIT
      AND .+17B    CONVERT TO BINARY
      STA TLOG       AND SAVE 
      LDA #LDFG    GET 1ST DIGIT
      MPY .+10     MULT. BY 10
      ADA TLOG     ADD 2ND DIGIT
      RSS 
#LOD0 LDA .+5      DEFAULT TO LU #5 
      STA #LDFG      AND SAVE 
#LOD1 LDA #LDFG    GET LU # 
      IOR =B700    SET EOT
      STA TLOG       STATUS FOR PT
      SPC 1 
      JSB EXEC
      DEF *+3 
      DEF .+3 
      DEF TLOG
      SPC 1 
      JMP SCHEQ 
      SPC 2 
LFLAG OCT 0 
STATS NOP 
TLOG  NOP 
PINA  DEF BUF 
BUF   BSS 50
BEND  DEF *-1 
BEND2 DEF BEND
PNTR  NOP 
CW1   OCT 501 
BIN   NOP 
SCNT  NOP 
M60B  OCT -60 
M41B  OCT -41 
M133B OCT -133
M96   OCT -140
DCON  OCT 56000 
SCHP  NOP 
QM    OCT 5077
      ASC 1,??
M32   DEC -32 
B140  OCT 140 
SCOM1 DEF COM1
SCOM5 DEF COM5-1
COM1  OCT 44121    SCRATCH
      OCT 46017    TAPE 
      OCT 24230    KEY
      OCT 26761    LPRINTER 
      OCT 26700     LOAD
COM2  OCT 43215    RUN
      OCT 26422    LIST 
      OCT 37215     PUNCH 
      OCT 56764     XPUNCH
COM3  OCT 44025    SAVE 
      OCT 14223    GET
      OCT 3404     BYE
      OCT 32014    NAME 
      OCT 5100      CSAVE 
      OCT 757       APPEND
      OCT 42215     RENUMBER
      OCT 26215     LENGTH
      OCT 6213      DELETE
      OCT 46414    TIME 
      OCT 24413    KILL 
      OCT 4023     CATALOG
      OCT 34744    OPEN 
COM5  DEF #SCR
      DEF #TAP
      DEF #KEY
      DEF #LPR
      DEF #LOD
COM4  EQU COM5
      DEF CMPLB 
      DEF LIST
      DEF PUNCB 
      DEF XPNCB 
COM6  ASC 3,SAVE
      ASC 3,GETB
      ASC 3,BYE 
      ASC 3,NAME
      ASC 3,CSAVE 
      ASC 3,APPEN 
      ASC 3,RENUM 
      ASC 3,LENGT 
      ASC 3,DELET 
      ASC 3,TIME
      ASC 3,KILL
      ASC 3,CATAL 
      ASC 3,OPEN
      SPC 2 
* SCOM SCANS A COMMAND INPUT BUFFER TO DETERMINE WHAT 
* THE COMMAND IS. THE CALLING SEQUENCE TO SCOM IS:
* 
*     JSB SCOM
*     <RETURN IF BLANK LINE>
*     <RETURN IF FIRST CHARACTER A DIGIT> 
*     <RETURN IF NO LEGAL COMMAND>
*     <RETURN IF COMMAND FOUNE--B=COMMAND ADDRESS>
* 
* SCOM ASSUMES THAT BEFORE IT IS CALLED, SBHED AND
* SCHL ARE INITIALIZED AS REQUIRED BY SCHAR,, AND 
* SCHP=0 FOR NORMAL USERS AND OCT40 FOR CONSOLE.
      SPC 1 
SCOM  NOP           ENTRY POINT.
      LDB .-3       INITIALIZE CHAR. COUNT TO -3
      STB SCNT
      JSB SCHAR     GET A CHARACTER.
      JMP SCOM,I    NOT THERE-BLANK LINE. 
      ISZ SCOM      BUMP SCOM TO POINT AT DIGIT RET.
      ADA M60B      TEST FOR FIRST CHAR A DIGIT.
      SSA           AUTOMATIC FAILURE IF
      ISZ SCOM       < ASC0 
      ADA .-10
      SSA,RSS       IF <=ASC9, RETURN TO P+2 IF A 
      JMP SCOME       DIGIT, P+3 IF NOT 
      LDA BKS 
      JSB S14SC 
      JMP SCOM,I
SCOME ISZ SCOM
      ADA M41B      TEST FOR LETTER.
SCOMB SSA,RSS 
      JMP SCOM,I    NOT ALETTER.
      ADA .+32B 
      SSA 
      JMP SCOM,I    NOT A LETTER. 
      IOR SCHP
      ISZ SCNT      TEST FOR 3 LETTERS IN.
      ALF,SLA,RAL   NO--ROTATE FOR NEXT ONE.
      JMP SCOMA     YES--GO TO SEARCH TABLE.
      STA SCHP
      JSB SCHAR     GET NEXT CHARACTER
      JMP SCOM,I    FAIL. 
      ADA M133B 
      JMP SCOMB 
* SEARCH COMMAND TABLE *
SCOMA LDB SCOM1     B=>TABLE
SCOMD CPA 1,I 
      JMP SCOMC     COMMAND FOUND 
      CPB SCOM5 
      JMP SCOM,I    ILLEGAL COMMAND 
      INB 
      JMP SCOMD 
SCOMC ISZ SCOM      RETURN O.K. 
      STB SCHP      SAVE ADDRESS OF COMMAND.
      JSB SCHAR     SCAN
      JMP *+4        FOR CR 
      CPA .+55B     OR DASH.
      RSS 
      JMP *-4 
      LDB SCHP
      JMP SCOM,I
* SCHAR FETCHES THE NEXT CHARACTER FROM A BUFFER. BUFFER
* POINTERS FOR SCHAR ARE INITIALIZED AS FOLLOWS:
*     SBHED=> FIRST CHARACTER IF TTY35; =0 OTHERWISE
*     SCHL,I=> END OF TTY35 BUFFER
* 
* SCHAR CALLING SEQUENCE: 
* 
*     JSB SCHAR 
*     RETURN HERE IF CR 
*     RETURN HERE IF ANY OTHER CHARACTER
      SPC 1 
SCHAR NOP 
SCHR1 LDA GTC 
      JSB S14SC 
      CPA .+40B     BLANK?
      JMP SCHR1    YES. IGNORE IT 
      CPA .+15B     NO, CARRIAGE RETURN?
      JMP SCHR2     YES 
      ADA M96       CHECK FOR CODES 
      SSA,RSS        GREATER THAN 140.
      ADA M32       CODE IS LOWER CASE, MAKE UPPER. 
      ADA B140      RESTORE ASCII CODES.
      ISZ SCHAR     NO--EXIT
      JMP SCHAR,I     TO (P+2)
SCHR2 EQU * 
      LDA BKS 
      JSB S14SC 
      JMP SCHAR,I 
      SPC 2 
GETCR NOP          GET A CHAR. FROM TTY 
#GTCR LDA GTC 
      JSB S14SC 
      CPA BLANK    BLANK? 
      JMP #GTCR    YES. IGNORE IT 
      CPA .+15B    NO. CR?
      JMP GETCR,I  YES. RETURN TO P+1 
      LDB A        B=A
      ISZ GETCR 
      JMP GETCR,I  RET. TO P+2
      SPC 5 
ABCHK NOP          DUMMY ABORT CHECK
      LDB MAIN
      JMP ABCHK,I 
      SPC 2 
ABFLG OCT 0 
      SPC 1 
ATERM LDA B 
      AND INF 
      OTA 1 
      LDA ATA 
      STA LT1 
      CLB 
      STB ABFLG 
      LDA .-4 
      JSB OUTST 
      LDA MAIN,I
      AND TERR
      SZA 
      JMP TAPER 
      JMP SCHEQ 
ATA   DEF * 
      OCT 6412
      ASC 2,STOP
      OCT 6412
      SPC 5 
S14T  NOP 
S14SC NOP          DUMMY FRONT-END ROUTINE
      STA S14T
      AND =B170000
      CPA STE 
      JMP XSTE
      LDA S14T
      AND =B160037
      CPA ALI 
      JMP XALI
      CPA SBP 
      JMP XSBP
      CPA RBP 
      JMP XRBP
      CPA BKS 
      JMP XBKS
      CPA GTC 
      JMP XGTC
      CPA IWT 
      JMP XIWT
      CPA UNR 
      JMP XUNR
      CPA UIR 
      JMP XUIR
      HLT 
      SPC 2 
XGTC  LDB PNTR     GET A CHAR. FROM INP. BUF. 
      CLE,ERB 
      LDA 1,I 
      ELB,SLB 
      RSS 
      ALF,ALF 
      AND B377
      INB 
      STB PNTR
      JMP S14SC,I 
      SPC 2 
XBKS  CCB          BACKSPACE IN BUFFER
      ADB PNTR
      STB PNTR
      JMP S14SC,I 
      SPC 2 
XALI  JMP S14SC,I  ALLOW INPUT
      SPC 2 
XSBP  LDA PNTR     SAVE CURRENT BUF. POINTER
      STA TEMPS 
      JMP S14SC,I 
      SPC 2 
XRBP  LDA TEMPS    RESTORE BUF. POINTER 
      STA PNTR
      JMP S14SC,I 
TEMPS NOP 
      SPC 2 
XUNR  JMP S14SC,I 
      SPC 2 
XIWT  CLA          INPUT WAIT 
      JSB LININ 
      JMP S14SC,I 
      SPC 2 
XUIR  JMP S14SC,I  USER IS RUNNING
      SPC 2 
XSTE  LDA S14T     START TIMING ENTER 
      AND B377
      CMA,INA 
      STA S14T     SAVE ALLOWABLE WAIT
      JSB EXEC     GET INITIAL TIME 
      DEF *+3 
      DEF .+11
      DEF TIME0 
      SPC 1 
      LDA TIME0+2  FORM REL. INITIAL TIME 
      MPY =D60
      ADA TIME0+1 
      CMA,INA 
      STA TIME0+4       & SAVE
      CLA,INA 
      JSB LININ    ENTER LINE 
      JMP S14SC,I  OK 
      ISZ S14SC    TIMEOUT RETURN 
      JMP S14SC,I 
      SPC 2 
TIME0 BSS 5 
TIME1 BSS 5 
?LINK EQU 15B 
BLANK OCT 40
MANE  DEF TTY 
MLINK DEF *+1 
      DEF TTY+?LINK 
MLINB EQU MLINK+1 
INF   OCT 77777 
TTY   OCT 0 
NAME  EQU TTY+5 
RTIME EQU TTY+15
      REP 18
      OCT 0 
NAMEA DEF NAME-1
      END START 
