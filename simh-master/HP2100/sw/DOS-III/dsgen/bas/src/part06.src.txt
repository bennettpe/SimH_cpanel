ASMB,R,B,L,X
      NAM ERR9,7
      SPC 2 
      ENT SAIR,SERR7,ERSTS,SERRB
      ENT SERR,WERRA,FERRA
      SPC 2 
      COM PBPTR,NEG(26),.(52),MAIN,SPROG,SYMTB,ERRCT
      COM SBPTR,SBUFA,SSTAK,SYNTQ,SERRS(36),RERRS(50) 
      COM FERRS(15),WERRS(10),MAXCR,MSFLG,PFLAG,SFLAG 
      COM DFLAG,TEMP(3),CDFLG,ARYAD,GFLAG,SIGN,UFLAG
      COM PINTG(2),TABLE,LNGTH,SMBGN,SLENG,COUNT
      COM TBLPT,TSPTR,SBT0
      COM EXP,MANT1,MANT2,EXPON,DPFLG,NT0,ENOUF 
      COM LNAME,CHRCT,OCTMP,SPTR,FILTB,NSPTR,INFST
      COM DCLC1,DEST,DCLC2,STEND,DFILT,FILPT,COMSN
      COM MVEND 
      COM STYPE,USESN,FILCT,.LNUM 
      COM PRGCT,STCT1,STCT2,NUMPT,STCT3,FLINK 
      COM STMP1,STMP2,VLFLG,VALTB,FCORE,COMPT,VTMP1 
      COM VTMP2,VTMP3,VTMP4,VTMP5,IFSS
      COM CU1,APTR,DCFLG,TEMP2,LT1,LT2,FCNTR,RTNST
      COM ASINP,RTRNQ,FORQ,TEMP1,FILE#
      COM INTMP,TMPST,TPRME,TNULL,PS1,FSCHA,TEMP5 
      COM PRIST,FBASE,RQ3,EOL,GTMP,FORST,EFN0,FVT 
      COM EFN1,EFN2,EFN3,MCNT,INITF,IFCNT,OPDST 
      COM FFLG,TEMP4,EPRFL,NCH,TEMP6,STRLN,ATIM,LT5 
      COM ASTYP,ASBFP,ORDNO,ATMP(2),RETCD 
      COM DCCNT,NXTDT,VL0,RCRD#,PMASK,ITEMP 
      COM OPTRQ,EST3,EST2,EST1,TT1,TT2,BINO1
      COM BINO2,TT3,TT4,CP0,CP1,PS0,MPT,NQT,RT0 
      COM RT1,TRFCH,TRS0,A1,A2,C1,EOPF,NUMBF(6),FD0 
      COM OT1,OT4,OT3,LBTMP,REPCT,RNDX1,RNDX2 
      COM X2TMP(2),CTMP,DTMP(2),BTMP(2),XTEMP(2),YTEMP(2) 
      COM UTEMP(2),LT0,LT8,LT3,LT6,LT7,LT4,DIGCT
      COM NMPTR,NMTMP,FFLAG,DGCNT 
      COM IFSTR,ZZ(67)
      COM SOURC,TAP0,TAP1 
      COM STIME(5)
      SPC 2 
      SPC 2 
      EXT OUTST,SCHIQ,GETCR,EXEC,OUTCR,OUTIN
      EXT OUTCL,FRMAT,LCDLP,SCHEQ,ABCHK,EDABR,S14SC 
      EXT BCKSP,ACCST 
      EXT #LDFG 
      SPC 2 
A     EQU 0 
B     EQU 1 
TOOP  OCT 76000 
INF   OCT 77777 
IWT   OCT 160003
B377  OCT 377 
UNR   OCT 160002
B2000 OCT 2000
B1000 OCT 1000
SAIR  DEF SERR
PGMSK EQU TOOP
TEMPZ NOP 
ADMSK OCT 1777
RERRA ABS SERRS-RERRS 
SERRA DEF SERRS 
TAPEF EQU .+8 
FERRA ABS RERRS-FERRS 
WERRA ABS FERRS-WERRS 
UNABT EQU .+16
CHNFG EQU B1000 
?TNUM EQU 1 
TERR  EQU .+1 
D128  DEC 128 
.103  OCT 103 
SECTR EQU 116B
NSECT NOP 
TRAK  OCT 0 
SECT  NOP 
TRAK2 NOP 
SECT2 NOP 
**                             ** 
***  OUTPUT AN ERROR MESSAGE  *** 
**                             ** 
* 
*  ERRORS ARE IDENTIFIED, THE APPROPRIATE SECTOR OF DRUM LOADED,
*  AND THE MESSAGE PRINTED.  IF A SYNTAX ERROR AND IN 'TAPE'
*  MODE, THE ERROR IS EMBEDDED IN THE PROGRAM AS A PSUEDO-
*  STATEMENT AND THE ERROR FLAGS ADJUSTED AS APPROPRIATE.  IF A 
*  SYNTAX ERROR AND IN 'KEY' MODE, 'ERROR' IS PRINTED AND INPUT 
*  REQUESTED.  THE ERROR MESSAGE IS PRINTED IF THE REPLY IS OTHER 
*  THAN A SIMPLE CARRIAGE RETURN.  IF THE LENGTH WORD OF THE ERROR
*  MESSAGE IS POSITIVE THE CURRENT LINE NUMBER IS APPENDED. 
* 
SERR  NOP 
#SERR CCA 
      ADA SERR
      LDB A,I      GET JSB SERRS(+N),I INSTRUCTION
      AND PGMSK    GET PAGE ADDR. 
      STA TEMPZ        AND SAVE 
      LDA B 
      AND B2000    GET BASE/CURRENT PAGE BIT
      SZA          CURRENT PAGE?
      JMP SERR0    YES
      LDA B        NO. GET JSB
      AND ADMSK    GET ADDR. OF BP LINK 
      LDA A,I      GET LINK 
      JMP SERRY 
      SPC 1 
SERR0 LDA B        GET JSB INSTRUCTION
      AND ADMSK    GET REL. ADDR. IN PAGE 
      ADA TEMPZ    FORM ACTUAL ADDR.
      LDB A,I      GET LINK WORD
      SSB,RSS      B(15)=1 =>NOT ON SAME PAGE ASSERRS 
      JMP SERRZ      =0 => ON SAME PAGE AS SERRS
      LDA B        GET LINK WORD
SERRY AND INF      REMOVE INDIRECT BIT
SERRZ CMA,INA      CALCULATE
      ADA SERRA      ERROR
      CMA,INA          NUMBER 
      STA LT3 
      ADA RERRA 
      STA LT5      SYNTAX 
      SSA,RSS        ERROR? 
      JMP SERR1    NO 
      LDA TAPEF      YES. TAPE
      AND MAIN,I
      SZA              MODE?
      JMP SERR6    YES
      CLB 
      STB LT2      OUTPUT 
      LDA ASCER      'ERROR'
      STA LT1          AND
      LDA .-3            WAIT 
      JSB OUTST            FOR
      LDA #LDFG     GET LOAD FLAG 
      SZA,RSS       LOAD? 
      JMP SERZZ     NO
      LDA .+15B     YES. OUTPUT A 
      JSB OUTCR       CR
      JMP SERR1 
SERZZ LDA IWT 
      JSB SCHIQ 
      JSB GETCR    CR ONLY? 
      JMP SERR4    YES
* 
**  PRINT THE MESSAGE  ** 
* 
SERR1 JSB ERSTS 
      JMP ERR2S 
      SPC 1 
ERSTS NOP 
      LDA TRAK      GET START TRACK # OF ERROR SEG. 
      SZA           =0? 
      JMP ERSTS,I    NO. CONTINUE 
      JSB EXEC      YES. GET START T/S OF ERROR SEG.
      DEF *+4 
      DEF .+18
      DEF ERS 
      DEF NSECT 
      SPC 1 
      STA TRAK
      AND B377
      STA SECT
      LDA TRAK
      ALF,ALF 
      AND B377
      STA TRAK
      ADB .+2       GET START ADDR. OF ERROR SEG. 
      LDA B,I 
      STA ERAD
      JMP ERSTS,I 
      SPC 1 
ERR2S CLB 
      STB LT6 
SERRB LDA LT5 
      SSA,RSS       EXECUTION ERROR?
      JMP SERR5     YES 
      CLA           NO, OUTPUT
      JSB OUTCR     A NULL. 
      LDA .-7 
      STA LT1       OUTPUT
      LDA .+40B 
      JSB OUTCR       7 
      ISZ LT1 
      JMP *-3           BLANKS. 
SERR2 LDA LT3       COMPUTE 
      ARS,ARS       GET REL. SECTOR # 
      ARS 
      ADA SECT      ADD START SECTOR
      CLB 
      DIV SECTR     DIVIDE BY # SECTORS/TRACK 
      ADA TRAK
      STA TRAK2 
      STB SECT2 
      SPC 1 
      JSB EXEC      GET PROPER SECTOR 
      DEF *+7 
      DEF .-1 
      DEF .103
ERAD  NOP 
      DEF D128
      DEF TRAK2 
      DEF SECT2 
      SPC 1 
      LDA LT3 
      AND .+7 
      ALF 
      ADA ERAD
      STA LT1           ADDRESS 
      CCB 
      LDA 0,I 
      SSA           LINE NUMBER?
      JMP *+3       NO
      STB LT6       YES, SET FLAG 
      CMA,INA 
      CLB           OUTPUT
      STB LT2         ERROR 
      JSB OUTST         MESSAGE 
      ISZ LT6       LINE NUMBER FLAG SET? 
      JMP SERR3     NO
      LDA LINEA     YES 
      STA LT1       PRINT 
      CLB 
      LDA .-5         " IN LINE " 
      JSB OUTST 
      LDB .LNUM     OUTPUT
      JSB OUTIN       LINE NUMBER 
SERR3 LDA LT5       'BAD
      ADA FERRA 
      STA LT5 
      ADA WERRA       INPUT'
      SZA,RSS           ERROR 
      JMP SERR8     YES 
      LDA .+15B     OUTPUT
      JSB OUTCR       CARRIAGE RETURN 
SERR4 LDA .+12B     OUTPUT
      JSB OUTCR       LINE FEED 
      LDA LT5       RUN 
      SSA             ERROR?
      JMP SER13     YES 
      ADA WERRA     NO, WARNING 
      SSA             ONLY? 
      JMP SERR9     NO, FORMAT ERROR
      CLA           YES 
SERR8 STA CHRCT 
      JMP SERR,I
SERR9 CLB 
      LDA IFSTR     CONVERT 
      CLE,ERA 
      ERB 
      STA LT1       SAVE POINTER
      LDA NCH       SAVE
      CMA,INA         NUMBER OF 
      STA NCH           CHARACTERS
      SZB           START WITH RIGHT BYTE?
      JMP SER11     YES 
SER10 EQU * 
      LDA LT1,I     FETCH 
      ALF,ALF         LEFT
      AND B377          CHAR
      JSB OUTCR     OUTPUT IT 
      ISZ NCH       FINISHED ALL CHARS? 
      RSS           NO
      JMP SER12     YES 
SER11 EQU * 
      LDA LT1,I     FETCH RIGHT 
      AND B377        CHARACTER 
      JSB OUTCR     OUTPUT IT 
      ISZ LT1       BUMP POINTER
      ISZ NCH       FINISHED ALL CHARS? 
      JMP SER10     NO
SER12 EQU * 
      JSB OUTCL     OUTPUT CR-LF
SER13 EQU * 
      LDA MAIN,I    DISALLOW
      IOR UNABT 
      STA MAIN,I      ABORTS
      LDA FCNTR 
      STA FRMAT 
      JSB LCDLP     UPDATE LAST CHANGE DATE 
      JMP SCHEQ 
* 
SERR5 LDA LT5 
      ADA FERRA     WARNING 
      ADA WERRA       ERROR?
      SSA,RSS 
      JMP SER5A     YES 
      LDA MAIN      NO, DOES USER 
      INA             HAVE LP?
      CPA PRIST 
      RSS 
      JMP SER5A     NO
      LDA .+23B     YES.
      JSB OUTCR     RELEASE IT
      LDA .+23B 
      JSB OUTCR 
      CLA 
      STA PRIST 
SER5A LDA .+15B       OUTPUT
      JSB OUTCR       CARRIAGE RETURN 
      LDA .+12B         AND 
      JSB OUTCR           LINE FEED 
      CLA           BLOCK 
      STA DCLC1,I     CLOCK 
      JSB ABCHK 
      LDA DCLC2,I   UNBLOCK 
      STA DCLC1,I     CLOCK 
      LDA LT5       WARNING 
      ADA FERRA 
      ADA WERRA 
      SSA,RSS         ERROR?
      JMP SERR2     YES 
      LDA CHNFG     NO
      CMA 
      AND 1,I       CLEAR 
      STA 1,I         CHAIN FLAG
      INB 
      LDA 1,I       TELL 2114 
      IOR UNR         THAT USER IS
      JSB S14SC 
      LDA .+4       CLEAR PBFLG AND CBFLG 
      ADB .-?TNUM 
      JSB EDABR          BITS.
      JMP SERR2 
SERR6 LDB PBPTR 
      ADB .+2       STORE 
      LDA LT3         ERROR 
      STA 1,I           NUMBER
      INB           SET POINTER TO
      STB SBPTR       LAST WORD +1 OF ERROR 
      SZA,RSS       'OUT OF STORAGE' ERROR? 
      ISZ SYMTB     YES 
      LDA MAIN,I    FIRST 
      IOR TERR
      CPA MAIN,I      ERROR?
      JMP *+4       NO
      STA MAIN,I    YES--SET FLAG 
      CLA           CLEAR 
      STA ERRCT       ERROR COUNTER 
      ISZ ERRCT     COUNT ERROR 
      JSB BCKSP     SEEK
SERR7 JSB GETCR       CARRIAGE RETURN 
      JMP ACCST 
      JMP *-2 
* 
ASCER DEF ERR-1 
ERR   OCT 5105
      ASC 2,RROR
LINEA DEF * 
      ASC 4, IN LINE
      OCT 20000 
ERS   ASC 3,ERSEG 
      END 
