ASMB,R,B,L,X
      NAM DELET,5 
      SPC 2 
      COM PBPTR,NEG(26),.(52),MAIN,SPROG,SYMTB,ERRCT
      COM SBPTR,SBUFA,SSTAK,SYNTQ,SERRS(36),RERRS(50) 
      COM FERRS(15),WERRS(10),MAXCR,MSFLG,PFLAG,SFLAG 
      COM DFLAG,TEMP(3),CDFLG,ARYAD,GFLAG,SIGN,UFLAG
      COM PINTG(2),TABLE,LNGTH,SMBGN,SLENG,COUNT
      COM TBLPT,TSPTR,SBT0
      COM EXP,MANT1,MANT2,EXPON,DPFLG,NT0,ENOUF 
      COM LNAME,CHRCT,OCTMP,SPTR,FILTB,NSPTR,INFST
      COM DCLC1,DEST,DCLC2,STEND,DFILT,FILPT,COMSN
      COM MVEND 
      COM STYPE,USESN,FILCT,.LNUM 
      COM PRGCT,STCT1,STCT2,NUMPT,STCT3,FLINK 
      COM STMP1,STMP2,VLFLG,VALTB,FCORE,COMPT,VTMP1 
      COM VTMP2,VTMP3,VTMP4,VTMP5,IFSS
      COM CU1,APTR,DCFLG,TEMP2,LT1,LT2,FCNTR,RTNST
      COM ASINP,RTRNQ,FORQ,TEMP1,FILE#
      COM INTMP,TMPST,TPRME,TNULL,PS1,DMY3,TEMP5
      COM PRIST,FBASE,RQ3,EOL,GTMP,FORST,EFN0,FVT 
      COM EFN1,EFN2,EFN3,MCNT,INITF,IFCNT,OPDST 
      COM FFLG,TEMP4,EORFL,NCH,TEMP6,STRLN,ATIM,LT5 
      COM ASTYP,ASBFP,ORDNO,ATMP(2),RETCD 
      COM DCCNT,NXTDT,VL0,RCRD#,PMASK,ITEMP 
      COM OPTRQ,EST3,EST2,EST1,TT1,TT2,DMY1 
      COM DMY2,TT3,TT4,CP0,CP1,PS0,MPT,NQT,RT0
      COM RT1,TRFCH,TRS0,A1,A2,C1,EOPF,NUMBF(6),FD0 
      COM OT1,OT2,OT3,LBTMP,REPCT,RNDX1,RNDX2 
      COM X2TMP(2),CTMP,DTMP(2),BTMP(2),XTEMP(2),YTEMP(2) 
      COM UTEMP(2),LT0,LT8,LT3,LT6,LT7,LT4,DIGCT
      COM NMPTR,NMTMP,FFLAG,DGCNT 
      COM IFSTR,EC,CC,DP,CONTR,CC1,CC2,PC1,PC2
      COM SFLG,FST,EFLAG,NUM1,NUM2,SBD,SAD,SNFLG
      COM NAD,NBD,FSP,EST,NUMW1,NUMW2,EXPW
      COM IHB,HBP,NHBW,NBLK,TOTDG,DCTR
      COM EDSTA,LCH,ELCNT 
      COM STPTR,RSPTR,TEMP3 
      COM MT1,MT0,MT3,MT2,MEXIT,DMY4,MOP,BS2,BS3
      COM MMT0,MMT1,MMT2,MMT6(2),MMT7(2),MMT3,MMT4,MMT5 
      COM MMT8(2),SCALR(2),ID0,ID1,MAXE(2),TOL(2),PIVEL 
      COM VT0,RD0 
      COM SOURC,TAP0,TAP1 
      COM STIME(5)
      COM LTEMP(16),LTYP1,LTYP2,LCHCR,MOVES,MOVED 
      SPC 2 
      EXT DCMPL,FNDPS,LTYPE,LCHAR,ILFER,LLEND 
      EXT SCHEQ 
      EXT GETCR 
      EXT MOVEW 
      SPC 2 
M72B  OCT -72 
* THE DELETE COMMAND ALLOWS A USER TO DELETE A SECTION OF HIS 
* PROGRAM IN A SINGLE LINE. THE FORMAT IS:
*     DELETE-M,N
*  OR 
*     DELETE-M
* WITH THE FIRST FORMAT, ALL LINES FROM M THROUGH N ARE 
* DELETED. WITH THE SECOND, ALL LINES FROM M TO THE END OF
* THE PROGRAM ARE DELETED.
      SPC 1 
DELET JSB DELNM    GET M. 
      STA DELM
      LDA DEL99 
      CPB .+54B     IF COMMA FOLLOWS, GO
      JSB DELNM      GET N. 
      STA DELN
      CPB .+15B     NEXT CHAR MUST BE A CR. 
      JMP DEL2
      JMP ILFER 
* 
DEL2  CMA           CHECK FOR M<=N
      ADA DELM
      SSA,RSS 
      JMP ILFER 
* 
      JSB DCMPL      DECOMPILE IT.
* 
      LDA PBPTR     LOCATE FIRST
      LDB DELM       STATEMENT TO BE DELETED. 
      JSB FNDPS 
      RSS 
      NOP 
      STB MOVED 
* 
      LDA PBPTR     LOCATE FIRST STATEMENT
      LDB DELN       NOT TO BE DELETED. 
      STA MOVES 
      CPB DEL99 
      JMP DEL3
      INB 
      JSB FNDPS 
      RSS 
      NOP 
      STB MOVES 
* 
DEL3  LDB MOVES     IF DEST=SOURCE, NOTHING DELETED.
      CPB MOVED 
      JMP DEL4
      CMB,INB       OTHERWISE, MOVE UP. 
      ADB PBPTR 
      CMB,INB 
      JSB MOVEW 
      LDA MOVED     SET NEW VALUE OF
      STA PBPTR      PBPTR. 
      JMP LLEND 
DEL4  LDA .-17
      LDB NDELA 
      JSB LTYPE 
      JMP SCHEQ 
NDELA DEF *+1 
      OCT 5116      LF-N
      ASC 7,OTHING DELETED
* 
* DELNM READS A NUMBER FROM THE USER'S BUFFER. IN CASE OF ERROR IT
* GOES TO DEL1. OTHERWISE IT RETURNS WITH A=# AND B=NEXT CHAR.
* 
DELNM NOP 
      CLA           INITIALIZE TO 0.
DEL7  EQU * 
      STA DELT
      JSB LCHAR 
      NOP 
      STA 1         SAVE IN B.
      ADA M72B      TEST FOR DIGIT. 
      SSA,RSS 
      JMP DEL6
      ADA .+10
      SSA 
      JMP DEL6
      LDB DELT
      ADB DELMX 
      SSB,RSS 
      JMP ILFER 
      LDB DELT      MPY OLD VALUE BY 10.
      RBL,RBL 
      ADB DELT
      RBL 
      ADA 1         ADD IN NEW. 
      JMP DEL7      LOOP. 
* 
DEL6  LDA DELT      GET VALUE 
      SZA           FAIL IF ZERO. 
      JMP DELNM,I 
      JMP ILFER 
* 
DELM  EQU LTEMP 
DELN  EQU LTEMP+1 
DELT  EQU LTEMP+2 
DEL99 DEC 9999
DELMX DEC -1000 
      END DELET 
