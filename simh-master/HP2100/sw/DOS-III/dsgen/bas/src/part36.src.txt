ASMB,R,B,L,X
      NAM NAME,5
      SPC 2 
      COM PBPTR,NEG(26),.(52),MAIN,SPROG,SYMTB,ERRCT
      COM SBPTR,SBUFA,SSTAK,SYNTQ,SERRS(36),RERRS(50) 
      COM FERRS(15),WERRS(10),MAXCR,MSFLG,PFLAG,SFLAG 
      COM DFLAG,TEMP(3),CDFLG,ARYAD,GFLAG,SIGN,UFLAG
      COM PINTG(2),TABLE,LNGTH,SMBGN,SLENG,COUNT
      COM TBLPT,TSPTR,SBT0
      COM EXP,MANT1,MANT2,EXPON,DPFLG,NT0,ENOUF 
      COM LNAME,CHRCT,OCTMP,SPTR,FILTB,NSPTR,INFST
      COM DCLC1,DEST,DCLC2,STEND,DFILT,FILPT,COMSN
      COM MVEND 
      COM STYPE,USESN,FILCT,.LNUM 
      COM PRGCT,STCT1,STCT2,NUMPT,STCT3,FLINK 
      COM STMP1,STMP2,VLFLG,VALTB,FCORE,COMPT,VTMP1 
      COM VTMP2,VTMP3,VTMP4,VTMP5,IFSS
      COM CU1,APTR,DCFLG,TEMP2,LT1,LT2,FCNTR,RTNST
      COM ASINP,RTRNQ,FORQ,TEMP1,FILE#
      COM INTMP,TMPST,TPRME,TNULL,PS1,DMY3,TEMP5
      COM PRIST,FBASE,RQ3,EOL,GTMP,FORST,EFN0,FVT 
      COM EFN1,EFN2,EFN3,MCNT,INITF,IFCNT,OPDST 
      COM FFLG,TEMP4,EORFL,NCH,TEMP6,STRLN,ATIM,LT5 
      COM ASTYP,ASBFP,ORDNO,ATMP(2),RETCD 
      COM DCCNT,NXTDT,VL0,RCRD#,PMASK,ITEMP 
      COM OPTRQ,EST3,EST2,EST1,TT1,TT2,DMY1 
      COM DMY2,TT3,TT4,CP0,CP1,PS0,MPT,NQT,RT0
      COM RT1,TRFCH,TRS0,A1,A2,C1,EOPF,NUMBF(6),FD0 
      COM OT1,OT2,OT3,LBTMP,REPCT,RNDX1,RNDX2 
      COM X2TMP(2),CTMP,DTMP(2),BTMP(2),XTEMP(2),YTEMP(2) 
      COM UTEMP(2),LT0,LT8,LT3,LT6,LT7,LT4,DIGCT
      COM NMPTR,NMTMP,FFLAG,DGCNT 
      COM IFSTR,EC,CC,DP,CONTR,CC1,CC2,PC1,PC2
      COM SFLG,FST,EFLAG,NUM1,NUM2,SBD,SAD,SNFLG
      COM NAD,NBD,FSP,EST,NUMW1,NUMW2,EXPW
      COM IHB,HBP,NHBW,NBLK,TOTDG,DCTR
      COM EDSTA,LCH,ELCNT 
      COM STPTR,RSPTR,TEMP3 
      COM MT1,MT0,MT3,MT2,MEXIT,DMY4,MOP,BS2,BS3
      COM MMT0,MMT1,MMT2,MMT6(2),MMT7(2),MMT3,MMT4,MMT5 
      COM MMT8(2),SCALR(2),ID0,ID1,MAXE(2),TOL(2),PIVEL 
      COM VT0,RD0 
      COM SOURC,TAP0,TAP1 
      COM STIME(5)
      COM LTEMP(16),LTYP1,LTYP2,LCHCR,MOVES,MOVED 
      SPC 2 
      EXT MLINB,LTYPE,SCHEQ 
      EXT LLEND,ILFER,LCHAR 
      SPC 2 
B     EQU 1 
?NAME EQU 5 
?LINK EQU 15B 
BIT15 OCT 100000
      HED NAME
* THE NAME ROUTINE ALLOWS A USER TO ASSIGN HIS PROGRAM A NAME.
      SPC 1 
NAME  LDA MLINB 
      ADA .+?NAME-?LINK 
      STA LTEMP 
      STA LTEMP+3   SAVE NAME POINTER IN LTEMP+3. 
      LDA 0,I       SAVE RUN-ONLY BIT IN LTEMP+4. 
      AND BIT15 
      STA LTEMP+4 
      LDA .-3       SET COUNTER.
      STA LTEMP+1 
      CLA           SET FLAG TO SAY 
      STA LTEMP+2   FIRST CHARACTER 
NAME1 JSB NAMER     GET A CHAR. 
      ALF,ALF 
      STA LTEMP,I 
      JSB NAMER     GET 2ND CHAR. 
      IOR LTEMP,I 
      STA LTEMP,I 
      ISZ LTEMP     BUMP NAME POINTER.
      ISZ LTEMP+1   TEST FOR DONE.
      JMP NAME1 
      LDA LTEMP+3,I GET FIRST WORD OF NAME
      IOR LTEMP+4   MERGE IN RUN-ONLY BIT.
      STA LTEMP+3,I 
      JSB NAMER     TEST FOR ONLY 6 CHARS.
      CPA .+40B 
      JMP LLEND 
* 
      LDA NAM27 
      LDB NAM26 
      JSB LTYPE 
      JMP SCHEQ 
NAM27 DEC -27 
NAM26 DEF *+1 
      OCT 5117      LF-O
      ASC 13,NLY 6 CHARACTERS ACCEPTED
* 
NAMER NOP           GETS NEXT NAME CHARACTER. 
      JSB LCHAR 
      LDA .+40B     USE BLANK IF END OF LINE. 
      STA 1         HOLD IN B.
      AND .140      TEST FOR CONTROL CHAR.
      SZA 
      CPA .140
      JMP NAMER+1   SKIP CONTROL CHARS. 
      LDA 1         GET IN A AGAIN. 
      LDB LTEMP+2   GET 1ST CHAR FLAG.
      ISZ LTEMP+2 
      CPA .+44B     IF $
      RSS 
      CPA .+52B       OR IF * 
      SZB               AND FIRST CHARACTER 
      JMP NAME2 
* 
      LDA .-24
      LDB NAM24 
      JSB LTYPE 
      JMP SCHEQ 
NAM24 DEF *+1 
      OCT 5111      LF-I
      ASC 11,LLEGAL FIRST CHARACTER 
NAME2 CPA .+54B     IS CHARACTER A COMMA? 
      RSS 
      JMP NAMER,I   NO. RETURN CHARACTER. 
      LDA .-3       YES. BLANK  
      STA LTEMP 
      LDB MLINB 
      ADB .+?NAME-?LINK 
      LDA NAMBL              ILLEGAL
NAMFL STA B,I 
      INB 
      ISZ LTEMP 
      JMP NAMFL                NAME.
      JMP ILFER 
NAMBL ASC 1,
.140  OCT 140 
      END NAME
