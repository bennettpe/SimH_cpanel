ASMB,R,B,L,X
      NAM GETB,5
      SPC 2 
      COM PBPTR,NEG(26),.(52),MAIN,SPROG,SYMTB,ERRCT
      COM SBPTR,SBUFA,SSTAK,SYNTQ,SERRS(36),RERRS(50) 
      COM FERRS(15),WERRS(10),MAXCR,MSFLG,PFLAG,SFLAG 
      COM DFLAG,TEMP(3),CDFLG,ARYAD,GFLAG,SIGN,UFLAG
      COM PINTG(2),TABLE,LNGTH,SMBGN,SLENG,COUNT
      COM TBLPT,TSPTR,SBT0
      COM EXP,MANT1,MANT2,EXPON,DPFLG,NT0,ENOUF 
      COM LNAME,CHRCT,OCTMP,SPTR,FILTB,NSPTR,INFST
      COM DCLC1,DEST,DCLC2,STEND,DFILT,FILPT,COMSN
      COM MVEND 
      COM STYPE,USESN,FILCT,.LNUM 
      COM PRGCT,STCT1,STCT2,NUMPT,STCT3,FLINK 
      COM STMP1,STMP2,VLFLG,VALTB,FCORE,COMPT,VTMP1 
      COM VTMP2,VTMP3,VTMP4,VTMP5,IFSS
      COM CU1,APTR,DCFLG,TEMP2,LT1,LT2,FCNTR,RTNST
      COM ASINP,RTRNQ,FORQ,TEMP1,FILE#
      COM INTMP,TMPST,TPRME,TNULL,PS1,DMY3,TEMP5
      COM PRIST,FBASE,RQ3,EOL,GTMP,FORST,EFN0,FVT 
      COM EFN1,EFN2,EFN3,MCNT,INITF,IFCNT,OPDST 
      COM FFLG,TEMP4,EORFL,NCH,TEMP6,STRLN,ATIM,LT5 
      COM ASTYP,ASBFP,ORDNO,ATMP(2),RETCD 
      COM DCCNT,NXTDT,VL0,RCRD#,PMASK,ITEMP 
      COM OPTRQ,EST3,EST2,EST1,TT1,TT2,DMY1 
      COM DMY2,TT3,TT4,CP0,CP1,PS0,MPT,NQT,RT0
      COM RT1,TRFCH,TRS0,A1,A2,C1,EOPF,NUMBF(6),FD0 
      COM OT1,OT2,OT3,LBTMP,REPCT,RNDX1,RNDX2 
      COM X2TMP(2),CTMP,DTMP(2),BTMP(2),XTEMP(2),YTEMP(2) 
      COM UTEMP(2),LT0,LT8,LT3,LT6,LT7,LT4,DIGCT
      COM NMPTR,NMTMP,FFLAG,DGCNT 
      COM IFSTR,EC,CC,DP,CONTR,CC1,CC2,PC1,PC2
      COM SFLG,FST,EFLAG,NUM1,NUM2,SBD,SAD,SNFLG
      COM NAD,NBD,FSP,EST,NUMW1,NUMW2,EXPW
      COM IHB,HBP,NHBW,NBLK,TOTDG,DCTR
      COM EDSTA,LCH,ELCNT 
      COM STPTR,RSPTR,TEMP3 
      COM MT1,MT0,MT3,MT2,MEXIT,DMY4,MOP,BS2,BS3
      COM MMT0,MMT1,MMT2,MMT6(2),MMT7(2),MMT3,MMT4,MMT5 
      COM MMT8(2),SCALR(2),ID0,ID1,MAXE(2),TOL(2),PIVEL 
      COM VT0,RD0 
      COM SOURC,TAP0,TAP1 
      COM STIME(5)
      COM LTEMP(16),LTYP1,LTYP2,LCHCR,MOVES,MOVED 
      COM ERSEC(77),DRFLG 
      SPC 2 
      EXT LCHAR,LWAUS,LIBER,LW97,MLINB,EXEC 
      EXT DLOOK,SEMIC,LLEND 
      EXT PBUFF 
      SPC 2 
B     EQU 1 
DLTEM DEF LTEMP 
LIBSC EQU LTEMP+8 
LIBPB EQU LTEMP+13
LIBSP EQU LTEMP+9 
.103  OCT 103 
?LINK EQU 15B 
?NAME EQU 5 
GET   LDA DRFLG 
      SZA 
      JMP GET0
      LDA .-22
      LDB *+2 
      JMP LIBER 
      DEF *+1 
      OCT 5044
      ASC 10,FILS NOT INITIALIZED 
GET0  LDA DLTEM 
      STA GETP
      LDA .-3       SET UP COUNTER. 
      STA GETC
      JSB LCHAR     GET FIRST INPUT CHAR. 
      JMP GET1      ERROR IF NONE.
GET3  ALF,ALF       SAVE CHARACTER IN TABLE.
      STA GETP,I
      JSB LCHAR     GET RIGHT CHARACTER.
      LDA .+40B     IF END CHANGE TO BLANK. 
      IOR GETP,I
      STA GETP,I
      ISZ GETP      BUMP POINTER. 
      JSB LCHAR     GET NEXT CHARACTER. 
      LDA .+40B 
      ISZ GETC      DO WE WANT IT?
      JMP GET3      YES.
      CPA .+40B     NO--A SHOULD NOW BE BLANK.
      JMP GET4
* 
GET1  LDA .-13
      LDB *+2 
      JMP LIBER 
      DEF *+1 
      OCT 5111
      ASC 6,NVALID NAME 
* 
GET4  LDA SPROG    SAVE END-OF- 
      STA GETS        COMMON POINTER
      JSB DLOOK    SEARCH DIRECTORY FOR PROGRAM 
      JMP GET9
* 
GET19 LDA .-16
      LDB *+2 
      JMP LIBER 
      DEF *+1 
      OCT 5116
      ASC 7,O SUCH PROGRAM
GET9  STA GETI     SAVR T/S 
      LDA 1,I      TEST FOR FILE
      SSA 
      JMP GET10 
* 
      LDA .-16
      LDB *+2 
      JMP LIBER 
      DEF *+1 
      OCT 5105      LF-E
      ASC 7,NTRY IS A FILE
GET10 STA GETLN    SAVE LENGTH
      INB 
      LDA B,I 
      ADA PBUFF 
      STA LIBSP 
      STA MEMSA 
      INB 
      LDA B,I 
      STA LIBSC 
      LDA GETLN 
      CMA,INA       COMPUTE FIRST 
      STA GETLN 
      ADA LIBSP       UNUSED WORD 
      STA LIBPB 
      CMA,INA       COMPUTE NEGATIVE
      LDB LIBSC 
      SSB,RSS       SEMI-COMPILED?
      JMP GET25     NO
      ADA LWAUS 
      SSA,RSS       TOO BIG?
      JMP GET13     NO
      JMP GET26     YES 
GET25 EQU * 
      ADA LW97      TOTAL LENGTH
      SSA,RSS       COMPARE WITH MAX ALLOWED. 
      JMP GET13     OK
GET26 EQU * 
      LDA .-18
      LDB *+2 
      JMP LIBER 
      DEF *+1 
      OCT 5120      LF-P
      ASC 8,ROGRAM TOO LARGE
* 
GET13 LDA GETI
      ALF,ALF 
      AND =B377 
      STA GETD
      LDA GETI
      AND =B377 
      STA GETI
      JSB EXEC     READ IN PROGRAM
      DEF *+7 
      DEF .-1 
      DEF .103
MEMSA NOP 
      DEF GETLN 
      DEF GETD
      DEF GETI
      SPC 1 
      LDB MLINB 
      ADB .+?NAME-?LINK 
      STB GETI
      INB 
      LDA LTEMP 
      STA GETI,I
      STB GETI
      DLD LTEMP+1 
      DST GETI,I    STORE IN TABLE. 
      JSB SEMIC 
      JMP LLEND     TERMINATE.
* 
GETP  EQU LTEMP+4 
GETI  EQU LTEMP+6 
GETC  EQU LTEMP+7 
GETLN EQU LTEMP+10
GETS EQU LTEMP+11 
GETD  EQU LTEMP+12
*     LTEMP+15 IS USED HERE ALSO
      END GET 
