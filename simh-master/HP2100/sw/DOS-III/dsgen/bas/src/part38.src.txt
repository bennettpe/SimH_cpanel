ASMB,R,B,L,X
      NAM CATAL,5 
      SPC 2 
      COM PBPTR,NEG(26),.(52),MAIN,SPROG,SYMTB,ERRCT
      COM SBPTR,SBUFA,SSTAK,SYNTQ,SERRS(36),RERRS(50) 
      COM FERRS(15),WERRS(10),MAXCR,MSFLG,PFLAG,SFLAG 
      COM DFLAG,TEMP(3),CDFLG,ARYAD,GFLAG,SIGN,UFLAG
      COM PINTG(2),TABLE,LNGTH,SMBGN,SLENG,COUNT
      COM TBLPT,TSPTR,SBT0
      COM EXP,MANT1,MANT2,EXPON,DPFLG,NT0,ENOUF 
      COM LNAME,CHRCT,OCTMP,SPTR,FILTB,NSPTR,INFST
      COM DCLC1,DEST,DCLC2,STEND,DFILT,FILPT,COMSN
      COM MVEND 
      COM STYPE,USESN,FILCT,.LNUM 
      COM PRGCT,STCT1,STCT2,NUMPT,STCT3,FLINK 
      COM STMP1,STMP2,VLFLG,VALTB,FCORE,COMPT,VTMP1 
      COM VTMP2,VTMP3,VTMP4,VTMP5,IFSS
      COM CU1,APTR,DCFLG,TEMP2,LT1,LT2,FCNTR,RTNST
      COM ASINP,RTRNQ,FORQ,TEMP1,FILE#
      COM INTMP,TMPST,TPRME,TNULL,PS1,DMY3,TEMP5
      COM PRIST,FBASE,RQ3,EOL,GTMP,FORST,EFN0,FVT 
      COM EFN1,EFN2,EFN3,MCNT,INITF,IFCNT,OPDST 
      COM FFLG,TEMP4,EORFL,NCH,TEMP6,STRLN,ATIM,LT5 
      COM ASTYP,ASBFP,ORDNO,ATMP(2),RETCD 
      COM DCCNT,NXTDT,VL0,RCRD#,PMASK,ITEMP 
      COM OPTRQ,EST3,EST2,EST1,TT1,TT2,DMY1 
      COM DMY2,TT3,TT4,CP0,CP1,PS0,MPT,NQT,RT0
      COM RT1,TRFCH,TRS0,A1,A2,C1,EOPF,NUMBF(6),FD0 
      COM OT1,OT2,OT3,LBTMP,REPCT,RNDX1,RNDX2 
      COM X2TMP(2),CTMP,DTMP(2),BTMP(2),XTEMP(2),YTEMP(2) 
      COM UTEMP(2),LT0,LT8,LT3,LT6,LT7,LT4,DIGCT
      COM NMPTR,NMTMP,FFLAG,DGCNT 
      COM IFSTR,EC,CC,DP,CONTR,CC1,CC2,PC1,PC2
      COM SFLG,FST,EFLAG,NUM1,NUM2,SBD,SAD,SNFLG
      COM NAD,NBD,FSP,EST,NUMW1,NUMW2,EXPW
      COM IHB,HBP,NHBW,NBLK,TOTDG,DCTR
      COM EDSTA,LCH,ELCNT 
      COM STPTR,RSPTR,TEMP3 
      COM MT1,MT0,MT3,MT2,MEXIT,DMY4,MOP,BS2,BS3
      COM MMT0,MMT1,MMT2,MMT6(2),MMT7(2),MMT3,MMT4,MMT5 
      COM MMT8(2),SCALR(2),ID0,ID1,MAXE(2),TOL(2),PIVEL 
      COM VT0,RD0 
      COM SOURC,TAP0,TAP1 
      COM STIME(5)
      COM LTEMP(16),LTYP1,LTYP2,LCHCR,MOVES,MOVED 
      COM ERSEC(77),DRFLG 
      SPC 2 
      EXT OUTCH,DLOOK,LLEND 
      EXT LIBER 
      EXT ABFLG 
      SPC 2 
LOUT  EQU OUTCH 
A     EQU 0 
B     EQU 1 
B377  OCT 377 
DVSRS DEC 10000 
      DEC 1000
      DEC 100 
      DEC 10
ASC00 ASC 1,00
* 
CATAL LDA DRFLG 
      SZA 
      JMP CAT0
      LDA .-22
      LDB *+2 
      JMP LIBER 
      DEF *+1 
      OCT 5044
      ASC 10,FILS NOT INITIALIZED 
CAT0  STA ABFLG     ALLOW ABORTS
      CLA           SET LTEMP(0:2) = 0
      STA LTEMP+1   WE WILL HAVE TO LOCATE THE FIRST
      STA LTEMP+2   DIRECTORY ENTRY FOLLOWING 
      STA LTEMP 
      LDA .+12B     LF
     JSB LOUT 
     LDA .-4
     STA CATJ 
CAT00 EQU * 
     LDA .-9
     STA CATN 
     LDA CATTP
     STA CATS 
CAT01 EQU * 
     LDA CATS,I 
     ALF,ALF
     AND B377 
     JSB LOUT       OUTPUT LEFT CHARACTER 
     LDA CATS,I 
     AND B377 
     JSB LOUT       OUTPUT RIGHT CHARACTER
     ISZ CATS       BUMP TO NEXT HEADING WORD 
     ISZ CATN 
     JMP CAT01
     ISZ CATJ 
     JMP CAT00
     LDA .+15B      CR
     JSB LOUT 
* 
      LDA .-4       SET N TO COUNT # OF NAMES ON
      STA CATN      THIS LINE.
      LDA .+12B     EMIT A LINE FEED. 
      JSB LOUT
* 
      JSB DLOOK 
      NOP           OUTPUT. 
      STB CATI     SAVE LAST ADDR. OF DIRECT. 
CAT1  ADB .-8      B => LAST ENTRY
      LDA B,I 
      CPA =A$F     ="$F"? 
      RSS          YES. CHECK FOR "ILS "
      JMP CAT1     NO. CHECK NEXT LOWER ENTRY 
      INB 
      LDA B,I      GET SECOND WORD OF NAME
      CPA =AIL     ="IL"? 
      JMP CAT2     YES. 
      ADB .-9 
      JMP CAT1+1
CAT2  INB 
      LDA B,I      GET THIRD WORD OF NAME 
      CPA =AS      ="S "? 
      JMP CAT3     YES. 
      ADB .-10     NO.
      JMP CAT1+1
      SPC 1 
CAT3  ADB .+6      POINT TO NEXT ENTRY
CAT10 CPB CATI     POINTER => LAST WORD OF DIREC.?
      JMP CAT13    YES. DONE
      ADB .-1 
      STB CATP
      JMP CAT23    NO.OUTPUT FILENAME & INFO. 
      SPC 1 
* 
CAT13 LDA .+15B     EMIT CRLF 
      JSB LOUT
      JMP LLEND 
* 
CAT23 LDA .-3       SET UP COUNTER FOR
      STA CATC       PRINTING NAME. 
CAT15 ISZ CATP      BUMP P TO NEXT WORD.
      LDA CATP,I
      ALF,ALF       GET LEFT CHAR.
      AND =B377 
      JSB LOUT      PRINT IT. 
      LDA CATP,I    OUTPUT IT 
      AND =B377 
      JSB LOUT
      ISZ CATC      TEST FOR 6 CHARS OUT. 
      JMP CAT15 
      LDA .+40B     OUTPUT A BLANK. 
      JSB LOUT
      LDB CATP
      ADB .+4 
      LDA B,I 
      SSA,RSS       SEMI-COMPILED?
      JMP CAT20     NO
      LDA ASCC      YES, OUTPUT A "C" 
      JMP CAT21 
CAT20 LDB .+2 
      ADB CATP
      LDA B,I       GET SECOND WORD OF NAME 
      SSA          FILE?
      JMP *+3       NO
      LDA ASCF      YES, OUTPUT "F" 
      RSS 
      LDA .+40B     NEITHER, OUTPUT " " 
CAT21 JSB LOUT
      LDA .+40B 
      JSB LOUT
      LDA .+40B     OUTPUT " "
      JSB LOUT
      LDA CATP      BUMP TO PROGRAM LENGTH
      ADA .+2 
      STA CATP
      CLA           INITIALIZE
      STA CATZS       ZERO SUPPRESS FLAG
* 
      LDA CATP,I    GET LENGTH
      SSA           MAKE POSITIVE IF NECESSARY
      CMA,INA 
      CLB 
      DIV DVSRS     DIVIDE IT BY 10000
      JSB CAT24     OUTPUT THE FIRST CHARACTER
      DIV DVSRS+1   DIV BY 1000.
      JSB CAT24     OUTPUT THE SECOND DIGIT 
      DIV DVSRS+2   DIV BY 100
      JSB CAT24     OUTPUT THE THIRD DIGIT
      DIV .+10
      JSB CAT24     OUTPUT THE FOURTH DIGIT 
      ADA ASC00 
      JSB LOUT
      LDA .+40B     OUTPUT A BLANK
      JSB LOUT
      LDA .+40B      AND ANOTHER
      JSB LOUT
      LDA .+40B       AND ANOTHER 
      JSB LOUT
* 
      LDB CATP
      ADB .+4 
      STB CATP
      ISZ CATN      END OF LINE?
      JMP CAT10     NO. 
* 
      LDA .+15B     OUTPUT CR.
      JSB LOUT
      LDA .-4 
      STA CATN
      LDA .+12B 
      JSB LOUT
      LDB CATP
      JMP CAT10 
CAT24 NOP 
      STB CATLN     SAVE REMAINDER
      SZA           IS DIGIT ZERO?
      JMP CAT27     NO, GO PRINT IT 
      LDB CATZS     YES 
      SZB           IS IT A LEADING ZERO? 
      JMP CAT25     NO, GO PRINT IT 
      LDA .+40B     YES, SUPPRESS IT AND
      JMP CAT26       PRINT " " 
CAT27  STA CATZS    NONZERO THE SUPPRESS FLAG 
CAT25  ADA ASC00    CONSTRUCT THE CHARACTER 
CAT26 JSB LOUT      PUT OUT CHARACTER 
      LDA CATLN     GET REMAINDER 
      CLB           PREPARE FOR DIVIDE
      JMP CAT24,I   RETURN
* 
CATI  EQU LTEMP+4 
CATP  EQU LTEMP+5 
CATJ  EQU LTEMP+6 
CATN  EQU LTEMP+7 
CATC  EQU LTEMP+10
CATLN EQU LTEMP+11
CATS EQU LTEMP+12 
CATZS EQU LTEMP+13
ASCC  OCT 103 
ASCF  OCT 106 
CATTP DEF CATIT 
CATIT ASC 9, NAME    LENGTH 
      END CATAL 
