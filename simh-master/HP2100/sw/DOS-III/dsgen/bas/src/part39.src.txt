ASMB,R,B,L,X
      NAM KILL,5
      SPC 2 
      COM PBPTR,NEG(26),.(52),MAIN,SPROG,SYMTB,ERRCT
      COM SBPTR,SBUFA,SSTAK,SYNTQ,SERRS(36),RERRS(50) 
      COM FERRS(15),WERRS(10),MAXCR,MSFLG,PFLAG,SFLAG 
      COM DFLAG,TEMP(3),CDFLG,ARYAD,GFLAG,SIGN,UFLAG
      COM PINTG(2),TABLE,LNGTH,SMBGN,SLENG,COUNT
      COM TBLPT,TSPTR,SBT0
      COM EXP,MANT1,MANT2,EXPON,DPFLG,NT0,ENOUF 
      COM LNAME,CHRCT,OCTMP,SPTR,FILTB,NSPTR,INFST
      COM DCLC1,DEST,DCLC2,STEND,DFILT,FILPT,COMSN
      COM MVEND 
      COM STYPE,USESN,FILCT,.LNUM 
      COM PRGCT,STCT1,STCT2,NUMPT,STCT3,FLINK 
      COM STMP1,STMP2,VLFLG,VALTB,FCORE,COMPT,VTMP1 
      COM VTMP2,VTMP3,VTMP4,VTMP5,IFSS
      COM CU1,APTR,DCFLG,TEMP2,LT1,LT2,FCNTR,RTNST
      COM ASINP,RTRNQ,FORQ,TEMP1,FILE#
      COM INTMP,TMPST,TPRME,TNULL,PS1,DMY3,TEMP5
      COM PRIST,FBASE,RQ3,EOL,GTMP,FORST,EFN0,FVT 
      COM EFN1,EFN2,EFN3,MCNT,INITF,IFCNT,OPDST 
      COM FFLG,TEMP4,EORFL,NCH,TEMP6,STRLN,ATIM,LT5 
      COM ASTYP,ASBFP,ORDNO,ATMP(2),RETCD 
      COM DCCNT,NXTDT,VL0,RCRD#,PMASK,ITEMP 
      COM OPTRQ,EST3,EST2,EST1,TT1,TT2,DMY1 
      COM DMY2,TT3,TT4,CP0,CP1,PS0,MPT,NQT,RT0
      COM RT1,TRFCH,TRS0,A1,A2,C1,EOPF,NUMBF(6),FD0 
      COM OT1,OT2,OT3,LBTMP,REPCT,RNDX1,RNDX2 
      COM X2TMP(2),CTMP,DTMP(2),BTMP(2),XTEMP(2),YTEMP(2) 
      COM UTEMP(2),LT0,LT8,LT3,LT6,LT7,LT4,DIGCT
      COM NMPTR,NMTMP,FFLAG,DGCNT 
      COM IFSTR,EC,CC,DP,CONTR,CC1,CC2,PC1,PC2
      COM SFLG,FST,EFLAG,NUM1,NUM2,SBD,SAD,SNFLG
      COM NAD,NBD,FSP,EST,NUMW1,NUMW2,EXPW
      COM IHB,HBP,NHBW,NBLK,TOTDG,DCTR
      COM EDSTA,LCH,ELCNT 
      COM STPTR,RSPTR,TEMP3 
      COM MT1,MT0,MT3,MT2,MEXIT,DMY4,MOP,BS2,BS3
      COM MMT0,MMT1,MMT2,MMT6(2),MMT7(2),MMT3,MMT4,MMT5 
      COM MMT8(2),SCALR(2),ID0,ID1,MAXE(2),TOL(2),PIVEL 
      COM VT0,RD0 
      COM SOURC,TAP0,TAP1 
      COM STIME(5)
      COM LTEMP(16),LTYP1,LTYP2,LCHCR,MOVES,MOVED 
      COM ERSEC(77),DRFLG,DIREC(256),DTEMP,DCNT 
      COM DTRAK,DSECT 
      SPC 2 
      EXT LCHAR,LIBER,DLOOK,LLEND,DCMPL,EXEC
      EXT OUTCH 
      SPC 2 
DLTEM DEF LTEMP 
ASCBB ASC 1,
KILP  EQU LTEMP+4 
KILC  EQU LTEMP+10
* THE KILL ROUTINE IS USED TO DELETE A PROGRAM FROM THE USER
* LIBRARY. THE PROCESS IS AS FOLLOWS: 
*     1) CHECK FORMAT AND DETERMINE PROGRAM NAME. 
*     2) SEARCH DIRECTORY FOR ENTRY.
*     3) OBTAIN LOCATION AND LENGTH.
*     4) COLLAPSE DIRECTORY.
*     5) UPDATE ADT AND IDT.
      SPC 1 
KILL  LDA DRFLG    CHECK FOR $FILS
      SZA 
      JMP KILL0 
      LDA .-22
      LDB *+2 
      JMP LIBER 
      DEF *+1 
      OCT 5044
      ASC 10,FILS NOT INITIALIZED 
      SPC 1 
KILL0 LDA DLTEM 
      STA KILP
      LDA .-3       SET UP COUNTER. 
      STA KILC
KIL1  JSB LCHAR     GET LEFT CHAR.
      LDA .+40B     BLANK IF NONE.
      ALF,ALF 
      STA KILP,I
      JSB LCHAR     GET RIGHT CHAR. 
      LDA .+40B 
      IOR KILP,I
      STA KILP,I
      ISZ KILP      BUMP POINTER. 
      ISZ KILC      ANY MORE? 
      JMP KIL1      YES.
      LDA LTEMP 
      CPA ASCBB 
      JMP KIL2      NO CHARS--ERROR.
      JSB LCHAR     TEST FOR TOO MANY.
      JMP KIL3      O.K.
* 
KIL2  LDB *+3 
      LDA .-13
      JMP LIBER 
      DEF *+1 
      OCT 5111
      ASC 6,LLEGAL NAME 
* 
KIL3  EQU * 
      JSB DELEN 
      JMP KIL10     FOUND.
* 
      LDA .-14
      LDB *+2 
      JMP LIBER 
      DEF *+1 
      OCT 5116
      ASC 6,O SUCH ENTRY
KIL10 LDA LTEMP+11 FILE KILLED OR PROG.?
      SSA 
      JMP LLEND    PROGRAM
      JSB DCMPL    FILE.
      JMP LLEND 
      SPC 2 
DELEN NOP 
      JSB DLOOK    SEARCH DIRECTORY 
      JMP DLN0     FOUND
      ISZ DELEN    NOT FOUND
      JMP DELEN,I 
      SPC 1 
DLN0  STA LTEMP+6  SAVE T/S 
      STB DTEMP    SAVE DIRECTORY POINTER 
      LDA B,I      GET FILE LNTH. 
      STA LTEMP+11   AND SAVE 
      SSA          FILE OR PROG.? 
      JMP DLN6     PROGRAM
      RAL          FILE. GET # OF SECTORS 
DLN1  CMA,INA      MAKE NEGATIVE
      STA LTEMP+5    AND SAVE 
      LDA DCNT
      CPA DIREC+5  LAST ENTRY IN DIREC.?
      JMP DLN4     YES
      LDA B        NO 
      ADB .+4      B => NEXT ENTRY
      ADA .-4 
      STA DTEMP    DTEMP => HOLE
      CCA 
      STA LTEMP+14
DLN15 LDA .-3      SET COUNTER
      STA LTEMP+12
DLN2  LDA B,I      SHIFT REMAINING DIREC. ENTRIES 
      STA DTEMP,I 
      INB 
      ISZ DTEMP 
      ISZ LTEMP+12
      JMP DLN2
      LDA B,I      MODIFY REL. SECTOR # IN ENTRY
      ISZ LTEMP+14
      RSS 
      STA LTEMP+15
      ADA LTEMP+5 
      STA DTEMP,I 
      INB 
      ISZ DTEMP 
      LDA .-4 
      STA LTEMP+12
DLN3  LDA B,I 
      STA DTEMP,I 
      INB 
      ISZ DTEMP 
      ISZ LTEMP+12
      JMP DLN3
      ISZ DCNT
      LDA DCNT
      CPA DIREC+5 
      RSS 
      JMP DLN15 
      SPC 1 
      LDA LTEMP+6  GET T/S OF HOLE
      AND =B377 
      STA LTEMP+7  SAVE SECTOR #
      LDA LTEMP+6 
      ALF,ALF 
      AND =B377 
      STA LTEMP+6  SAVE TRACK # 
      LDA LTEMP+5  GET NEG. SIZE OF HOLE
      CMA,INA      MAKE POS.
      ADA LTEMP+7  ADD HOLE SECTOR #
      CLB 
      DIV SECTR    DIVIDE BY # SECTORS/TRACK
      STB LTEMP+9  SAVE SECTOR #
      ADA LTEMP+6  GET TRACK #
      STA LTEMP+8    AND SAVE 
      LDA LTEMP+15
      CMA,INA 
      ADA DIREC+4 
      CMA 
      STA DTEMP 
      JSB WDIRC    WEITR OUT DIREC. 
DLN9  JSB EXEC     READ A FILE SECTOR 
      DEF *+7 
      DEF .-1 
      DEF .103
      DEF DIREC 
      DEF D128
      DEF LTEMP+8 
      DEF LTEMP+9 
      SPC 1 
      JSB EXEC     WRITE FILE SECTPR TO HOLE
      DEF *+7 
      DEF .-2 
      DEF .103
      DEF DIREC 
      DEF D128
      DEF LTEMP+6 
      DEF LTEMP+7 
      SPC 1 
      LDA LTEMP+7  GET HOLE SECTOR #
      INA 
      CPA SECTR    END OF TRACK?
      JMP DLN10    YES
DLN95 STA LTEMP+7  NO 
      LDA LTEMP+9  GET FILE SECTOR #
      INA 
      CPA SECTR    END OF TRACK?
      JMP DLN11    YES
DLN96 STA LTEMP+9  NO 
      ISZ DTEMP    DONE?
      JMP DLN9     NO 
      JSB RDIRC    YES. READ DIRECTORY
      SPC 1 
DLN4  LDA DIREC+5 
      ADA .-1 
      STA DIREC+5 
      LDA LTEMP+5 
      ADA DIREC+4 
      STA DIREC+4 
      JSB WDIRC 
      JMP DELEN,I 
      SPC 1 
DLN6  CMA,INA 
      STA LTEMP+13
      AND =B177 
      STA B 
      LDA LTEMP+13
      AND =B177600
      ALF,ALF 
      RAL 
      SZB 
      INA 
      LDB DTEMP 
      JMP DLN1
      SPC 1 
DLN10 CLA 
      ISZ LTEMP+6 
      JMP DLN95 
DLN11 CLA 
      ISZ LTEMP+8 
      JMP DLN96 
      SPC 2 
WDIRC NOP 
      JSB EXEC
      DEF *+7 
      DEF .-2 
      DEF .103
      DEF DIREC 
      DEF D256
      DEF DTRAK 
      DEF DSECT 
      JMP WDIRC,I 
      SPC 1 
RDIRC NOP 
      JSB EXEC
      DEF *+7 
      DEF .-1 
      DEF .103
      DEF DIREC 
      DEF D256
      DEF DTRAK 
      DEF DSECT 
      JMP RDIRC,I 
      SPC 2 
.103  OCT 103 
D256  DEC 256 
SECTR EQU 116B
D128  DEC 128 
B     EQU 1 
      END KILL
