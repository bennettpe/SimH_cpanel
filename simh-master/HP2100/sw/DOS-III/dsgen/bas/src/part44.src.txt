ASMB,R,B,L,X
      NAM OPEN,5
      SPC 2 
      COM PBPTR,NEG(26),.(52),MAIN,SPROG,SYMTB,ERRCT
      COM SBPTR,SBUFA,SSTAK,SYNTQ,SERRS(36),RERRS(50) 
      COM FERRS(15),WERRS(10),MAXCR,MSFLG,PFLAG,SFLAG 
      COM DFLAG,TEMP(3),CDFLG,ARYAD,GFLAG,SIGN,UFLAG
      COM PINTG(2),TABLE,LNGTH,SMBGN,SLENG,COUNT
      COM TBLPT,TSPTR,SBT0
      COM EXP,MANT1,MANT2,EXPON,DPFLG,NT0,ENOUF 
      COM LNAME,CHRCT,OCTMP,SPTR,FILTB,NSPTR,INFST
      COM DCLC1,DEST,DCLC2,STEND,DFILT,FILPT,COMSN
      COM MVEND 
      COM STYPE,USESN,FILCT,.LNUM 
      COM PRGCT,STCT1,STCT2,NUMPT,STCT3,FLINK 
      COM STMP1,STMP2,VLFLG,VALTB,FCORE,COMPT,VTMP1 
      COM VTMP2,VTMP3,VTMP4,VTMP5,IFSS
      COM CU1,APTR,DCFLG,TEMP2,LT1,LT2,FCNTR,RTNST
      COM ASINP,RTRNQ,FORQ,TEMP1,FILE#
      COM INTMP,TMPST,TPRME,TNULL,PS1,DMY3,TEMP5
      COM PRIST,FBASE,RQ3,EOL,GTMP,FORST,EFN0,FVT 
      COM EFN1,EFN2,EFN3,MCNT,INITF,IFCNT,OPDST 
      COM FFLG,TEMP4,EORFL,NCH,TEMP6,STRLN,ATIM,LT5 
      COM ASTYP,ASBFP,ORDNO,ATMP(2),RETCD 
      COM DCCNT,NXTDT,VL0,RCRD#,PMASK,ITEMP 
      COM OPTRQ,EST3,EST2,EST1,TT1,TT2,DMY1 
      COM DMY2,TT3,TT4,CP0,CP1,PS0,MPT,NQT,RT0
      COM RT1,TRFCH,TRS0,A1,A2,C1,EOPF,NUMBF(6),FD0 
      COM OT1,OT2,OT3,LBTMP,REPCT,RNDX1,RNDX2 
      COM X2TMP(2),CTMP,DTMP(2),BTMP(2),XTEMP(2),YTEMP(2) 
      COM UTEMP(2),LT0,LT8,LT3,LT6,LT7,LT4,DIGCT
      COM NMPTR,NMTMP,FFLAG,DGCNT 
      COM IFSTR,EC,CC,DP,CONTR,CC1,CC2,PC1,PC2
      COM SFLG,FST,EFLAG,NUM1,NUM2,SBD,SAD,SNFLG
      COM NAD,NBD,FSP,EST,NUMW1,NUMW2,EXPW
      COM IHB,HBP,NHBW,NBLK,TOTDG,DCTR
      COM EDSTA,LCH,ELCNT 
      COM STPTR,RSPTR,TEMP3 
      COM MT1,MT0,MT3,MT2,MEXIT,DMY4,MOP,BS2,BS3
      COM MMT0,MMT1,MMT2,MMT6(2),MMT7(2),MMT3,MMT4,MMT5 
      COM MMT8(2),SCALR(2),ID0,ID1,MAXE(2),TOL(2),PIVEL 
      COM VT0,RD0 
      COM SOURC,TAP0,TAP1 
      COM STIME(5)
      COM LTEMP(16),LTYP1,LTYP2,LCHCR,MOVES,MOVED 
      COM ERSEC(77),DRFLG,DIREC(256),DTEMP,DCNT 
      COM DTRAK,DSECT 
      SPC 2 
      EXT LCHAR,OUTCH,ILFER,LIBER,DLOOK,EXEC
      EXT LLEND 
      SPC 2 
DLTEM DEF LTEMP 
M72B  OCT -72 
B400  OCT 400 
.140  OCT 140 
M64   DEC -64 
OM193 DEC -193
B     EQU 1 
.103  OCT 103 
D256  DEC 256 
SECTR EQU 116B
OPEDF EQU LTEMP+9 
OPEP  EQU LTEMP+10
OPEC  EQU LTEMP+11
OPEF  EQU LTEMP+8 
OPEDT EQU LTEMP+12
OPEDS EQU LTEMP+13
* THE OPEN COMMAND IS A USER FUNCTION WHICH CAUSES THE SYSTEM TO
* CREATE ROOM FOR A DATA FILE. THE FORMAT FOR THE COMMAND IS: 
* 
*     OPEN - FILENAME, FILE LENGTH, RECORD SIZE 
* 
* THE FILENAME IS SUBJECT TO ALL THE RULES OF PROGRAM NAMES. THE
* LENGTH IS A DECIMAL INTEGER FROM 1 TO 32767 INDICATING THE FILE 
* LENGTH IN RECORDS.  RECORD SIZE IS A DECIMAL INTEGER FROM 64 TO 
* 256 INDICATING THE RECORD SIZE IN WORDS.
      SPC 2 
OPEN  LDA DRFLG     GET DIRECTORY FLAG
      SZA           SET?
      JMP OPE       YES. CONTINUE 
      LDA .-22      NO. ERROR 
      LDB *+2 
      JMP LIBER 
      DEF *+1 
      OCT 5044
      ASC 10,FILS NOT INITIALIZED 
OPE   LDA DLTEM     SET POINTER FOR NAME
      ADA .-1 
      STA OPEP       NAME.
      LDA .-3       SET UP COUNTER. 
      STA OPEC
      CLA           SET FLAG SAYING FIRST 
      STA OPEF       CHAR.
OPE1  JSB OPER      GET A CHARACTER.
      ALF,ALF       STORE IN
      ISZ OPEP       NAME AREA. 
      STA OPEP,I
      JSB OPER      GET RIGHT CHAR. 
      IOR OPEP,I    MERGE IN. 
      STA OPEP,I
      ISZ OPEC      TEST FOR ANY MORE.
      JMP OPE1
* 
      JSB OPER      NEXT CHARACTER MUST BE
      CPA .+40B      BLANK. 
      JMP OPE2
      LDA .-14
      LDB *+2 
      JMP LIBER 
      DEF *+1 
      OCT 5116      LF-N
      ASC 6,AME TOO LONG
* 
OPER  NOP           GET NEXT CHAR FOR NAME. 
      LDA OPEF      TEST TO SEND BACK BLANK.
      CPA .+40B 
      JMP OPER,I
OPER1 JSB LCHAR     GET NEXT CHAR.
      JMP ILFER     FAIL IF NONE. 
      LDB 0         SAVE IN B.
      AND .140      SKIP CONTROL CHARS. 
      SZA 
      CPA .140
      JMP OPER1 
      LDA OPEF      GET 1ST CHAR. FLAG. 
      SZA           TEST FOR 1ST CHAR.
      JMP OPER2     NOT.
      CPB .+54B     TEST FOR COMMA OR $ 
      JMP ILFER 
      CPB .+44B 
      JMP OPE16 
      CPB .+52B     TEST FOR *
      JMP OPE16 
OPER3 LDA 1         RETURN CHAR IN B
      ISZ OPEF
      JMP OPER,I
OPER2 CPB .+54B     COMMA TEST
      RSS 
      JMP OPER3 
      LDA .+40B 
      STA OPEF
      JMP OPER,I
* 
OPE16 LDA .-24
      LDB *+2 
      JMP LIBER 
      DEF *+1 
      OCT 5111      LF-I
      ASC 11,LLEGAL FIRST CHARACTER 
* 
* GET FILE LENGTH.
* 
OPE2  CLA           INITIALIZE # TO 0 
      STA OPEF
      JSB LCHAR     GET A CHAR. 
      JMP OPE51     DONE
      CPA .+54B     IS IT A COMMA?
      JMP OPE50     YES, GO GET RECORD SIZE 
      ADA M72B      DIGIT TEST. 
      SSA,RSS 
      JMP ILFER 
      ADA .+10
      SSA 
      JMP ILFER 
      STA OPEC      SAVE DIGIT. 
      LDA OPEF
      JSB OPESB 
      JMP OPE2+1
* 
* GET RECORD SIZE 
* 
OPE50 CLA           INITIALIZE SIZE TO 0
      STA OPEP
      JSB LCHAR     GET A CHARACTER 
      JMP OPE3      DONE
      ADA M72B      DIGIT TEST
      SSA,RSS 
      JMP ILFER 
      ADA .+10
      SSA 
      JMP ILFER 
      STA OPEC      SAVE DIGIT
      LDA OPEP      BUILD 
      JSB OPESB 
      JMP OPE50+1   GET NEXT DIGIT
* 
OPE51 LDA B400      DEFAULT RECORD SIZE 
      STA OPEP        IS 256 WORDS
OPE3  LDA OPEF      TEST FOR LEGAL VALUE. 
      SSA,RSS 
      SZA,RSS 
      JMP ILFER     ERROR IF ZERO OR NEGATIVE 
      RAL           DOUBLE # OF RECORDS 
      STA OPEF        AND SAVE # OF SECTORS 
      LDA OPEP      GET RECORD SIZE 
      ADA M64 
      SSA           LESS THAN 64? 
      JMP ILFER     YES, AND THAT'S NOT PLAYING 
*                                     BY THE RULES. 
      ADA OM193 
      SSA,RSS       GREATER THAN 256? 
      JMP ILFER     THAT'S EQUALLY A NO-NO
* 
      LDB DIREC+4 
      ADB OPEF
      CMB 
      ADB DIREC+3 
      SSB,RSS 
      JMP OPE4
* 
      LDB *+3 
OPE7  LDA .-19
      JMP LIBER 
      DEF *+1 
      OCT 5114      LF-L
      ASC 9,IBRARY SPACE FULL 
OPE4  LDA DIREC+5 
      CPA .+32
      RSS 
      JMP OPE5
      LDA .-15
      LDB *+2 
      JMP LIBER 
      DEF *+1 
      OCT 5104
      ASC 7,IRECTORY FULL 
      SPC 1 
OPE5  JSB DLOOK     SEARCH DIRECTORY
      JMP OPE8
      ISZ DIREC+5 
      LDA DIREC+4 
      STA OPEDF 
      ADA OPEF
      STA DIREC+4 
      LDA LTEMP 
      STA B,I 
      INB 
      LDA LTEMP+1 
      STA B,I 
      INB 
      LDA LTEMP+2 
      STA B,I 
      INB 
      LDA OPEDF 
      INA 
      STA B,I 
      INB 
      LDA OPEF
      RAR 
      STA B,I 
      INB 
      LDA OPEP
      STA B,I 
      INB 
      CLA 
      STA B,I 
      INB 
      STA B,I 
      SPC 1 
      JSB EXEC
      DEF *+7 
      DEF .-2 
      DEF .103
      DEF DIREC 
      DEF D256
      DEF DTRAK 
      DEF DSECT 
      SPC 1 
      LDA OPEDF 
      INA 
      ADA DSECT 
      CLB 
      DIV SECTR 
      ADA DTRAK 
      STA OPEDT 
      STB OPEDS 
      SPC 1 
      LDA OPEF
      RAR 
      CMA,INA 
      STA OPEF
      SPC 1 
OPE13 JSB EXEC
      DEF *+7 
      DEF .-2 
      DEF .103
      DEF .-1 
      DEF .+1 
      DEF OPEDT 
      DEF OPEDS 
      ISZ OPEF
      RSS 
      JMP LLEND 
      LDA OPEDS 
      ADA .+2 
      CLB 
      DIV SECTR 
      ADA OPEDT 
      STA OPEDT 
      STB OPEDS 
      JMP OPE13 
* 
OPESB NOP 
      MPY .+10      BUILD NEW 
      CLE            NUMBER.
      ADA OPEC
      SEZ,SZB,RSS   OVERFLOW CHECK. 
      JMP OPESB,I   EXIT
      JMP ILFER 
* 
OPE8  EQU * 
      LDA .-16
      LDB *+2 
      JMP LIBER 
      DEF *+1 
      OCT 5104
      ASC 7,UPLICATE ENTRY
      END OPEN
