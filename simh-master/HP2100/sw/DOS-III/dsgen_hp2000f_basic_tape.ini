
; Loads HP 2000F BASIC for DOS-III from mag tape
; called by dsgen.ini script

goto start_bas_module_load

:att_file
echo
echo ---> Attach %1
att ptr -q dsgen/bas/%1
return

:type
; simulate typing on tty. 
; %1 (parameter one) is the text to type 
send '%1'
send "\r"
go until "\n"
return

:typecmd
; simulate typing on tty command for DOS. 
; %1 (parameter one) is the text to type 
send "%1"
send "\r"
go until "@"
go until "\n"
return

:start_bas_module_load

call typecmd ":JOB"
call typecmd ":LIST,U,1"
call typecmd ":CLEAR"

echo
echo ---> mount HP2000F BASIC mag tape to load rel programs to disc
; -r -> read only
att msc -q -r dsgen/bas/hp2000f_basic_dosIII.tape
; store rel files in disc from magtape
send ":STORE,R,BCFIL,8\r"
go until "DONE?"
go until "\n"
send "YES\r"
go until "@"
go until "\n"
send ":STORE,R,HP2KF,8\r"
go until "DONE?"
send "YES\r"
go until "@"
go until "\n"

det -q msc

; relocate file from disc to create a executable program
; must give the name of file as it is not in the job source work area
; as the filename is not the main progam name, we must indicate the
; programs to relocate that are contained in the file: BFILE and CFILE
send ":PROG,LOADR,2\r"
go until "ENTER FILE NAME(S) OR /E" 
send "BCFIL(BFILE,CFILE),/E\r"
go until "@"
; there is no "\n" after this @!!!

; Saves all the relocated files that are in binary area: BFILE and CFILE
call typecmd ":STORE,P"

; the relocatable file no longer needed, delete it
call typecmd ":PURGE,BCFIL"

; create file that holds basic filesystem and init it with BFILE
call typecmd ":ST,B,$FILS,50"

send ":PR,BFILE\r"
go until "@"
; there is no "\n" after this @!!!

call typecmd ":EJOB"
call typecmd ":JOB"
call typecmd ":LIST,U,1"
call typecmd ":CLEAR"

; Now load basic special relocatable loader (defaul DOS one
; gives an base page overflow error because program is too big)
; LOADR,5 <- Local Unit 5=papertape reader
; there is no "\n" after this @!!! on commands
call att_file 22608-60001_C_LOADR.rel
send "\r:PROG,LOADR,5\r"
go until "ENTER FILE NAME(S) OR /E" 
send "/E\r" 
go until "@"
send ":GO\r"
go until "@"
send ":UP,2\r"
go until "@"
send ":GO,1\r"
go until "@"

; now run it to relocat basic. LU=2 to get file from disk
send ":PROG,LOADR,2,1,0,1\r"
go until "ENTER FILE NAME(S) OR /E" 
send "HP2KF,/E\r"
go until "@"

; saves the relocated basic. name can be given or not
; send ":STORE,P,BASIC\r"
call typecmd ":STORE,P"
call typecmd ":PURGE,HP2KF"

; now erase unneeded LOADR file from disk. (if not we will
; still using BASIC LOADR instead of system one)

call typecmd ":PURGE,LOADR"
call typecmd ":EJOB"

; Now load program
call typecmd ":JOB"
call typecmd ":LIST,U,1"

; call att_file TREK73.BAS.txt

send ":RUN,BASIC\r"

go until "READY"
go until "\n" 

set tty noautolf
  
call type '10 PRINT "HP2000F BASIC FOR DOS-III HOLA"'
call type '20 END'
call type 'LIST'
go until     "END"
go until     "\n"
call type 'RUN'
go until     "DONE"
go until     "\n"
call type 'NAME-HOLA'
call type 'SAVE'
call type 'CAT'
go until     "NAME"
go until     "HOLA"
go until     "\n"
call type 'SCRATCH'
call type 'LIST'
call type 'GET-HOLA'
call type 'RUN'
go until     "DONE"
go until     "\n"
call type 'KILL-HOLA'
call type 'CAT'
go until     "NAME"
go until "\n"
go until "\n"

set tty autolf

call type 'BYE'
go until "@"

call typecmd ":EJOB"

