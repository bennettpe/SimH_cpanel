ASMB,R,B,L
      NAM PTLIB,7
      ENT PLTLU,PLOT,PLTC,PLTCS
      EXT .ENTR,EXEC
*
* BY ROBERTO SANCHO MAY 2017
* 
* 
*         PLTLU ENTRY POINT 
* 
* 
.LU   BSS 1         LU FOR PLOTTER
PTNUM BSS 1 
PLTLU NOP         
      JSB .ENTR   
      DEF .LU
      LDA .LU,I     LOAD LU GIVEN AS PARAM
      AND B77
      STA LU        STORE TO BE USED BY OTHER ROUTINES
      JMP PLTLU,I   RETURN TO CALLING PROGRAM 
* 
* 
*         PLOT ENTRY POINT 
* 
* 
.PC   BSS 1        PEN COMMAND: -1 (PEN UP), 0 (POINTS), 1 (PEN DOWN)
.MC   BSS 1        MODE COMMAND: RELATIVE (-1) OR ABS COORDS (1)
.IX   BSS 1        X COORD
.IY   BSS 1        Y COORD
PLOT  NOP         
      JSB .ENTR   
      DEF .PC 
      LDA LU
      IOR B100      
      STA CNWD1     BIT7=1 IN EXEC CALL CONTROL WORD -> POSITION CALL
      LDA .PC,I     LOAD PEN COMMAND GIVEN AS PARAM
      STA BPC
      LDA .MC,I     LOAD MODE COMMAND GIVEN AS PARAM
      STA BMC
      LDA .IX,I     LOAD COORD GIVEN AS PARAM
      STA BIX
      LDA .IY,I     LOAD COORD GIVEN AS PARAM
      STA BIY
      JSB EXEC      SEND BUFFER TO DRIVER
       DEF *+5      
       DEF D2 
       DEF CNWD1
       DEF BUF1
       DEF BFLN1
      JMP PLOT,I   RETURN TO CALLING PROGRAM 
CNWD1 BSS 1        CONTROL WORD: 0001LU
BFLN1 DEC 5        BUFFER LEN: 5 WORDS
BUF1  DEC 0        FIRST WORD OF BUFFER: 0 (IS IGNORED)
BPC   BSS 1        SECOND WORD OF BUFFER: PC COMMAND
BMC   BSS 1        THIRD WORD OF BUFFER: MC COMMAND
BIX   BSS 1        FOURTH WORD OF BUFFER: X COORD
BIY   BSS 1        FIFTH WORD OF BUFFER: Y COORD
* 
* 
*         PLTC ENTRY POINT 
* 
* 
.IXX  BSS 1        BOX FOR PLOTTED CHAR
.IXY  BSS 1         
.IYX  BSS 1
.IYY  BSS 1
.CH   BSS 1        ASCII CODE OF PLOTTED CHARACTER
PLTC  NOP         
      JSB .ENTR   
      DEF .IXX
      LDA .IXX,I   LOAD BOX DATA
      STA IXX       STORE VALUES TO INITIALIZE
      LDA .IXY,I     LETTERING EXEC CALL 
      STA IXY         
      LDA .IYX,I      
      STA IYX      
      LDA .IYY,I   
      STA IYY      
      LDA LU       INIT CONTROL WORD
      STA CNWD2      BIT7=0  -> LETTERING CALL 
      LDA M1       -1 IS INITAL VALUE FOR 1ST WORD OF BUF
      STA BUF2     
      LDA .CH,I    LOAD ASCII VALUE FOR CHAR TO PLOT
      SSA          IS CH<0?
      JMP PLTC,I    YES, CHAR TO PLOT IS NEGATIVE -> RETURN TO CALLING PROGRAM 
      AND B377     ISOLATE CHAR
      ALF,ALF      LEFT JUSTIFY CHAR INTO WORD
      IOR BACCD    ADD BACK ARROW AS TERMINATING CHAR
      STA CH
      LDA M12      PLOTTING TWO CHARS: GIVEN ONE AND BACK ARROW 
      STA BFLN2     TO AVOID SIMULATED CR 
      JSB EXEC     DO THE LETTERING CALL
       DEF *+5      
       DEF D2 
       DEF CNWD2
       DEF BUF2
       DEF BFLN2
      JMP PLTC,I  RETURN TO CALLING PROGRAM 
CNWD2 BSS 1
BFLN2 BSS 1        1 CHARACTER LEN BUFFER (IBFLN = -10 - N  CHARACTERS)
BUF2  BSS 1        LETTERING CALL, BINARY SIZE SPEC USAGE
IXX   BSS 1
IXY   BSS 1
IYX   BSS 1
IYY   BSS 1
CH    BSS 1
* 
* 
*         PLTCS ENTRY POINT 
* 
* 
.IASC BSS 1        BASIC ASCII STRING TO PLOT
PLTCS NOP         
      JSB .ENTR   
      DEF .IASC
      LDA .IASC,I  GET BASIC STRING LEN
      AND B377     ISOLATE 
      SZA,RSS      IS LEN=0?
      JMP PLTCS,I   YES, RETURN TO CALLING PROGRAM
      CMA,INA      MAKE STR LEN NEGATIVE 
      STA IALEN
      ISZ .IASC    SKIP 1ST WORD (THE LEN) IN IASC
      CLA,INA
      STA HLCH     INIT HI-LO PACK CHAR EXTRACTION FLAG
*                  MAIN PRINT CHAR LOOP
SL1   LDA .IASC,I  LOAD PACKED CHARS FROM STRING
      ISZ HLCH
      LDB HLCH     GET REMAINING LEN TO PRINT 
      SLB          IS EVEN?
      JMP SL1B      YES, CHAR IS ALREADY IN LOW BITS OF WORD
      ALF,ALF       NO, CHAR IS IN HI BITS OF WORD. ROTATE TO PUT THEM IN LO BITS
      JMP SL1C
SL1B  ISZ .IASC    BUMP STR ADDR
SL1C  AND B377     ISOLATE CHAR
      ALF,ALF     
      IOR BACCD    ADD BACK ARROW TO AVOID SIMULATED CR 
      STA CH       CH HAS THE STR CHAR IN HI BITS
*                  CHECK IF LAST CHAR TO PLOT
      LDA M12      SET 2 PACKED CHARS TO PRINT
      STA BFLN2
      LDA IALEN    GET REMAINING LEN TO PRINT 
      CPA M1        =-1?
      RSS
      JMP SL2      NO, OK TO PRINT 2 CHARS, GO FOR IT
      LDA M11      YES, SET ONLY 1 CHAR TO PRINT (DISCARD ADDED BACCD CHAR)
      STA BFLN2
SL2   JSB EXEC     DO THE LETTERING CALL
       DEF *+5      
       DEF D2 
       DEF CNWD2
       DEF BUF2
       DEF BFLN2
      LDA CNWD2    MARK NEXT CALL WITH BIT8=1 IN EXEC CALL 
      IOR B1000    CONTROL WORD -> CONTINUATION LETTERING CALL 
      STA CNWD2    
      ISZ IALEN    INC LEN 
      JMP SL1      LEN<>0 -> STILL CHARS REMAINS 
      JMP PLTCS,I  LEN=0 -> RETURN TO CALLING PROGRAM
*
IALEN BSS 1
LU    DEC 0
HLCH  BSS 1
*
B377  OCT 377
B100  OCT 100
B1000 OCT 1000
B77   OCT 77
D2    DEC 2
M11   DEC -11
M12   DEC -12
M1    DEC -1       
BACCD OCT 000137    BACK ARROW CHAR CODE
      END 
