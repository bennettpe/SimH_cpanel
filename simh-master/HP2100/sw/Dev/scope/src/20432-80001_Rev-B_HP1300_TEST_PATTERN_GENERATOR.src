      HED 1300 TEST PATTERN GENERATOR 12/4/70 
* 
* 
*TEST PATTERN GENERATOR FOR HP-1300 SCOPE INTERFACE 
* 
*STARTING OCTAL ADDRESS = 100B
* 
*CONVERTER INTERFACE CARD AND PUSH RUN ON COMPUTER
* 
* 
* 
* 
*SET SWITCH REGISTER TO OCTAL I/O LOCATION OF D/A 
*THE FOLLOWING SWITCH REGISTER SETTINGS 
*ARE USED FOR PROGRAM CONTROL AFTER COMPUTER HALTS
*WITH "T" REGISTER = 102033 
* 
* 
*   BIT 0 = 1 --ALIGNMENT TEST PATTERN IS DISPLAYED.
*               THE HP 1300 SCOPE IS CALIBRATED 
*               AND ALIGNED FOR FULL SCALE WITH 
*               THIS TEST PATTERN.
*               HP 1300 SCOPE INTERFACE SPECS AND 
*               ALIGNMENT ARE CHECKED IN THIS MODE. 
* 
*   BIT 1 = 1 --VERTICAL AND HORIZONAL TEST PATTERN 
*               IS DISPLAYED UNDER INTERRUPT. 
* 
*   BIT 2 = 1 --ALIGNMENT TEST PATTERN IS DISPLAYED 
*               UNDER DMA CONTROL(IF DMA OPTION IS
*               IN COMPUTER). 
* 
* 
*MAIN PROGRAM 
* 
      ORG 100B
      CLC 0 
* 
*INITIALIZE I/O LOC & TRAP CELL 
* 
      LIA 1B       I/O LOC FROM SW. REG.
      AND MASK
      STA LOC 
      XOR OTA 
      STA OTA2
      STA OTA1
      LDA LOC 
      XOR STCC
      STA STC1
      LDA LOC 
      STA CW1      INITIALIZE DMA CONTROL WORD
      LDA LOC       INITIALIZE
      XOR STA       INTERRUPT 
      STA STA1         TRAP CELL
      LDA TRAP           TO 
STA1  STA 0                JUMP CHECK 
* 
*LOAD BUFFER WITH POINTS FOR TEST PATTERN 
* 
      LDA SAVE     FIRST ADDRESS OF BUFFER
      STA STORE 
* 
*   GENERATE VERTICAL & HORIZONTAL MARKS
* 
      LDA VHCNT 
      STA VH       COUNTER
      LDA VMAR     INITIALIZE 
      STA MARK       VERTICAL 
      LDA YLSB         MARKS
      STA MARKI          PARAMETERS 
      LDA VMARI 
      STA NEXTM    NEXT MARK
LOOP5 LDA MC       8 MARKS PER LINE 
      STA MCNT     MARK COUNTER 
LOOP4 LDB PC       POINT COUNTER
      LDA MARK     FIRST POINT IN MARK
LOOP3 STA STORE,I  PUT POINT IN BUFFER
      ADA MARKI    NEXT POINT IN MARK 
      ISZ STORE    BUMP ADDRESS 
      ISZ 1        BUMP COUNTER 
      JMP LOOP3    GENERATE NEXT POINT
      LDA MARK     INITIALIZE 
      ADA NEXTM      FIRST POINT
      STA MARK          OF NEXT MARK
      ISZ MCNT     MARKS COMPLETE?
      JMP LOOP4    NO 
      LDA HMAR     INITIALIZE 
      STA MARK       PARAMETERS 
      LDA HMARI         FOR 
      STA NEXTM         GENERATING
      LDA XLSB             HORIZONTAL 
      STA MARKI              MARKS
      ISZ VH       VERT.& HOR MARKS COMPLETE? 
      JMP LOOP5    NO 
* 
*   GENERATE DIAGONAL LINE (CENTER OUT) 
* 
      LDA C127
      STA VH       DIAGONAL COUNTER 
      LDA X5Y5     COUNTER UPPER LEFT HALF
      LDB X5Y5     COUNTER LOWER RIGHT HALF 
LOOP6 ADA YLSB     INCREMENT Y
      CMA,INA 
      CMA          DECREMENT X
      STA STORE,I  STORE UP POINT 
      ISZ STORE    BUMP ADDRESS 
      ADB MYLSB    DECREMENT Y
      INB          INCREMENT X
      STB STORE,I  STORE DOWN POINT 
      ISZ STORE    BUMP BUFFER ADDRESS
      ISZ VH       DIAGONAL POINTS COMPLETE?
      JMP LOOP6    NO 
* 
*   GENERATE DIAGONAL LINE (TOP OF BOTTOM LEFT) 
* 
      LDB C255     COUNTER
      LDA X9Y9     UPPER RIGHT POINT
LOOP7 STA STORE,I  PUT POINT IN BUFFER
      ADA XYLSB    X AXIS-1,Y AXIS-1
      ISZ STORE    BUMP BUFFER ADDRESS
      ISZ 1B       ALL POINTS STORED? 
      JMP LOOP7    NO 
* 
*   GENERATE VERTICAL LINE POINTS 
* 
      LDB C256     POINT COUNTER
      LDA X5Y0     FIRST VERT. POINT
LOOP1 STA STORE,I  PUT POINT IN BUFFER
      ADA YLSB     NEXT POINT 
      ISZ STORE    BUMP BUFFER ADDRESS
      ISZ 1B       ALL POINTS STORED? 
      JMP LOOP1    NO 
* 
*   GENERATE HORIZONTAL LINE POINTS 
* 
      LDB C256     POINT COUNTER
      LDA X0Y5     FIRST HOR. POINT 
LOOP2 STA STORE,I  PUT POINT IN BUFFER
      INA          NEXT POINT 
      ISZ STORE    BUMP BUFFER ADDRESS
      ISZ 1B       ALL POINTS STORED? 
      JMP LOOP2    NO 
      LDA LAST     LAST POINT = 000000
      STA STORE,I  STORE IN END OF BUFFER 
* 
*   ALL POINTS FOR TEST PATTERN 
*   ARE NOW STORED IN BUFFER
* 
      HLT 33B      STOP TO EXERSIZE OPTIONS 
CHECK LIA 1B       CHECK REGISTER BIT "0" 
      SLA          TEST BIT 0 
      JMP ALIGN     TEST PATTERN SELECT 
      ARS 
      SLA           CHECK SWITCH BIT 1
      JMP START    VER AND HOR LINES SELECTED 
      ARS 
      SLA          CHECK SWITCH BIT 2 
      JMP DMA2     DMA DRIVER SELECTED
* 
*ALIGNMENT TEST PATTERN DRIVER
*   VERIFY THAT ALIGNMENT IS PROPER BY OBSERVING
*   TEST PATTERN DISPLAYED ON HP1300 SCOPE
* 
ALIGN CLC 0,C      NON-INTERRUPT
      LDB SAVE     TEST PATTERN BUFFER ADDRESS
LOOP9 LDA 1,I      GET DATA POINTS FROM BUFFER
OTA2  OTA 0        OUTPUT DATA TO SCOPE 
      INB          BUMP BUFFER ADDRESS
      SZA          TEST FOR LAST WORD (0) 
      JMP LOOP9    NEXT POINT 
      JMP CHECK    CHECK OPTION BEFORE NEXT OUTPUT
* 
*VERTICAL AND HORIZONTAL LINE TEST PATTERN DRIVER 
*   DISPLAYING UNDER INTERRUPT
* 
START STF 0 
STC1  STC 0,C     START REFRESH TIMER 
      LDB SAVE     BUFFER ADDRESS 
      ADB VHO      START BUFFER AT HOR & VERT LINES 
LOOP8 LDA 1,I      DATA POINT 
OTA1  OTA 0        OUTPUT POINT TO SCOPE
      INB          BUMP BUFFER ADDRESS
      SZA          TEST FOR LAST WORD(0)
      JMP LOOP8    NEXT POINT 
      JMP *        WAIT FOR INTERRUPT 
* 
*ALIGHMENT TEST PATTERN DRIVER
*   OPERATING UNDER DMA CONTROL 
* 
DMAC1 EQU 2 
DMAD1 EQU 6 
DMA2  CLC 0,C      NON-INTERRUPT
      LDA CW1      STC, CLC AND PACK DISABLED;D/A LOC 
      OTA DMAD1    OUTPUT CW1 TO DMA 1
      CLC DMAC1    PREPARE MEMORY ADDRESS REGISTER
      LDA CW2      MEMORY OUTPUT AND BUFFER START 
      OTA DMAC1    OUTPUT CW2 TO DMA 1
      STC DMAC1    PREPARE WORD COUNT REGISTER
      LDA CW3      2'S COMPLEMENT OF BUFFER LENGTH
      OTA DMAC1    OUTPUT CW3 TO DMA 1
      STC DMAD1,C  TURN ON DMA 1
      SFS DMAD1    DMA 1 OPERATION COMPLETE ? 
      JMP *-1        NO 
      CLC DMAD1    SHUT OFF DMA 1 
      JMP CHECK    OPTION CHECK BEFORE NEXT OUTPUT
* 
*PROGRAM CONSTANTS,COUNTERS,AND BUFFER STORAGE
* 
CW1   NOP          DMA CONTROL WORD 
CW2   OCT 346      DMA CONTROL WORD (MEMORY ADD.) 
CW3   OCT 175522   DMA CONTROL WORD (WORD CNT.) 
OTA   OCT 102600
STCC  OCT 103700
STA  OCT 070000 
C127  DEC -127
C255  DEC -255
C256  DEC -256
X5Y0  OCT 000200
X0Y5  OCT 100000   CENTER LEFT POINT
YLSB  OCT 000400
VMARI OCT 000040  SPACING OF VERTICAL MARKS 
VMAR  OCT 075400   FIRST POINT IN VERT MARK 
MARK  NOP 
MARKI NOP 
MCNT  NOP          MARK COUNTER 
PCNT  NOP         POINT COUNTER 
MC    DEC -8          # MARKS PER LINE
PC    DEC -11      POINTS PER MARK
HMAR  OCT 000173   FIRST POINT IN HOR. MARK 
HMARI OCT 020000
VHCNT DEC -2
VH    NOP 
X5Y5  OCT 100200    CENTER POINT
MYLSB OCT -400
XYLSB OCT -000401 
X9Y9  OCT 177777    UPPER RIGHT POINT 
LAST  OCT 0        ZERO POINT(LOWER LEFT POINT) 
VHO   DEC 686 
TRAP  JMP CHECK 
MASK  OCT 77
LOC   OCT 0        I/O LOCATION CONTANT 
NEXTM NOP 
XLSB  OCT 1 
* 
*BUFFER STORAGE FOR DATA POINTS 
* 
*BUFFER STORAGE ALLOCATIONS 
*   1) MARKS=OCT 315 TO 574 
*   2) DIAGONAL(INSIDE OUT)=OCT 575 TO 1172 
*   3) DIAGONAL(TOP TO BOT LEFT)=OCT 1173 TO 1571 
*   4) VERTICAL LINE=OCT 1572 TO 2171 
*   5) HORIZONAL LINE=OCT 2172 TO 2571
*   6)  1  LOCATION FOR LAST POINT(000000)=OCT 2572 
* 
STORE DEF BUFF        BUFFER ADDRESS COUNTER
SAVE  DEF BUFF     SAVE FIRST ADDRESS OF BUFFER 
BUFF  BSS 1199     DATA POINT STORAGE 
      END 
