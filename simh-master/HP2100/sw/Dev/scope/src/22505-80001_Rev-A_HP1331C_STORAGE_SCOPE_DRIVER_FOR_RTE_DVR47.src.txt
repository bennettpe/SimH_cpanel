ASMB,R,B,L
      HED <1331C STORAGE SCOPE RTE DRIVER - WR> 
      NAM DVR47 
      ENT I.47,C.47 
* 
**
**     THIS DRIVER FOR A 1331C STORAGE OSCILLOSCOPE DOES NOT
**   WORK UNDER INTERRUPT. IN ORDER NOT TO BLOCK HIGHER PRIORITY
**   DEVICES, KEEP THE BUFFER LENGTH SMALL !
**
**   WRITE :
**      JSB EXEC
**      DEF *+5 
**      DEF RECOD    -> = DEC 2 
**      DEF IFUN     -> = LOGICAL UNIT NUMBER 
**      DEF BUF      -> = BUFFER WHICH CONTAINS PACKED DATA (X&Y) 
**      DEF LEN      -> = LENGTH (POS = WORDS, NEG = CHARS) 
**       <- NORMAL RETURN 
**
**   ERASE :
**      JSB EXEC
**      DEF *+3 
**      DEF RECOD    -> = DEC 3 (CONTROL REQUEST) 
**      DEF IFUN    -> = OCT 20XX  (XX = LU#) 
**       <- NORMAL RETURN 
**   THE PROGRAM MUST SUSPEND ITSELF DURING 1 SECOND, SO THAT THE 
**   ERASE CAN BE DONE. 
**
**                                WIM ROELANDTS, HP BRUSSELS
**
* 
I.47  NOP 
* 
** CONFIGURE THE DRIVER 
* 
      IOR COTA      MAKE OTA
      STA OTA1
      ADA B300      MAKE CLF
      STA CLF1
* 
      LDA EQT6,I    GET REQUEST CODE
      AND B77 
      CPA D2        WRITE ? 
      JMP WRIT      YES, GO DO IT 
      CPA D3        CONTROL (ERASE)?
      JMP CONTR 
      CLA,INA       NONE OF THESE, REJECT 
      JMP I.47,I
* 
** DO OUTPUT
* 
WRIT  LDA EQT7,I    GET BUFFER ADDRESS
      STA POIN      SAVE IN POINTER 
      LDA EQT8,I    GET LENGTH
      SZA,RSS       ZERO ?
      JMP FIN       YES, TERMINATE
      SSA,RSS       WORDS OR CHARS ?
      CMA,INA,RSS   WORDS, MAKE NEGATIF 
      ARS           CHARS, DIVIDE BY 2
      STA LEN       SAVE AS COUNTER 
* 
GO    LDA POIN,I    GET A DATA
      ISZ POIN      BUMP POINTER
OTA1  OTA 0         GET IT OUT
      ISZ LEN       ALL DONE ?
      JMP GO        NO, CONTINUE
FIN   LDB EQT8,I    YES, TERMINATE
      SSB,RSS       MAKE LOG
      JMP *+3 
      BRS 
      CMB,INB 
RET4  LDA D4        DO IMMEDIATE COMPLETION 
      JMP I.47,I
      SKP 
* 
** DO ERASE 
* 
CONTR LDA EQT6,I    GET RQUEST CODE 
      AND B177H     ISOLATE FUNCTION
      CPA B2000     FOR THIS ONE ?
      JMP CLF1      YES, GO DO IT 
      LDA D2        NO, REJECT
      JMP I.47,I
* 
CLF1  CLF 0 
      CLB 
      JMP RET4
      SPC 2 
* 
** DUMMY CONTINUATOR
* 
C.47  NOP 
      CLA,INA 
      JMP C.47,I
      SKP 
* 
**     ***   D A T A   ***
* 
D2    DEC 2 
D3    DEC 3 
D4    DEC 4 
B77   OCT 77
B300  OCT 300 
B177H OCT 17700 
B2000 OCT 2000
COTA  OTA 0 
POIN  NOP 
LEN   NOP 
* 
EQT6  EQU 1665B 
EQT7  EQU 1666B 
EQT8  EQU 1667B 
      END 
