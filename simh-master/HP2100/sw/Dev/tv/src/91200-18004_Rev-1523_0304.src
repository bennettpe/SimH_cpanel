****************************************************************
*                                                              *
*              -- GAIN CHECK ROUTINE FOR TV --                 *
*                                                              *
****************************************************************
      SPC 1 
* THE GAIN SUBROUTINE GENERATES A RECTANGLE 
* AND ARROWS TO TEST THE HEIGHT AND WIDTH ON THE TV 
      SPC 1 
GAIN  JSB ERASE     ERASE ENTIRE SCREEN 
      DEF *+1 
      JSB VIDLU     SET WRITE MODE
      DEF *+2 
      DEF LUTV
      SPC 1 
      LDA ADD       SAVE POSITION REDUCER INST. 
      STA ADDX
      LDB MTEST     GET AMERICAN SCAN INDICATOR 
      LDA X         GET Y = 256 
      SZB           AMERICAN SCAN?
      LDA YAMER       YES - GET Y = 240 
      STA Y         SET VERTICAL DIMENSION
      LDA SA0       GET RECT 1 Y FOR NON-AMER.
      SZB           AMERICAN SCAN?
      ADA STADR       YES - Y = Y - 8 
      STA S0        SET VERTICAL POSITION 1 
      LDA SA1       GET RECT 2 Y FOR NON-AMER 
      SZB           AMERICAN SCAN?
      ADA STADR       YES - Y = Y - 8 
      STA S1        SET VERTICAL POSITION 2 
      CLA           GET NOP FOR ARROW POS. REDUCER
      SZB           AMERICAN SCAN?
      LDA ADDX        YES - GET INST. FOR POS. RED. 
      STA ADD       SET ARROW POSITION REDUCER
      SPC 1 
      JSB SQUAR     GO TO SQUAR SBR TO DRAW A RECT. 
      OCT 0 
Y     DEC 256        VERT. DIM. OF RECT.
X     DEC 256        HORIZ. DIM. OF RECT. 
      SPC 1 
      LDA SYMBL     GET NUMBER OF DOTS IN SYMBOL
      STA DOTC      PUT IN DOT COUNTER
      LDA POSIT     GET NUMBER OF POSITIONS 
      STA POSC      PUT IN POSITION COUNTER 
      LDA LOOPS     GET NUMBER OF LOOPS (ARROWS)
      STA LOOPC     PUT IN LOOP COUNTER 
      LDA ARW1S     GET START OF SYMBOL 
      STA ARW1        AND SAVE
      LDA ARW1T     GET START OF COMMON POSITION
      STA ARW1P       AND SAVE
      SKP 
****************************************************************
*                                                              *
*              -- GAIN ROUTINE CONTINUED --                    *
*                                                              *
****************************************************************
      SPC 1 
HEAD1 LDA BFAD      SET BUFFER
      STA PNTR        POINTER 
      STA A1
HEAD0 LDA ARW1,I    GET DOT 
      LDB ARW1P,I   GET POSITION
      ADA B         ADD DOT TO POSITION 
ADD   ADA STADR     REDUCE Y BY 8 FOR AMER SCAN ONLY
      STA PNTR,I    STORE POINT 
      ISZ PNTR        IN OUTPUT BUFFER
      ISZ DOTC      INCREMENT DOT COUNTER 
      JMP HEADA     SKIP
      JSB POINT     OUTPUT THE
      DEF *+3         POINT BUFFER
A1    NOP 
      DEF D12 
      JMP HEAD2     GET NEXT POSITION 
HEADA ISZ ARW1      INCREMENT DOT POINTER 
      JMP HEAD0     SET UP NEXT DOT 
      SPC 1 
HEAD2 LDA SYMBL     GET -12 
      STA DOTC      PUT IN DOT COUNTER
      ISZ POSC      INCREMENT POSITION
      JMP *+2       ALL 5 NOT DRAWN 
      JMP HEAD3     RESTORE POSC AND TEST LOOPC 
      LDA ARW1
      ADA ND11
      STA ARW1
      ISZ ARW1P 
      JMP HEAD1 
      SKP 
*************************************************************** 
*                                                             * 
*              -- GAIN ROUTINE CONTINUED --                   * 
*                                                             * 
*************************************************************** 
      SPC 1 
HEAD3 LDA POSIT     GET -5
      STA POSC      PUT IN POSITION COUNTER 
      ISZ LOOPC     ARE ALL SYMBOLS DRAWN 
      JMP *+2       ALL SYMBOLS NOT DRAWN 
      JMP HEAD4     ALL SYMBOLS DRAWN 
      ISZ ARW1      5 SYMBOLS OF ONE TYPE DRAWN 
      ISZ ARW1P 
      JMP HEAD1 
      SPC 1 
HEAD4 JSB SQUAR     WRITE A 
S0    OCT 77575       CROSS 
      DEC 2             IN THE
      DEC 6               MIDDLE
      JSB SQUAR             OF THE
S1    OCT 76577               SCREEN
      DEC 6                     BY WRITING
      DEC 2                       TWO RECTANGLES
      LDA ADDX      RESTORE INSTRUCTION 
      STA ADD 
      JMP L11 
      SPC 1 
ADDX  NOP 
DOTC  NOP 
BFAD  DEF BUFFR 
PNTR  NOP 
LOOPC NOP 
BUFFR BSS 256 
SYMBL DEC -12 
POSC  NOP 
D12   DEC 12
ND11  DEC -11 
LOOPS DEC -8
      SKP 
****************************************************************
*                                                              *
*              -- PIN CUSHION TEST, CROSS HATCH PATTERN --     *
*                                                              *
****************************************************************
      SPC 1 
PIN   JSB ERASE     ERASE ENTIRE SCREEN 
      DEF *+1 
      JSB VIDLU     SET WRITE MODE
      DEF *+2 
      DEF LUTV
      SPC 1 
      LDB MTEST     GET AMERICAN SCAN INDICATOR 
      LDA MASK0     GET VERT MIDDLE DONE, NON-AMER
      SZB           AMERICAN SCAN?
      LDA MASK6       YES - GET VERT MID DONE FOR AMER
      STA MSK0      SET VERTICLE MIDDLE DONE TESTER 
      LDA MASK6     GET VERT MIDDLE, NON-AMER 
      SZB           AMERICAN SCAN?
      LDA MASK1       YES - GET VERT MID FOR AMER 
      STA MSK6      SET VERTICLE MIDDLE TESTER
      LDA MASK9     GET VERT NXT TO MID, NON-AMER 
      SZB           AMERICAN SCAN?
      LDA MASK5       YES - GET NXT TO MID FOR AMER 
      STA MSK9      SET VERT NEXT TO MIDDLE TESTER
      SPC 1 
      CLB           CLEAR X COUNTER 
PIN1  LDA BFAD      SET UP BUFFER 
      STA PNTR        POINTER 
      STA A2
      CLA           CLEAR Y COUNTER 
FAX1  IOR B         MERGE X INTO Y
      STA PNTR,I    PUT POINT INTO
      ISZ PNTR        OUTPUT BUFFER 
      AND MASK2     MASK OUT X PORTION
      CPA MASK2     TEST FOR Y LINE FINSIHED
      JMP FAX2        Y LINE FINISHED 
      ADA X         ADD 1 TO Y
      JMP FAX1
      SKP 
*************************************************************** 
*                                                             * 
*              -- PIN CUSHION TEST CONTINUED --               * 
*                                                             * 
*************************************************************** 
      SPC 1 
FAX2  STB POSC      SAVE X COUNTER
      JSB POINT     OUTPUT COMPLETE 
      DEF *+3         LINE FROM BUFFER
A2    NOP 
      DEF X 
      LDB POSC      RESTORE X COUNTER 
      CPB MASK3     IS LAST LINE FINISHED?
      JMP FAX4      LAST FINISHED 
      CPB MASK4     IS NEXT TO MIDDLE FINISHED? 
      JMP FAX3        YES 
      CPB MASK7     IS THIS THE MIDDLE? 
      JMP FAX3A       YES 
      CPB MASK8     IS MIDDLE FINISHED? 
      JMP FAX3        YES 
      ADB D8        ADD 10B TO X
      JMP PIN1      DRAW NEXT VERTICLE
      SPC 1 
FAX3  ADB MES16     ADD 7B TO X 
      JMP PIN1
      SPC 1 
FAX3A ADB B1        ADD 1 TO X
      JMP PIN1
      SPC 1 
B1    OCT 1 
MASK0 OCT 100000
MASK1 OCT 70000 
MASK4 OCT 170 
MASK5 OCT 73400 
MASK6 OCT 74000 
MASK7 OCT 177 
MASK8 OCT 200 
MASK9 OCT 77400 
      SPC 2 
FAX4  CLB           CLEAR Y COUNTER 
FAX5  LDA BFAD      SET BUFFER
      STA PNTR        POINTER 
      STA A3
      CLA           CLEAR X COUNTER 
FAX6  IOR B         MERGE Y INTO X
      STA PNTR,I    PUT POINT INTO
      ISZ PNTR        OUTPUT BUFFER 
      AND MASK3     MASK OUT Y PORTION
      CPA MASK3     TEST FOR HORIZONTAL FINISHED
      JMP FAX7      FINISHED
      INA           ADD 1 TO X
      JMP FAX6
      SKP 
****************************************************************
*                                                              *
*              -- PIN CUSHION TEST CONTINUED --                *
*                                                              *
****************************************************************
      SPC 1 
FAX7  STB POSC      SAVE Y COUNTER
      JSB POINT     OUTPUT A COMPLETE 
      DEF *+3         LINE FROM BUFFER
A3    NOP 
      DEF X 
      LDB POSC      RESTORE Y COUNTER 
      CPB MASK2     IS LAST LINE FINISHED?
      JMP RESP      LAST FINISHED RETURN TO MAIN
      CPB MSK6      IS NEXT TO MIDDLE FINISHED? 
      JMP FAX8        YES 
      CPB MSK9      IS THIS THE MIDDLE? 
      JMP FAX8A       YES 
      CPB MSK0      IS MIDDLE FINISHED? 
      JMP FAX8        YES 
      ADB B4000     ADD 10B TO Y
      JMP FAX5      DRAW NEXT HORIZONTAL
      SPC 1 
FAX8  ADB CON2      ADD 7B TO Y 
      JMP FAX5
      SPC 1 
FAX8A ADB X         ADD 1 TO Y
      JMP FAX5
      SPC 1 
RESP  JMP L11 
      SPC 1 
MSK0  NOP 
MSK6  NOP 
MSK9  NOP 
B4000 OCT 4000
CON2  OCT 3400
STADR OCT 174000
      SKP 
****************************************************************
*                                                              *
*              -- WRITE ALL POINTS --                          *
*                                                              *
****************************************************************
      SPC 1 
BURN  JSB ERASE     ERASE ENTIRE SCREEN 
      DEF *+1 
      JSB VIDLU     SET WRITE MODE
      DEF *+2 
      DEF LUTV
      SPC 1 
      JSB AREA      WRITE AREA
      DEF OUT         256 POINTS BY 
      DEF ZERO          256 POINTS
      DEF ZERO
      DEF MASK3 
      DEF MASK3 
      DEF ZERO
      DEF ZERO
      SPC 1 
OUT   JMP L11 
      SPC 2 
MASK2 OCT 177400
MASK3 DEC 255 
POSIT OCT -5
SA2   OCT 174370
SXA2  OCT 164370
SXA3  OCT 164000
      SKP 
****************************************************************
*                                                              *
*              -- SETTLING TIME, SQUARES IN 4 CORNERS --       *
*                                                              *
****************************************************************
      SPC 1 
SETTM JSB ERASE     ERASE ENTIRE SCREEN 
      DEF *+1 
      JSB VIDLU     SET WRITE MODE
      DEF *+2 
      DEF LUTV
      SPC 1 
      LDB MTEST     GET AMERICAN SCAN INDICATOR 
      LDA SA2       GET RIGHT SQUARE POS, NON-AMER
      SZB           AMERICAN SCAN?
      LDA SXA2        YES - GET RT SQR FOR AMER 
      STA S2        SET RIGHT SQUARE STARTING POINT 
      LDA STADR     GET LEFT SQUARE POS, NON-AMER 
      SZB           AMERICAN SCAN?
      LDA SXA3        YES - GET LEFT SQR FOR AMER 
      STA S3        SET LEFT SQUARE STARTING POINT
      SPC 1 
      JSB SQUAR     PUTS 8X8 SQUARE 
ZERO  OCT 0           IN THE
D8    DEC 8             LOWER LEFT
      DEC 8               CORNER
      JSB SQUAR     PUTS 8X8 SQUARE 
S2    OCT 174370      IN THE
      DEC 8             UPPER RIGHT 
      DEC 8               CORNER
      JSB SQUAR     PUTS 8X8 SQUARE 
S3    OCT 174000      IN THE
      DEC 8             UPPER LEFT
      DEC 8               CORNER
      JSB SQUAR     PUTS 8X8 SQUARE 
      OCT 370         IN THE
      DEC 8             LOWER RIGHT 
      DEC 8               CORNER
      JMP L11 
      SPC 2 
NEG1  OCT -1
      SKP 
****************************************************************
*                                                              *
*              -- DRAW SPECIFIED SQUARE --                     *
*                                                              *
****************************************************************
      SPC 1 
*            ** USED BY GAIN AND SETTM ** 
      SPC 1 
SQUAR NOP 
      LDA SQUAR,I   THIS GETS LLHC
      STA BUFF
      LDB SQUAR 
      INB 
      LDA 1,I       ADDRESS OF VERT IN B
      ADA NEG1      REDUCE LENGTH BY ONE
      STA VERT1 
      INB           ADD TO ADDRESS
      LDA 1,I       GET HORIZONTAL
      ADA NEG1      REDUCE LENGTH BY ONE
      STA HORZ1 
      INB           PREPARE RETURN ADDRESS
      STB SQUAR     STORE IN RETURN POINT 
      LDA BUFF
      AND MASK2 
      ALF,ALF 
      STA VERT2 
      LDA BUFF
      AND MASK3 
      STA HORZ2 
      SKP 
*************************************************************** 
*                                                             * 
*              -- DRAW SQUARE, CONTINUED --                   * 
*                                                             * 
*************************************************************** 
      SPC 1 
UP    JSB VECTR 
      DEF RIGHT 
      DEF HORZ2 
      DEF VERT2 
      DEF B2
      DEF VERT1 
      SPC 1 
RIGHT JSB VECTR 
      DEF DOWN
      DEF XCONN 
      DEF VERT2 
      DEF ZERO
      DEF HORZ1 
      DEF B3
      SPC 1 
DOWN  JSB VECTR 
      DEF LEFT
      DEF XCONN 
      DEF VERT2 
      DEF MES13 
      DEF VERT1 
      DEF B3
      SPC 1 
LEFT  JSB VECTR 
      DEF XCONN 
      DEF XCONN 
      DEF VERT2 
      DEF JBUFR 
      DEF HORZ1 
      DEF B3
      DEF ZERO
      DEF B2
      SPC 1 
XCONN JMP SQUAR,I 
      SPC 1 
BUFF  NOP 
HORZ1 NOP 
HORZ2 NOP 
VERT1 NOP 
VERT2 NOP 
B2    OCT 2 
B3    OCT 3 
JBUFR DEC 4 
      OCT 21050,24537 
TCON1 DEC -30769
      SKP 
****************************************************************
*                                                              *
*              -- GENERAL PURPOSE TIME DELAY --                *
*                                                              *
****************************************************************
      SPC 1 
*  THE FOLLOWING IS NOT COMPENSATED FOR CYCLE TIME; 
      SPC 1 
TIME5 NOP            1 SECOND TIMER 
      LDB POSIT     GET 1 SECOND COUNT
      LDA TCON1 
      ISZ A         INNER TIMER 
      JMP *-1         COUNT-DOWN LOOP 
      ISZ B         OUTER TIMER 
      JMP *-4         COUNT-DOWN LOOP 
      JMP TIME5,I 
      SPC 4 
****************************************************************
*                                                              *
*              -- DISPLAY HP LOGO --                           *
*                                                              *
****************************************************************
      SPC 1 
LOGO  JSB ERASE     ERASE ENTIRE SCREEN 
      DEF *+1 
      JSB VIDLU     SET WRITE MODE
      DEF *+2 
      DEF LUTV
      JSB POINT     OUTPUT ENTIRE 
      DEF *+3         HP LOGO BUFFER
      DEF LOBUF 
      DEF HWMNY 
      JMP L11 
      SPC 2 
HWMNY DEC 2013
      SKP 
