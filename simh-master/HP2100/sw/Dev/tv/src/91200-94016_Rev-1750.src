ASMB,L,C
      NAM DCTN,7  91200-94004   REV. 1750 
* 
      ENT DCTN
      EXT EXEC      
* 
*  THIS ROUTINE IS DESIGNED TO ACCEPT A 15X18 DOT MATRIX AND CONVERT
*  TO X AND Y COORDINATE VALUES TO BE PASSED TO THE HP 91200B RTE 
*  DRIVER DVA13.
* 
*  THE ROUTINE REQUIRES A MICROCODED SECTION, HP PART NO. 91200-94006 
*  BE INSTALLED IN A HP 21MX-E SERIES COMPUTER. 
* 
*  THE CALLING SEQUENCE IS: 
* 
*          JSB DCTN 
*          DEF *+5    RETURN ADDRESS
*          DEF CCHA   DOT MATRIX ADDRESS
*          DEF YCO    Y AXIS ORGIN ADDRESS
*          DEF XCO    X AXIS ORGIN ADDRESS
*          DEF CON    CONTROL WORD FOR DVA13
* 
      SKP 
* 
CCHA  NOP           DOT MATRIX ADDRESS
YCO   NOP           Y AXIS ORGIN ADDRESS
XCO   NOP           X AXIS ORGIN ADDRESS
CON   NOP           CONTROL WORD
* 
DCTN  NOP           ENTRY POINT 
      OCT 105223    GET PARAMETER ADDRESSES (.ENTR) 
      DEF CCHA
      LDB YCO,I     GET Y ORGIN VALUE 
      BLF,BLF       MOVE TO UPPER BYTE
      LDA XCO,I     GET X ORGIN VALUE 
      IOR B         COMBINE X AND Y VALUES
      LDB CCHA      B REG.= DOT MATRIX ADDRESS
* 
      OCT 105440    GO TO MICROCODED SECTION
      DEF *+2       RETURN ADDRESS
      DEF BUFR      OUTPUT BUFFER ADDRESS 
* 
      STA BLC       SAVE BUFFER LENGTH COUNT
      SZA,RSS       ANYTHING TO OUTPUT? 
      JMP DCTN,I    NO,RETURN 
* 
      JSB EXEC      OUTPUT BUFFER TO DRIVER 
      DEF *+5 
      DEF FUN 
      DEF CON,I 
      DEF BUFR
      DEF BLC 
      JMP DCTN,I    RETURN TO CALLING PROGRAM 
* 
FUN   DEC 2 
BLC   BSS 1 
BUFR  BSS 180 
* 
B     EQU 1 
* 
      END DCTN
