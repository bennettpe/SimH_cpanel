
; set console -n -q log=console.txt 
; set debug -n -q debug.txt

if "%CPANEL%"=="1" goto chk_cp_ok1

echo
echo ERROR!
echo HP2100 compiled without control panel support. Build again 
echo with CPANEL, USE_SIM_VIDEO and HAVE_LIBSDL symbols defined 
echo
goto end

:chk_cp_ok1

if exist ../cpanel/hp2100.ini goto chk_cp_ok2

echo
echo ERROR!
echo cannot find the control panel definition file
echo "../cpanel/hp2100.ini" 
echo
goto end

:chk_cp_ok2

; start control panel

echo *** 
echo *** Start Control Panel GUI
echo *** 
echo *** HotKeys on Control Panel window:
echo ***                 +/- Zoom In/Zoom Out (also ^+ and ^-)
echo ***                 ^Y  Toggle zoom 100%% <-> 50%%
echo ***                 ^T  Toggle flash on clickable controls
echo ***                 ^I  Toggle info panel      
echo *** On Plotter window:   
echo ***                 +/- and ^+, ^-, ^Y as above           
echo ***                 ^F  Fast mode: while pressed, accelerates plotter drawind
echo ***                 ^P  Saves the current image to "Plotter_Image_NN.png" file
echo *** On Scope window:               
echo ***                 +/- and ^+, ^-, ^Y as above           
echo ***                 ^I  Toggle info panel (readable at 100%% zoom)      
echo ***                 i/I Increase/decrease intensity
echo ***                 f/F Widens/narrows beam spot focus
echo ***                 p/P Increase/decrease tube persistence time 
echo ***                 e   Bluk Erase (only for Storage Scope) 
echo *** 
echo ***           Click on control panel image and drag mouse to move the window.
echo ***           Right click mouse to show a tooltip with image at 100% scale.
echo *** 

att cpanel ../cpanel/hp2100.ini

; Parse FAST parameter. FAST makes SimH to run at max speed
if "%1"!="FAST" goto endfast
set env fastmode=1
shift
:endfast

; first parameter is the cpu type: 2116A, 2116B, ...
; second parameter is the scale: 10..200 (10-> cpanel size set to 10%, 200-> size set to 200%)

:cpu0
if "%1"!="2116A" goto cpu1
   set cpanel option=CpuType/%1
   set cpanel scale=100
   set throttle 5000/17
   goto cpuend
:cpu1
if "%1"!="2116B" goto cpu2
   set cpanel option=CpuType/%1
   set cpanel scale=50
   set throttle 5000/17
   goto cpuend
:cpu2
if "%1"!="2116C" goto cpu3
   set cpanel option=CpuType/%1
   set cpanel scale=50
   set throttle 5000/17
   goto cpuend
:cpu3
if "%1"!="2115A" goto cpu4
   set cpanel option=CpuType/%1
   set cpanel scale=50
   set throttle 5000/22
   goto cpuend
:cpu4
if "%1"!="2114A" goto cpu5
   set cpanel option=CpuType/%1
   set cpanel scale=50
   set throttle 5000/22
   goto cpuend
:cpu5
if "%1"!="2114B" goto cpu6
   set cpanel option=CpuType/%1
   set cpanel scale=50
   set throttle 5000/22
   goto cpuend
:cpu6
if "%1"!="2100A" goto cpu7
   set cpanel option=CpuType/%1
   set cpanel scale=50
   set throttle 5000/11
   goto cpuend
:cpu7
if "%1"!="2100S" goto cpu8
   set cpanel option=CpuType/%1
   set cpanel scale=50
   set throttle 5000/11
   goto cpuend
:cpu8
if "%1"!="21MXM" goto cpu9
   set cpanel option=CpuType/%1
   set cpanel scale=50
   set throttle 10000/7
   goto cpuend
:cpu9
if "%1"!="21MXE" goto cpu10
   set cpanel option=CpuType/%1
   set cpanel scale=50
   set throttle 10000/13
   goto cpuend
:cpu10
if "%1"!="1000M" goto cpu11
   set cpanel option=CpuType/%1
   set cpanel scale=50
   set throttle 10000/13
   goto cpuend
:cpu11
if "%1"!="1000E" goto cpu12
   set cpanel option=CpuType/%1
   set cpanel scale=50
   set throttle 10000/7
   goto cpuend
:cpu12
if "%1"!="1000F" goto cpu13
   set cpanel option=CpuType/%1
   set cpanel scale=50
   set throttle 10000/7
   goto cpuend
:cpu13
   echo Unknown cpu "%1", using default 1000E
   set cpanel option=CpuType/1000E
   set cpanel scale=50
   set throttle 5000/7
   goto cpuend

:cpuend

if "%fastmode%"=="1" set nothrottle

set cpanel pos=20/70
set cpanel option=ShowInfo
set cpanel on

; second parameter is the working directory
if "%2"=="" goto end
cd %2

; execute script given as parameter
if "%3"=="" goto end

do %3 %4 %5 %6 %7 %8 %9


:end