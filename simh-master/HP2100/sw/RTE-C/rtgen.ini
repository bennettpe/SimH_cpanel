
set console -q -n log=log.txt 

; set debug -n -q debug.txt
; set debug stdout

; set clk debug=CSRW;PSERV;IOBUS
; set tty debug=CSRW;SERV;PSERV;XFER;IOBUS
; set ptr debug=SERV;XFER;IOBUS
; set ptp debug=CSRW;SERV;XFER;IOBUS
; set cpu debug=INSTR;DATA;FETCH;REG;OPNDEXEC;NOOS
; set mp  debug=IOBUS
; set drc debug=IOBUS

echo
echo ***
echo *** RTE-C Operating System (1973) 
echo *** Generation
echo ***

; generates a new RTE-C system

set cpu 2116

set cpu 32k
SET CPU EAU

set ptr dev=12
set clk dev=11
set tty dev=16
set ptp dev=17
set lpt dev=20

set tty autolf
send after=200k
send delay=30k

; No disc

set dpd dis
set drc dis
set dpc dis

; Load SIO drivers

; Reload and configure 24K console SIO driver as SC 16
att ptr -q sio/29100-60017_A_24K_SIO_TTY.abs
boot ptr

deposit S 16
deposit P 2
go

; Load and configure 24K paper tape reader SIO driver as SC 12

att ptr -q sio/29100-60019_A_24K_SIO_RDR.abs
boot ptr

deposit S 12
deposit P 2
go

; Load and configure 24K paper tape punch SIO driver as SC 17
att ptr -q sio/29100-60020_A_24K_SIO_PUNCH.abs
boot ptr
 
deposit S 17
deposit P 2
go

; load RTE-C SYSTEM GENERATOR

att ptr -q rtcgen/29101-60011_B_RTSGN.abs
boot ptr


; Run

deposit S 0
deposit P 100

; attach boot papertape

att ptp -n -q rtcgen/tmp.rte-c_boot.abs
  

; initialization phase

go until "PRAM INPT?" 
send "1\r"

go until "TBG CHNL?" 
send "11\r"

go until "PRIV. INT?" 
send "0\r"

go until "FWA BP?" 
send "100\r"

go until "LWA MEM?" 
send "37677\r"

go until "FWA SYS MEM" 
send "36000\r"

go until "REL SYS MODS"
go until "\n-" 


goto start_module_load

d s 0
d p 100
go

; program input phase: Now load rel files for RTE

goto start_module_load

:load_module
echo
echo ---> Load Module %1
att ptr -q rtcgen/%1
send "RELOCATE\r"
go until "\n-"
return

:start_module_load

send "MAP GLOBALS,MODULES,LINKS\r"
go until "\n-"

call load_module 29101-60003_C_RTIOC.rel
call load_module 29101-60002_B_SCHED.rel
call load_module 29101-60001_C_EXEC.rel
call load_module 29029-60001_RTE_DVR00.rel

send "DISPLAY UNDEFS\r"
go until "\n-"
send "END\r"

; Table generation phase

; equipement table
; each eqt maps a physical device at given select code address
go until "EQT TBL"

; eqt 1 is teletype (tty)
go until "1 =?" 
send "16,DVR00\r"

; eqt 2 is paper tape reader (ptr)
go until "2 =?" 
send "12,DVR01\r"

; eqt 3 is paper tape punch (ptp)
go until "3 =?" 
send "17,DVR02,B,T=1000\r"

go until "4 =?" 
send "END\r"

; logical unit table
; each LU maps one eqt
; define LU1 is tty, LU4 is ptp, LU5 is ptr, 
;        LU6 is the list device (usually printer) but directed to tty

go until "LU#"

go until "1 = EQT #?" 
send "1\r"

go until "2 = EQT #?" 
send "0\r"

go until "3 = EQT #?" 
send "0\r"

go until "4 = EQT #?" 
send "3\r"

go until "5 = EQT #?" 
send "2\r"

go until "6 = EQT #?" 
send "1\r"

go until "7 = EQT #?" 
send "END\r"

; interrupt table. 
go until "INT TBL" 

send "10,PRG,APLDR\r"
send "12,EQT,2\r"
send "16,EQT,1\r"
send "17,EQT,3\r"
send "END\r"

; program/parameter input phase

go until "#ID SEG?" 
send "7\r"

go until "STRT-UP PROG?" 
send "APLDR\r"

go until "\n-"

call load_module 24151-01_F2E.rel
call load_module 24153-60001_FF.N.rel

send "END\r"

go until "# WDS IN COMM?"
send "100\r"
go until "\n-"

call load_module 29101-60004_C_APLDR.rel

send "END\r"
go until "ENTER PRAMS"
send "0\r"
go until "\n-"
send "END\r"

go until "SNAPSHOT?"
go until "\n"

att ptp -n -q rtcgen/rte-c_snapshot.bin

send "YES\r"
go

; RTSGN finished

det -q ptp

echo
echo ***
echo *** System generated 
echo *** Boot the system
echo ***
echo

reset
d 0-37770 0

set tty noautolf
send after=200k
send delay=30k

att -e -q ptr rtcgen/tmp.rte-c_boot.abs
boot ptr

d s 0
d p 2

go until "APLDR: DONE"
send "\r"
go until "*"

; System created and prepared into tmp.rte-c_boot.abs 
; Copy as production papertape

det -q all

copy -q    rtcgen/tmp.rte-c_boot.abs   rtcgen/rte-c_boot.abs
del  -q    rtcgen/tmp.rte-c_boot.abs   

echo
echo
echo ***
echo *** Generation finished 
echo ***

if "%1"=="QUIT" quit
echo
echo *** Press Enter to quit
set env -p "   " a
echo
 
echo
quit
goto end


:end
