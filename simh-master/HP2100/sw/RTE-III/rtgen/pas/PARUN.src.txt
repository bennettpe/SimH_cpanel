ASMB,R,F
 NAM PARUN,7
* 
 EXT EXEC,.OPSY,%READ,TAB,ATAB,BTAB,STAB,RCONS
 EXT LUNDA,LLIMT,LULST,S,TSI,RELBS,STKSZ
 ENT SUB00,SUB01,SUB02,SUB03,SUB04,SUB05,SUB06,SUB07
 ENT SUB08,SUB09,SUB10,SUB11,SUB12,SUB13,SUB14,SUB15
 ENT SUB16,SUB17,SUB18,SUB19,SUB20,SUB21,SUB22,SUB23
 ENT SUB24,SUB25,SUB26,SUB27,SUB28,SUB29,SUB30,SUB31
 ENT SUB32,SUB33,SUB34,SUB35,SUB36,SUB37,SUB38,SUB39
 ENT SUB40,SUB41,SUB42,SUB43,SUB44,SUB45,SUB46,SUB47
 ENT SUB48,SUB49,SUB50,SUB51,SUB52,SUB53,SUB54,SUB55
 ENT SUB56,SUB57,SUB58,SUB59,SUB60,SUB61,SUB62,SUB63
 ENT SUB64,SUB65
 EXT SIN,COS,EXP,LN,SQRT,ARCTA,..FCM,.RND 
 EXT .FLUN,..DLC,ABS
* 
 SUP
 DEC -6,-5,-4,-3,-2,-1
. DEC 0 
 DEC 1,2,3,4,5,6,7,8,9,10 
 DEC 11,12,13,14,15,16,17 
 DEC 18,19,20,21,22,23,24 
 DEC 25,26,27,28,29,30,31 
 DEC 32,33,34,35,36,37,38 
 DEC 39,40,41,42,43,44,45 
 DEC 46,47,48,49,50,51,52 
 DEC 53,54,55,56,57,58,59 
 DEC 60,61,62,63,64,65,66 
 DEC 67,68,69,70,71,72,73 
 DEC 74,75,76,77,78,79,80 
 DEC 81,82,83,84,85,86,87 
 DEC 88,89,90,91,92,93,94 
 DEC 95,96,97,98,99,100 
 DEC 101,102,103,104,105
 DEC 106,107,108,109,110
 DEC 111,112,113,114,115
 DEC 116,117,118,119,120
 DEC 121,122,123,124,125
 DEC 126,127,128,129,130
* 
D2 DEC 2
DM2 DEC -2
D20B OCT 20 
D40B OCT 40 
D5 DEC 5
D16 DEC 16
D7 DEC 7
D17B OCT 17 
DM1 DEC -1
DM6 DEC -6
D77B OCT 77 
KKKKK BSS 1 
* 
BLANK EQU .+40B 
EQUAL EQU .+75B 
MINAS EQU .+55B 
PLUAS EQU .+53B 
POINT EQU .+56B 
RNUM BSS 2
* 
.TERM JSB EXEC
 DEF *+3
 DEF RCD5 
 DEF RCDM1
 JSB EXEC 
 DEF *+2
 DEF RCD6 
* 
RCD5 DEC 5
RCD6 DEC 6
RCDM1 DEC -1
* 
* ROUTINES AND DATA OF COMMON USE 
* 
LINED BSS 60     LINE BUFFER
* 
GETAD NOP 
 LDA 0,I         TO GET TRUE ADDRESS THROUGH
 RAL,CLE,SLA,ERA AN INDIRECT CHAIN
 JMP *-2
 JMP GETAD,I
* 
GETBD NOP 
 LDB 1,I         TO GET TRUE ADDRESS THROUGH
 RBL,CLE,SLB,ERB AN INDIRECT CHAIN
 JMP *-2
 JMP GETBD,I
* 
MOVEO NOP        TO WRITE A LINE
 LDB MOVEO
 JSB GETBD
 LDA 1,I    CALL: JSB MOVEO 
 CMA,INA          DEF BUFF
 STA RLEN 
 INB        WHERE: BUFF DEC N 
 STB BUFAD              BSS M (N CHARS) 
 JSB EXEC   WRITE OUT THE BUFFER
 DEF *+5
 DEF D2 
 DEF LULST
BUFAD NOP   BUFFER ADDRESS
 DEF RLEN   RECORD LENGTH 
 ISZ MOVEO
 JMP MOVEO,I
* 
RLEN BSS 1
* 
INSRT NOP   INSERTS THE CHARACTER CH
 STA GARAG  AT THE (I+1)-ST PLACE 
 LDA INSRT  IN THE BUFFER BUF.
 JSB GETAD
 STA GARA2
 LDA GARAG
 CCE,SLA,RSS
 BLF,CLE,BLF
 ARS
 ADA GARA2
 STA GARAG     CALL: LDA I
 LDA 0,I             LDB CH 
 SEZ                 JSB INSRT
 ALF,ALF             DEF BUF
 AND .377 
 SEZ
 ALF,ALF
 ADA 1
 STA GARAG,I
 ISZ INSRT
 JMP INSRT,I
* 
GARAG BSS 1 
GARA2 BSS 1 
.377 OCT 377
* 
ADBUF NOP 
 LDA ADBUF    ADDS A CHARACTER CH IN THE
 JSB GETAD    FIRST EMPTY POSITION OF A 
 STA *+4      BUFFER. CALL: LDB CH
 ISZ *+3                    JSB ADBUF 
 LDA 0,I                    DEF BUFFER
 JSB INSRT    WHERE: BUFFER DEC N (NO OF CHARS
 NOP                      PRESENT)
 LDA ADBUF
 JSB GETAD
 ISZ 0,I
 ISZ ADBUF
 JMP ADBUF,I
* 
ADOUT NOP        ADDS A CHARACTER TO THE OUTPUT 
 AND .377        BUFFER (FIRST 8 BITS OF CHAR 
 LDB 0           ARE IGNORED). CALL: LDA CHAR 
 JSB ADBUF                           JSB ADOUT
 DEF OUTPT
 JMP ADOUT,I
* 
DCONV NOP       CONVERTS THE BINARY NUMBER N
 CMB,INB        TO ASCII (M DIGITS). THE
 STB CHCNT      RESULT IS ADDED TO BUF
 LDB DCONV
 JSB GETBD
 STB WHERE
 INB LDB M
 STB WHATS
 LDB PUNT1
 ADB CHCNT
 STB PUNTS
 CLB
 STB LDZRO
 STB SFLAG
 STB SIGN.
 SSA,RSS
 JMP *+3
 ISZ SIGN.
 CMA,INA
 ISZ CHCNT
 RSS
 JMP *+3
 JSB CONV1
 JMP *-4
 ISZ LDZRO
 JSB CONV1
 ISZ DCONV
 JMP DCONV,I
CONV1 NOP 
 CLB
 ADA PUNTS,I
 SSA
 JMP *+3
 INB
 JMP *-4
 CMA,INA
 ADA PUNTS,I
 CMA,INA
 STA GARA1
 SZB
 JMP *+3
 CPB LDZRO
 JMP *+4
 ISZ LDZRO
 ISZ SFLAG
 ADB D20B 
 ADB D40B 
 JSB ADBUF
WHERE NOP 
 CLB,INB
 CPB SFLAG
 RSS
 JMP WHICH
 CPB SIGN.
 RSS
 JMP WHICH
 LDB MINAS
 LDA WHERE,I
 ADA DM2
 JSB INSRT
WHATS NOP 
WHICH LDA GARA1 
 ISZ PUNTS
 JMP CONV1,I
* 
GARA1 BSS 1 
SIGN. BSS 1 
SFLAG BSS 1 
CHCNT BSS 1 
LDZRO BSS 1 
PUNTS BSS 1 
 DEC -10000 
 DEC -1000
 DEC -100 
 DEC -10
 DEC -1 
PUNT1 DEF * 
* 
SETAD NOP 
 AND D77B 
 CLB
 CLE,ERA
 ERB
 CLE,ERA
 ERB
 RBL,RBL
 ADB SETAD,I
 CMA
 STA BITKN
 CLA,INA
 RAR
 RAL
 ISZ BITKN
 JMP *-2
 ISZ SETAD
 JMP SETAD,I
* 
BITKN BSS 1 
* 
INSET NOP 
 LDB INSET    CALL: LDA N 
 JSB GETBD          JSB INSET 
 STB *+2            DEF X 
 JSB SETAD       -RETURN IF N NOT IN X -
 NOP             -RETURN IF N IN X -
 AND 1,I
 ISZ INSET
 SZA
 ISZ INSET
 JMP INSET,I
* 
WRILN NOP        PRINTS OUT THE OUTPUT
 JSB MOVEO       BUFFER AND CLEARS IT.
 DEF OUTPT
 CLA
 STA OUTPT
 JMP WRILN,I
* 
OUTPT OCT 0     OUTPUT BUFFER LENGTH (:=0)
 BSS 68         IN CHARACTERS, AND BUFFER.
* 
MOVES NOP         THE PROCEDURE MOVES COPIES
 STA SAVE1        A BUFFER INTO ANOTHER.
 LDA MOVES
 JSB GETAD
 CMA,INA
 STA SAVE2
 ISZ MOVES
MOVE1 LDA SAVE1,I 
 STA 1,I
 INB
 ISZ SAVE1
 ISZ SAVE2
 JMP MOVE1
 JMP MOVES,I
* 
SAVE1 BSS 1 
SAVE2 BSS 1 
* 
TWEEQ NOP 
 LDB TWEEQ,I   TESTS A NUMBER X 
 CMB,INB       TO SEE WHETHER IT IS BETWEEN 
 ISZ TWEEQ     TWO OTHER NUMBERS Y AND Z. 
 ADB 0         CALL:LDA X 
 SSB           JSB TWEEQ
 JMP *+5       ABS Y
 LDB 0         ABS Z
 CMB,INB       -RETURN IF Y<=X<=Z 
 ADB TWEEQ,I   -RETURN IF NOT 
 SSB
 ISZ TWEEQ
 ISZ TWEEQ
 JMP TWEEQ,I
* 
LETTR NOP         LDA CH
 JSB TWEEQ        JSB LETTR 
 OCT 101,132      - RET IF CH IS A LETTER 
 JMP LETTR,I      - RET IF NOT
 ISZ LETTR
 JMP LETTR,I
* 
DIGIT NOP         AS PROCEDURE LETTR
 JSB TWEEQ        (CHECKS FOR DIGITS) 
 OCT 60,71
 JMP DIGIT,I
 ISZ DIGIT
 JMP DIGIT,I
* 
OTPAD DEF OUTPT+7 
* 
* STANTYPSYS=[NOTYP,INTS,REALS,BOOLS,CHARS] 
* 
STYPS OCT 3,1,1,1 
* 
OTNUM NOP 
 LDB D5 
 JSB DCONV
 DEF OUTPT
 JMP OTNUM,I
* 
SIXBL NOP 
 JSB WRILN
 LDA DM6
 STA KKKKK
 LDA BLANK
 JSB ADOUT
 ISZ KKKKK
 JMP *-3
 JMP SIXBL,I
* 
EXPSN BSS 1       SIGN OF EXPONENT PART 
EXVAL BSS 1       VALUE OF EXPONENT (DECIMAL) 
IDNUM BSS 1       TO BUILD A NUMBER IN. 
IDNU1 BSS 1 
EXPF BSS 1        BINARY EXPONENT 
FCNT BSS 1        COUNTS FRACTION DIGITS
TTTTT BSS 1       TEMPORARY STORAGE 
TTTT1 BSS 1 
TENTH OCT 146314  0.8 BINARY (...)
* 
NORML NOP     NORML LEFT JUSTIFIES THE
 SZA,RSS      NUMBER IN A AND B.
 SZB
 JMP NRML1    IF IT IS ZERO, EXIT 
 STA EXPF     WITH BINARY EXPONENT EXPF=0.
NRML3 STA IDNUM 
 STB IDNU1
 JMP NORML,I
NRML1 SSA        IF HIGH BIT IS NOT SET 
 JMP NRML2
 CLE,ELB
 ELA             SHIFT LEFT 
 STA IDNUM
 CCA             SUBTRACT 1 FROM EXP AND
 ADA EXPF 
 STA EXPF        LOOP BACK
 LDA IDNUM
 JMP NRML1
NRML2 CLE,ERA    IF HIGH BIT IS SET,
 ERB             SHIFT RIGHT
 ISZ EXPF        ADD 1 TO EXPF
 JMP NRML3
 JMP NRML3       AND EXIT 
* 
MPY NOP      MULTIPLIES TWO 16-BIT
 STA TTTTT   NUMBERS IN REGISTERS A 
 LDA D16
 CMA,INA     AND B, PRODUCING A 
 STA TTTT1   32-BIT RESULT IN A 
 CLA         AND B (HIGH PART IN A).
 CLE,SLB
 ADA TTTTT
 ERA
 ERB
 ISZ TTTT1
 JMP *-5
 JMP MPY,I
* 
* ROUTINES FOR TABLE ACCESSES 
* 
LINK BSS 1     (LINK) 
OBJ BSS 1      (ORD(OBJ)) 
TYP BSS 1      (ORD(TYP)) 
REF BSS 1      (REF)
NORMA BSS 1    (ORD(NORMAL))
LEV BSS 1      (LEV)
ADR BSS 1      (ADR)
NAME BSS 5     (NAME) 
WORD1 BSS 3 
* 
TABSC NOP     THE CALL
 JSB TABAD        LDA I 
 LDA TABGA        JSB TABSC 
 LDB NAMAD    UNPACKS THE VARIOUS FIELDS
 JSB MOVES    OF TAB[I], PUTTING THEM 
 DEC 5        INTO THE PLACES 
 LDA TABGA    LINK,OBJ,...,NAME   ABOVE.
 ADA D5 
 STA TABGA
 LDA TABGA,I
 AND .777 
 STA LINK 
 LDA TABGA,I
 ALF,ALF
 RAR
 AND D7 
 STA OBJ
 LDA TABGA,I
 ALF
 AND D17B 
 STA TYP
 ISZ TABGA
 LDA TABGA,I
 AND D77B 
 STA LEV
 LDA TABGA,I
 CLB
 SSA
 INB
 STB NORMA
 ALF,ALF
 RAL,RAL
 AND .777 
 STA REF
 ISZ TABGA
 LDA TABGA,I
 STA ADR
 JMP TABSC,I
* 
TABGA BSS 1 
NAMAD DEF NAME
.777 OCT 777
* 
TABAD NOP   GIVEN THE CALL: 
 ADA 0           LDA I
 ADA 0           JSB TABAD
 ADA 0      TABAD RETURNS IN TABGA
 ADA TAB    THE ADDRESS OF TAB[I].
 STA TABGA
 JMP TABAD,I
* 
INXTP BSS 1   (INXTYP)
ELTYP BSS 1   (ELTYP) 
ELREF BSS 1   (ELREF) 
NAMEM BSS 1 
LOW BSS 1     (LOW) 
HIGH BSS 1    (HIGH)
ELSIZ BSS 1   (ELSIZE)
SIZE BSS 1    (SIZE)
* 
ATASC NOP  LIKE TABSC, FOR ATAB.
 JSB ATAAD
 LDA TABGA,I
 AND .777 
 STA ELREF
 LDA TABGA,I
 ALF
 AND D17B 
 STA INXTP
 LDA TABGA,I
 ALF,ALF
 RAR
 AND D7 
 STA ELTYP
 LDA TABGA
 INA
 LDB LOWAD
 JSB MOVES
 DEC 4
 JMP ATASC,I
* 
LOWAD DEF LOW 
NAMEX DEF NAMEM 
* 
ATAAD NOP     LIKE TABAD, FOR ATAB. 
 ADA DM1
 STA TABGA
 ADA 0
 ADA 0
 ADA TABGA
 ADA ATAB 
 STA TABGA
 JMP ATAAD,I
* 
LAST BSS 1
LASTP BSS 1 
PSIZE BSS 1 
VSIZE BSS 1 
LASAD DEF LAST
* 
BTASC NOP      LIKE ATASC, FOR BTAB.
 JSB BTAAD
 LDA TABGA
 LDB LASAD
 JSB MOVES
 DEC 4
 JMP BTASC,I
* 
BTAAD NOP 
 ADA DM1
 ADA 0
 ADA 0
 ADA BTAB 
 STA TABGA
 JMP BTAAD,I
* 
RCOSC NOP    AS ABOVE, BUT FOR RCONS
 JSB RCOAD
 LDA TABGA,I
 ISZ TABGA
 LDB TABGA,I
 STA RCNST
 STB RCNST+1
 JMP RCOSC,I
* 
RCNST BSS 2 
* 
RCOAD NOP 
 ADA DM1
 ADA 0
 ADA RCONS
 STA TABGA
 JMP RCOAD,I
* 
F BSS 1 
X DEC 0 
Y DEC 0 
* 
STAAD NOP    THE CALL LDA X, JSB STAAD, WITH X=BYTE 
 CLE,ERA     POSITION IN STAB, RETURNS A=WORD ADDRESS,
 ADA STAB    E=0(HIGH),1(LOW), FOR THE BYTE PLACE IN
 JMP STAAD,I THE WORD.
* 
VARIA EQU .+1   VARIABLE
EEEEE EQU .+105B THE LETTER E 
* 
* ERR1 WRITES AN ERROR MESSAGE, SUPPLIED BY THE CALLING 
* ROUTINE IN THE A AND B REGISTERS (2 DIGITS + 2 LETTERS).
* 
ERR1 NOP
 STA XXERR
 STB YYERR
 LDB PRIER
 JSB .OPSY
 SSA,RSS
 JMP *+3
 LDB IDSGA
 ADB IDGIN
 LDA 1,I
 STA BUERR+1
 INB
 LDA 1,I
 STA BUERR+2
 INB
 LDA 1,I
 AND ER774
 IOR ERBLN
 STA BUERR+3
 JSB WRILN
 JSB EXEC 
 DEF *+5
 DEF RCDER
 DEF LULST
 DEF BUERR
 DEF BUERL
 CLA
 CLB
 JMP RPEXT   TERMINATE EXECUTION. 
* 
BUERR ASC 5,        : 
XXERR ASC 2,
YYERR ASC 1,
IDGIN DEC 12
ER774 OCT 177400
ERBLN OCT 40
RCDER DEC 2 
BUERL DEC -16 
PRIER OCT 141 
IDSGA EQU 1717B 
* 
RUN EQU . 
FIN EQU .+1 
CASCK EQU .+2 
DIVCK EQU .+3 
INXCK EQU .+4 
STKCK EQU .+5 
LINCK EQU .+6 
LNGCK EQU .+7 
REDCK EQU .+8 
NUMCK EQU .+9 
OVFCK EQU .+10
* 
.LMAX EQU 15
DISPL DEF * 
 DEC 0
 BSS .LMAX-1      =LMAX-1 
* 
BI DEC 0
FLD DEF * 
 DEC 10,22,10,1 
* 
PS DEC 0
LNCNT DEC 0 
OCNT DEC 1
BLKCN BSS 1 
H1 BSS 1
H2 BSS 1
H3 BSS 1
H4 BSS 1
PC BSS 1
INDUM BSS 1 
* 
RPINT ISZ OCNT
 NOP
 JMP PC,I 
* 
RETU1 NOP 
 LDA RETU1
 ADA .-2
 LDA 0,I
 STA PC 
 JMP RETU1,I
* 
RETU2 NOP 
 LDA RETU2
 ADA .-2
 LDA 0,I
 LDB 0,I
 STB Y
 INA
 STA PC 
 JMP RETU2,I
* 
RETU3 NOP 
 LDA RETU3
 ADA .-2
 LDA 0,I
 LDB 0,I
 STB X
 INA
 LDB 0,I
 STB Y
 INA
 STA PC 
 JMP RETU3,I
* 
ICHKW NOP 
 ISZ TSI
 CLA,INA
 ADA STKSZ
 CMA,INA
 ADA TSI
 SSA
 JMP ICHKW,I
 LDA STKCK
 STA PS 
 JMP RPEXT
* 
SUB00 NOP 
 JSB RETU3        LOAD ADDRESS
 JSB ICHKW
 LDA X
 ADA DISPL
 LDA 0,I
 ADA Y
 LDB TSI
 ADB 1
 ADB S
 STA 1,I
 JMP RPINT
* 
SUB01 NOP 
 JSB RETU3
 JSB ICHKW    LOAD VALUE
 LDA X
 ADA DISPL
 LDA 0,I
 ADA Y
 ADA 0
 ADA S
LAB1 LDB TSI
 ADB 1
 ADB S
 STB INDUM
 DLD 0,I
 DST INDUM,I
 JMP RPINT
* 
SUB02 NOP 
 JSB RETU3
 JSB ICHKW   LOAD INDIRECT
 LDA X
 ADA DISPL
 LDA 0,I
 ADA Y
 ADA 0
 ADA S
 LDA 0,I
 ADA 0
 ADA S
 JMP LAB1 
* 
SUB03 NOP 
 JSB RETU3
 LDA Y               UPDATE DISPLAY 
 STA H1 
 LDA X
 STA H2 
 LDA BI 
 STA H3 
RPL03 LDA H3
 LDB H1 
 ADB DISPL
 STA 1,I
 CCA
 ADA H1 
 STA H1 
 LDA .+2
 ADA H3 
 ADA 0
 ADA S
 LDA 0,I
 STA H3 
 LDA H1 
 CPA H2 
 JMP RPINT
 JMP RPL03
* 
SUB04 NOP 
 JSB RETU2
 JMP RPINT
SUB05 NOP 
 JSB RETU2
 JMP RPINT
SUB06 NOP 
 JSB RETU2
 JMP RPINT
* 
SUB07 NOP 
 JSB RETU2
 JMP RPINT
* 
SUB08 NOP 
 JSB RETU2
 LDA TSI
 ADA 0
 ADA S
 STA INDUM
 LDA Y
 ADA LB08S
 LDA 0,I
 JMP 0,I
* 
LB08S DEF *+1 
 DEF LAB80  0 
 DEF LAB81  1 
 DEF LAB82  2 
 DEF LAB83  3 
 DEF LAB84  4 
 DEF LAB85  5 
 DEF LAB86  6 
 DEF LAB87  7 
 DEF LAB88  8 
 DEF LAB89  9 
 DEF LAB90 10 
 DEF LAB91 11 
 DEF LAB92 12 
 DEF LAB93 13 
 DEF LAB94 14 
 DEF LAB95 15 
 DEF LAB96 16 
 DEF LAB97 17 
 DEF LAB98 18 
 DEF LAB99 19 
* 
LAB80 LDA INDUM,I  INT:=ABS(INT)
 SSA
 CMA,INA
 STA INDUM,I
 JMP RPINT
* 
LAB81 DLD INDUM,I  REAL:=ABS(REAL)
 JSB ABS
 DST INDUM,I
 JMP RPINT
* 
LAB82 LDA INDUM,I  INT:=SQR(INT)
 CLO
 MPY INDUM,I
 SSA
 INB
 SZB
 JMP INTOV
 STA INDUM,I
 JMP RPINT
* 
LAB83 DLD INDUM,I   REAL:=SQR(REAL) 
 CLO
 FMP INDUM,I
 SOC C
 JMP REAOV
 DST INDUM,I
 JMP RPINT
* 
LAB84 LDA INDUM,I  BOOL:=ODD(INT) 
 CLB
 SLA
 CMB
 STB INDUM,I
 JMP RPINT
* 
LAB85 LDA INDUM,I CHAR:=CHR(INT)
 JSB TWEEQ
 DEC 0,63 
 JMP RPINT
LAB7 LDA INXCK
 STA PS 
 JMP RPEXT
* 
LAB86 JMP RPINT INT:=ORD(CHAR)
* 
LAB87 LDA INDUM,I CHAR:=SUCC(CHAR)
 INA
 STA INDUM,I
 JMP RPINT
* 
LAB88 LDA INDUM,I CHAR:=PRED(CHAR)
 ADA .-1
 STA INDUM,I
 JMP RPINT
* 
LAB89 DLD INDUM,I    INT:=ROUND(REAL) 
 CLO
 JSB .RND 
 SOC C
 JMP INTOV
 STA INDUM,I
 JMP RPINT
* 
LAB90 DLD INDUM,I   INT:=TRUNC(REAL)
 CLO
 FIX
 SOC C
 JMP INTOV
 STA INDUM,I
 JMP RPINT
* 
LAB91 DLD INDUM,I    REAL:=SIN(REAL)
 JSB SIN
 JSB ERR1 
 DST INDUM,I
 JMP RPINT
* 
LAB92 DLD INDUM,I    REAL:=COS(REAL)
 JSB COS
 JSB ERR1 
 DST INDUM,I
 JMP RPINT
* 
LAB93 DLD INDUM,I    REAL:=EXP(REAL)
 JSB EXP
 JSB ERR1 
 DST INDUM,I
 JMP RPINT
* 
LAB94 DLD INDUM,I    REAL:=LN(REAL) 
 JSB LN 
 JSB ERR1 
 DST INDUM,I
 JMP RPINT
* 
LAB95 DLD INDUM,I    REAL:=SQRT(REAL) 
 JSB SQRT 
 JSB ERR1 
 DST INDUM,I
 JMP RPINT
* 
LAB96 DLD INDUM,I   REAL:=ARCTAN(REAL)
 JSB ARCTA
 DST INDUM,I
 JMP RPINT
* 
LAB97 JSB ICHKW  EOS
 LDB INDUM
 ADB .+2
 LDA EOFWR
 STA 1,I
 JMP RPINT
* 
LAB98 JSB ICHKW  EOLN 
 LDB INDUM
 ADB .+2
 LDA EOLIN
 STA 1,I
 JMP RPINT
* 
LAB99 JSB ICHKW  RANDOM 
 LDB INDUM
 ADB .+2
 STB INDUM
 JSB RANDM
 DST INDUM,I
 JMP RPINT
* 
SUB09 NOP 
 JSB RETU2
 LDA TSI    OFFSET
 ADA 0
 ADA S
 LDB 0,I
 ADB Y
 STB 0,I
 JMP RPINT
* 
SUB10 NOP 
 JSB RETU2
 LDA Y       JUMP 
 ADA RELBS
 STA PC 
 JMP RPINT
* 
SUB11 NOP 
 JSB RETU2
 LDA TSI   CONDITIONAL JUMP 
 ADA 0
 ADA S
 LDA 0,I
 LDB Y
 ADB RELBS
 SSA,RSS
 STB PC 
 LDA TSI
 ADA .-1
 STA TSI
 JMP RPINT
* 
SUB12 NOP 
 JSB RETU2
 LDA TSI    SWITCH
 ADA 0
 ADA S
 LDA 0,I
 STA H1 
 LDA TSI
 ADA .-1
 STA TSI
 LDA Y
 STA H2 
RPL12 LDA H2
 ADA RELBS
 LDA 0,I
 CPA .+13 
 JMP *+4
 LDA CASCK
 STA PS 
 JMP RPEXT
 LDA H2 
 ADA RELBS
 INA
 LDA 0,I
 CPA H1 
 RSS
 JMP *+8
 LDA H2 
 ADA .+3
 ADA RELBS
 LDA 0,I
 ADA RELBS
 STA PC 
 JMP RPINT
 LDA H2 
 ADA .+4
 STA H2 
 JMP RPL12
* 
SUB13 NOP 
 JSB RETU2
 JMP RPINT
* 
SUB14 NOP 
 JSB RETU2
 CCA         FOR1UP 
 ADA TSI
 ADA 0
 ADA S
 LDA 0,I
 STA H1 
 LDA TSI
 ADA 0
 ADA S
 LDA 0,I
 LDB H1 
 CMB,INB
 ADA 1
 SSA
 JMP *+11 
 LDA TSI
 ADA .-2
 ADA 0
 ADA S
 LDA 0,I
 ADA 0
 ADA S
 LDB H1 
 STB 0,I
 JMP RPINT
 LDA TSI
 ADA .-3
 STA TSI
 LDA Y
 ADA RELBS
 STA PC 
 JMP RPINT
* 
SUB15 NOP 
 JSB RETU2
 LDA TSI   FOR2UP 
 ADA .-2
 ADA 0
 ADA S
 LDA 0,I
 STA H2 
 ADA 0
 ADA S
 LDA 0,I
 INA
 STA H1 
 CMA,INA
 LDB TSI
 ADB 1
 ADB S
 LDB 1,I
 ADB 0
 SSB
 JMP LAB2 
LAB3 LDA H2 
 ADA 0
 ADA S
 LDB H1 
 STB 0,I
 LDA Y
 ADA RELBS
 STA PC 
 JMP RPINT
LAB2 LDA TSI
 ADA .-3
 STA TSI
 JMP RPINT
* 
SUB16 NOP 
 JSB RETU2
 CCA      FOR1DOWN
 ADA TSI
 ADA 0
 ADA S
 LDA 0,I
 STA H1 
 LDB TSI
 ADB 1
 ADB S
 LDB 1,I
 CMB,INB
 ADB 0
 SSB
 JMP *+11 
 LDA TSI
 ADA .-2
 ADA 0
 ADA S
 LDA 0,I
 ADA 0
 ADA S
 LDB H1 
 STB 0,I
 JMP RPINT
 LDA Y
 ADA RELBS
 STA PC 
 JMP LAB2 
* 
SUB17 NOP 
 JSB RETU2
 LDA TSI    FOR2DOWN
 ADA .-2
 ADA 0
 ADA S
 LDA 0,I
 STA H2 
 ADA 0
 ADA S
 LDA 0,I
 ADA .-1
 STA H1 
 LDB TSI
 ADB 1
 ADB S
 LDB 1,I
 CMB,INB
 ADB 0
 SSB
 JMP LAB2 
 JMP LAB3 
* 
SUB18 NOP 
 JSB RETU2
 LDA Y     MARK STACK 
 JSB TABSC
 LDA REF
 JSB BTASC
 LDA VSIZE
 STA H1 
 ADA TSI
 CLB,INB
 ADB STKSZ
 CMB,INB
 ADB 0
 SSB
 JMP *+4
 LDA STKCK
 STA PS 
 JMP RPEXT
 LDA TSI
 ADA .+5
 STA TSI
 ADA .-1
 ADA 0
 ADA S
 CCB
 ADB H1 
 STB 0,I
 LDA TSI
 ADA 0
 ADA S
 LDB Y
 STB 0,I
 JMP RPINT
* 
SUB19 NOP 
 JSB RETU2
 LDA Y       CALL 
 CMA,INA
 ADA TSI
 STA H1 
 ADA .+4
 ADA 0
 ADA S
 LDA 0,I
 STA H2 
 JSB TABSC
 LDA LEV
 STA H3 
 INA
 ADA DISPL
 LDB H1 
 STB 0,I
 LDA .+3
 ADA 1
 ADA 0
 ADA S
 LDA 0,I
 ADA 1
 STA H4 
 INB
 ADB 1
 ADB S
 STB INDUM
 LDA PC 
 CMA,INA
 ADA RELBS
 CMA,INA
 STA INDUM,I
 LDA H3 
 ADA DISPL
 LDA 0,I
 ISZ INDUM
 ISZ INDUM
 STA INDUM,I
 LDA BI 
 ISZ INDUM
 ISZ INDUM
 STA INDUM,I
 LDA TSI
 STA H3 
 ADA 0
 ADA S
 STA INDUM
 CLB
 LDA H3 
LAB4 CPA H4 
 JMP *+6
 ISZ INDUM
 ISZ INDUM
 STB INDUM,I
 INA
 JMP LAB4 
 LDA H1 
 STA BI 
 LDA H4 
 STA TSI
 LDA H2 
 JSB TABSC
 LDA ADR
 ADA RELBS
 STA PC 
 JMP RPINT
* 
LAB8 NOP
 LDA Y
 STA H1 
 JSB ATASC
 LDA LOW
 STA H2 
 LDA TSI
 ADA 0
 ADA S
 LDA 0,I
 STA H3 
 LDA H2 
 STA LAB5 
 LDA H1 
 JSB ATASC
 LDA HIGH 
 STA LAB6 
 LDA H3 
 JSB TWEEQ
LAB5 NOP
LAB6 NOP
 RSS
 JMP LAB7 
 CCA
 ADA TSI
 STA TSI
 ADA 0
 ADA S
 STA INDUM
 JMP LAB8,I 
* 
SUB20 NOP 
 JSB RETU2
 JSB LAB8    INDEX1 
 LDA H2 
 CMA,INA
 ADA H3 
LAB9 ADA INDUM,I
 STA INDUM,I
 JMP RPINT
* 
SUB21 NOP 
 JSB RETU2
 JSB LAB8     INDEX 
 LDA H1 
 JSB ATASC
 LDA H2 
 CMA,INA
 ADA H3 
 MPY ELSIZ
 JMP LAB9 
* 
SUB22 NOP 
 JSB RETU2
 LDA TSI      LOAD BLOCK
 ADA 0
 ADA S
 LDA 0,I
 STA H1 
 CCA
 ADA TSI
 STA TSI
 ADA Y
 STA H2 
 CLB,INB
 ADB STKSZ
 CMB,INB
 ADA 1
 SSA
 JMP *+4
 LDA STKCK
 STA PS 
 JMP RPEXT
LAB1. LDA TSI 
 INA
 CMA,INA
 ADA H2 
 SSA
 JMP RPINT
 ISZ TSI
 LDA TSI
 ADA 0
 ADA S
 LDB H1 
 ADB 1
 ADB S
 STA INDUM
 STB INDUP
 DLD INDUP,I
 DST INDUM,I
 ISZ H1 
 JMP LAB1.
* 
SUB23 NOP 
 JSB RETU2
 CCA     COPY BLOCK 
 ADA TSI
 ADA 0
 ADA S
 LDB 0,I
 STB H1 
 ADB Y
 STB H3 
 INA
 INA
 LDB 0,I
 STB H2 
LAB2. LDA H1
 INA
 CMA,INA
 ADA H3 
 SSA
 JMP *+16 
 LDA H2 
 ADA 0
 ADA S
 LDB H1 
 ADB 1
 ADB S
 STA INDUM
 STB INDUP
 DLD INDUM,I
 DST INDUP,I
 ISZ H1 
 ISZ H2 
 JMP LAB2.
 LDA TSI
 ADA .-2
 STA TSI
 JMP RPINT
* 
SUB24 NOP 
 JSB RETU2
 JSB ICHKW   LITERAL
 LDA TSI
 ADA 0
 ADA S
 LDB Y
 STB 0,I
 JMP RPINT
* 
SUB25 NOP 
 JSB RETU2
 JSB ICHKW   LOAD REAL
 LDA Y
 JSB RCOSC
 LDA TSI
 ADA 0
 ADA S
 STA INDUM
 DLD RCNST
 DST INDUM,I
 JMP RPINT
* 
SUB26 NOP 
 JSB RETU2
 LDA Y      FLOAT 
 CMA,INA
 ADA TSI
 STA H1 
 ADA 0
 ADA S
 STA INDUM
 LDA 0,I
 FLT
 DST INDUM,I
 JMP RPINT
* 
SUB27 NOP 
 JSB RETU2
 LDA TSI    READ
 ADA 0
 ADA S
 LDA 0,I
 ADA 0
 ADA S
 STA INDUM
 LDB Y
 CPB .+1
 JMP LAB3.
 CPB .+2
 JMP LAB4.
 CPB .+4
 JMP LAB5.
LAB3. JSB READI 
 JMP *+5
LAB4. JSB READR 
 JMP *+3
LAB5. JSB GETCH 
 STA INDUM,I
 CCA
 ADA TSI
 STA TSI
 JMP RPINT
* 
SUB28 NOP 
 JSB RETU2
 LDA TSI     WRITE STRING 
 ADA 0
 ADA S
 LDA 0,I
 STA H1 
 LDA Y
 STA H2 
 CCA
 ADA TSI
 STA TSI
 LDA H1 
 JSB LGCHK
 LDA H2 
 JSB STAAD
 LDB H1 
 JSB OUTST
 JMP RPINT
* 
LGCHK NOP 
 LDB LLENG
 INB
 CMB,INB
 ADB OUTPT
 ADB 0
 SSB
 JMP LGCHK,I
 LDA LNGCK
 STA PS 
 JMP RPEXT
* 
OUTST NOP 
 STA H4STR
 STB H1STR
 CLA
OUTSS STA H3STR 
 LDA H4STR,I
 SEZ,CME,RSS
 ALF,ALF
 ELB
 STB H5STR
 JSB ADOUT
 LDB H5STR
 ERB
 SEZ,RSS
 ISZ H4STR
 LDA H3STR
 INA
 CPA H1STR
 JMP OUTST,I
 JMP OUTSS
* 
H1STR BSS 1 
H3STR BSS 1 
H4STR BSS 1 
H5STR BSS 1 
* 
SUB29 NOP 
 JSB RETU2
 LDA Y     WRITE1 
 ADA FLD
 LDA 0,I
 STA INDUM
 JSB LGCHK
LA291 LDA TSI 
 ADA 0
 ADA S
 STA INDUP
 CCA
 ADA TSI
 STA TSI
 LDA Y
 ADA LA292
 LDA 0,I
 JMP 0,I
LA292 DEF * 
 DEF *+4
 DEF *+5
 DEF *+6
 DEF *+7
 JSB WRINT
 JMP RPINT
 JSB WRREA
 JMP RPINT
 JSB WRBOO
 JMP RPINT
 JSB WRCHA
 JMP RPINT
* 
SUB30 NOP 
 JSB RETU2
 LDA TSI      WRITE2
 ADA 0
 ADA S
 LDA 0,I
 STA INDUM
 JSB LGCHK
 CCA
 ADA TSI
 STA TSI
 JMP LA291
* 
SUB31 NOP 
 JSB RETU1
 JMP RPFIN    HALT
* 
SUB32 NOP 
 JSB RETU1
 CCA    EXIT PROCEDURE
 ADA BI 
 STA TSI
LAB7. LDA BI
 INA
 ADA 0
 ADA S
 LDA 0,I
 ADA RELBS
 STA PC 
 LDA BI 
 ADA .+3
 ADA 0
 ADA S
 LDA 0,I
 STA BI 
 JMP RPINT
* 
SUB33 NOP 
 JSB RETU1
 LDA BI     EXIT FUNCTION 
 STA TSI
 JMP LAB7.
* 
SUB34 NOP 
 JSB RETU1
 LDA TSI     INDIRECT 
 ADA 0
 ADA S
 STA INDUM
 LDA 0,I
 ADA 0
 ADA S
 DLD 0,I
 DST INDUM,I
 JMP RPINT
* 
SUB35 NOP 
 JSB RETU1
 LDA TSI        NOT 
 ADA 0
 ADA S
 STA INDUM
 LDA 0,I
 CLB
 SSA,RSS
 CMB
 STB INDUM,I
 JMP RPINT
* 
SUB36 NOP 
 JSB RETU1
 LDA TSI      INTEGER COMPLEMENT
 ADA 0
 ADA S
 STA INDUM
 LDA 0,I
 CMA,INA
 STA INDUM,I
 JMP RPINT
* 
SUB37 NOP 
 JSB RETU1
 LDA TSI   WRITE(FIXED POINT) 
 ADA 0
 ADA S
 LDA 0,I
 STA INDUP
 CCA
 ADA TSI
 STA TSI
 ADA 0
 ADA S
 LDA 0,I
 STA INDUM
 JSB LGCHK
 CCA
 ADA TSI
 STA TSI
 ADA 0
 ADA S
 JSB WRRFX
 CCA
 ADA TSI
 STA TSI
 JMP RPINT
* 
SUB38 NOP 
 JSB RETU1
 CCA           STORE
 ADA TSI
 ADA 0
 ADA S
 STA INDUM
 ISZ INDUM
 ISZ INDUM
 LDA 0,I
 ADA 0
 ADA S
 STA INDUP
 DLD INDUM,I
 DST INDUP,I
 LDA TSI
 ADA .-2
 STA TSI
 JMP RPINT
* 
SUB39 NOP 
 JSB RETU1
 JSB LAB8.   REAL=REAL
 CMA,CLE,INA
 ERA
 JMP LAB9.
* 
SUB40 NOP 
 JSB RETU1
 JSB LAB8.    REAL#REAL 
 SZA
 CCA
 JMP LAB9.
* 
SUB41 NOP 
 JSB RETU1
 JSB LAB8.    REAL<REAL 
 JMP LAB9.
* 
SUB42 NOP 
 JSB RETU1
 JSB LAB8.    REAL<=REAL
 SZA,RSS
 CCA
 JMP LAB9.
* 
SUB43 NOP 
 JSB RETU1
 JSB LAB8.     REAL>REAL
 SZA
 CMA
 JMP LAB9.
* 
SUB44 NOP 
 JSB RETU1
 JSB LAB8.     REAL>=REAL 
 CMA
 JMP LAB9.
* 
SUB45 NOP 
 JSB RETU1
 JSB LAB0.       INT=INT
 JMP SUB39+3
* 
SUB46 NOP 
 JSB RETU1
 JSB LAB0.     INT#INT
 JMP SUB40+3
* 
SUB47 NOP 
 JSB RETU1
 JSB LAB0.     INT<INT
 JMP LAB9.
* 
SUB48 NOP 
 JSB RETU1
 JSB LAB0.     INT<=INT 
 JMP SUB42+3
* 
SUB49 NOP 
 JSB RETU1
 JSB LAB0. INT>INT
 JMP SUB43+3
* 
SUB50 NOP 
 JSB RETU1
 JSB LAB0.     INT>=INT 
 JMP SUB44+3
* 
SUB51 NOP 
 JSB RETU1
 JSB LABZZ        OR
 LDA INDUM,I
 IOR INDUP,I
 STA INDUM,I
 JMP RPINT
* 
SUB52 NOP 
 JSB RETU1
 JSB LABZZ     + INT
 LDA INDUP,I
 CLO
 ADA INDUM,I
 SOC C
 JMP INTOV
 STA INDUM,I
 JMP RPINT
* 
SUB53 NOP 
 JSB RETU1
 JSB LABZZ      - INT 
 LDA INDUP,I
 CMA,INA
 JMP SUB52+4
* 
SUB54 NOP 
 JSB RETU1
 JSB LABZZ    + REAL
 DLD INDUM,I
 CLO
 FAD INDUP,I
 SOC C
 JMP REAOV
 DST INDUM,I
 JMP RPINT
* 
SUB55 NOP 
 JSB RETU1
 JSB LABZZ    - REAL
 DLD INDUM,I
 CLO
 FSB INDUP,I
 SOC C
 JMP REAOV
 DST INDUM,I
 JMP RPINT
* 
SUB56 NOP 
 JSB RETU1
 JSB LABZZ      AND 
 LDA INDUM,I
 AND INDUP,I
 STA INDUM,I
 JMP RPINT
* 
SUB57 NOP 
 JSB RETU1
 JSB LABZZ    * INT 
 LDA INDUM,I
 MPY INDUP,I
 SSA
 INB
 SZB
 JMP INTOV
 STA INDUM,I
 JMP RPINT
* 
INDUP BSS 1 
* 
LABZZ NOP 
 CCA
 ADA TSI
 STA TSI
 ADA 0
 ADA S
 STA INDUM
 INA
 INA
 STA INDUP
 JMP LABZZ,I
* 
LAB8. NOP 
 JSB LABZZ
 DLD INDUM,I
 FSB INDUP,I
 JMP LAB8.,I
* 
LAB9. CLB 
 SSA
 CMB
 STB INDUM,I
 JMP RPINT
* 
LAB0. NOP 
 JSB LABZZ
 LDA INDUP,I
 CMA,INA
 ADA INDUM,I
 JMP LAB0.,I
* 
LAB6. NOP 
 LDA INDUP,I
 SZA
 JMP *+4
 LDA DIVCK
 STA PS 
 JMP RPEXT
 LDA INDUM,I
 SSA
 CCB,RSS
 CLB
 DIV INDUP,I
 JMP LAB6.,I
* 
SUB58 NOP 
 JSB RETU1
 JSB LABZZ      DIV 
 JSB LAB6.
 STA INDUM,I
 JMP RPINT
* 
SUB59 NOP 
 JSB RETU1
 JSB LABZZ      MOD 
 JSB LAB6.
 STB INDUM,I
 JMP RPINT
* 
SUB60 NOP 
 JSB RETU1
 JSB LABZZ   * REAL 
 DLD INDUM,I
 CLO
 FMP INDUP,I
 SOC C
 JMP REAOV
 DST INDUM,I
 JMP RPINT
* 
SUB61 NOP 
 JSB RETU1
 JSB LABZZ      / REAL
 DLD INDUM,I
 CLO
 FDV INDUP,I
 SOC C
 JMP REAOV
 DST INDUM,I
 JMP RPINT
* 
SUB62 NOP 
 JSB RETU1
 LDA EOLIN   READLN 
 SZA
 JMP RPINT
 JSB GETCH
 JMP *-4
* 
SUB63 NOP 
 JSB RETU1
 JSB WRILN   WRITELN
 LDA LLIMT
 CMA,INA
 ADA LNCNT
 SSA
 JMP *+4
 LDA LINCK
 STA PS 
 JMP RPEXT
 ISZ LNCNT
 JMP RPINT
* 
INTOV DLD AABB. 
 JSB ERR1 
REAOV DLD AACC. 
 JSB ERR1 
* 
AABB. ASC 2,00OF   INTEGER OVERFLOW 
AACC. ASC 2,01OF   REAL OVERFLOW
* 
RPEXT JSB WRILN 
 LDA .+9
 STA MSSG1
 LDA PC 
 CMA,INA
 ADA RELBS
 CMA,INA
 LDB .+5
 JSB DCONV
 DEF MSSG1
 LDA PS 
 LDB MERRO
 CPA RUN
 JMP RPEXX
 LDB MSSG2
 CPA CASCK
 JMP RPEXX
 LDB MSSG3
 CPA DIVCK
 JMP RPEXX
 LDB MSSG4
 CPA INXCK
 JMP RPEXX
 LDB MSSG5
 CPA STKCK
 JMP RPEXX
 LDB MSSG6
 CPA LINCK
 JMP RPEXX
 LDB MSSG7
 CPA LNGCK
 JMP RPEXX
 LDB MSSG8
 CPA REDCK
 JMP RPEXX
 LDB MSS10
 CPA NUMCK
 JMP RPEXX
 LDB MSS11
 CPA OVFCK
 JMP RPEXX
 JMP RPPPP
* 
MSSG. DEF *+1 
 DEC 6
 ASC 3, STEPS 
MSSGG DEF *+1 
 DEC 11 
 ASC 6, CALLED AT 
MSSG1 NOP 
 ASC 5,0HALT AT 
 BSS 2
 ASC 6, BECAUSE OF
MSSG0 BSS 12
MERRO DEF *+1 
 DEC 9
 ASC 9,ERROR (SEE ABOVE)
MSSG2 DEF *+1 
 DEC 7
 ASC 7,UNDEFINED CASE 
MSSG3 DEF *+1 
 DEC 7
 ASC 7,DIVISION BY 0
MSSG4 DEF *+1 
 DEC 7
 ASC 7,INVALID INDEX
MSSG5 DEF *+1 
 DEC 8
 ASC 8,STORAGE OVERFLOW 
MSSG6 DEF *+1 
 DEC 8
 ASC 8,TOO MUCH OUTPUT
MSSG7 DEF *+1 
 DEC 7
 ASC 7,LINE TOO LONG
MSSG8 DEF *+1 
 DEC 12 
 ASC 12,READING PAST END OF FILE
MSS10 DEF *+1 
 DEC 8
 ASC 8,DIGIT NOT FOUND
MSS11 DEF *+1 
 DEC 8
 ASC 8,NUMBER OVERFLOW
MSSG9 DEF MSSG0 
* 
RPEXX LDA 1,I 
 STA *+9
 ADA 0
 ADA .+12 
 ADA MSSG1
 STA MSSG1
 INB
 LDA 1
 LDB MSSG9
 JSB MOVES
 NOP
 JSB MOVEO
 DEF MSSG1
RPPPP LDA BI
 STA H1 
 LDA .+10 
 STA BLKCN
RPXXX JSB WRILN 
 CCA
 ADA BLKCN
 STA BLKCN
 SZA,RSS
 STA H1 
 LDA H1 
 ADA .+4
 ADA 0
 ADA S
 LDA 0,I
 STA H2 
 LDA H1 
 SZA,RSS
 JMP RPYYY
 LDA BLANK
 JSB ADOUT
 LDA H2 
 JSB TABSC
 LDA NAMAD
 CLE
 LDB .+6
 JSB OUTST
 LDA MSSGG
 LDB 0,I
 CLE,INA
 JSB OUTST
 LDA H1 
 INA
 ADA 0
 ADA S
 LDA 0,I
 JSB OTNUM
 JSB WRILN
RPYYY LDA H2
 JSB TABSC
 LDA REF
 JSB BTASC
 LDA LAST 
 STA H2 
RPZZZ LDA H2
 SZA,RSS
 JMP RPWWW
 JSB TABSC
 LDA OBJ
 CPA VARIA
 RSS
 JMP RPQQQ
 LDA TYP
 JSB INSET
 DEF STYPS
 JMP RPQQQ
 LDA .-4
 STA INDUP
 LDA BLANK
 JSB ADOUT
 ISZ INDUP
 JMP *-3
 LDA NAMAD
 CLE
 LDB .+6
 JSB OUTST
 LDA BLANK
 JSB ADOUT
 LDA EQUAL
 JSB ADOUT
 LDA BLANK
 JSB ADOUT
 LDA H1 
 ADA ADR
 LDB NORMA
 SZB
 JMP *+4
 ADA 0
 ADA S
 LDA 0,I
 STA H3 
 ADA 0
 ADA S
 STA INDUP
 LDA TYP
 ADA FLD
 LDA 0,I
 STA INDUM
 LDA TYP
 ADA LA293
 LDA 0,I
 JMP 0,I
LA293 DEF * 
 DEF *+4
 DEF *+5
 DEF *+6
 DEF *+7
 JSB WRINT
 JMP RPNNN
 JSB WRREA
 JMP RPNNN
 JSB WRBOO
 JMP RPNNN
 JSB WRCHA
RPNNN JSB WRILN 
RPQQQ LDA LINK
 STA H2 
 JMP RPZZZ
RPWWW LDA H1
 ADA .+3
 ADA 0
 ADA S
 LDA 0,I
 STA H1 
 SSA,RSS
 JMP RPXXX
RPFIN JSB WRILN 
 JSB SIXBL
 LDA OCNT 
 JSB OTNUM
 LDA MSSG.
 LDB 0,I
 CLE,INA
 JSB OUTST
 JSB WRILN
 JMP .TERM     TERMINATE
* 
CH BSS 1
* 
GETCH NOP 
 LDA EOFWR
 SSA,RSS
 JMP *+4
 LDA REDCK
 STA PS 
 JMP RPEXT
 LDA CCGTC
 CPA LLGTC
 RSS
 JMP EXGTC
 JSB %READ
 DEF *+5
 DEF LUNDA
LINEQ DEF LINED 
 DEF LLNG 
 CLB
 SZB,RSS
 JMP EFGTC
 STB LLGTC
 ISZ LLGTC
 CLA
 STA CCGTC
 STA EOLIN
EXGTC ISZ CCGTC 
 LDB CCGTC
 CPB LLGTC
 JMP GTBLC
 CCA
 ADA 1
 CLE,ERA
 ADA LINEQ
 LDA 0,I
 SEZ,RSS
 ALF,ALF
 AND .377 
 STA CH 
 JMP GETCH,I
GTBLC CCA 
 STA EOLIN
 LDA BLANK
 JMP *-5
EFGTC CCA 
 STA EOFWR
 JMP *-4
* 
EOLIN OCT 0 
EOFWR OCT 0 
CCGTC OCT 0 
LLGTC OCT 0 
* 
LLENG DEC 136 
LLNG DEC -120 
* 
READI NOP 
 JSB READZ
 LDA IDNUM
 SZA
 JMP LIT5Z
 LDB IDNU1
 SSB
 JMP LIT5Z
 LDA SIGNI
 SZA
 CMB,INB
 STB INDUM,I
 CCA
 ADA CCGTC
 STA CCGTC
 CLA
 STA EOLIN
 JMP READI,I
LIT5Z LDA OVFCK 
 STA PS 
 JMP RPEXT
* 
READZ NOP 
 CLA
 STA SIGNI
 STA IDNUM
 STA IDNU1
 STA EXPF 
 STA EXVAL
 STA EXPSN
 STA FCNT 
 JSB GETCH
 CPA BLANK
 JMP *-2
 CPA MINAS
 RSS
 JMP *+4
 ISZ SIGNI
 JSB GETCH
 JMP *+3
 CPA PLUAS
 JSB GETCH
 JSB DIGIT
 JMP *+4
LITZ5 LDA NUMCK 
 STA PS 
 JMP RPEXT
 JSB LIT9Z
 CLB
 STB FCNT 
 JMP READZ,I
* 
SIGNI BSS 1 
* 
LIT9Z NOP 
LIT1Z ISZ FCNT
 LDB EXPF 
 SZB
 JMP LIT0Z
 LDA CH 
 AND .+17B
 STA TTTTT
 LDA IDNUM
 LDB IDNU1
 CLE,ELB
 ELA
 CLE,ELB
 ELA
 SEZ,RSS
 SSA
 JMP LIT0Z
 ADA IDNUM
 ADB IDNU1
 SEZ
 CLE,INA
 CLE,ELB
 ELA
 SEZ,RSS
 SSA
 JMP LIT0Z
 ADB TTTTT
 SEZ
 CLE,INA
 SSA
 JMP LIT0Z
 STA IDNUM
 STB IDNU1
 RSS
LIT0Z ISZ EXPF
 JSB GETCH
 JSB DIGIT
 JMP LIT1Z
 JMP LIT9Z,I
* 
READR NOP 
 JSB READZ
 CPA POINT
 JMP TPOIZ
 CPA EEEEE
 JMP TEEEZ
 JMP LIZ51
TPOIZ JSB GETCH 
 JSB DIGIT
 RSS
 JMP LITZ5
 JSB LIT9Z
 CPA EEEEE
 RSS
 JMP LIZ51
TEEEZ JSB GETCH 
 CPA PLUAS
 JMP LIZ17
 CPA MINAS
 JMP LIZ18
LIZ19 JSB DIGIT 
 JMP LIZ50
 JMP LITZ5
LIZ18 ISZ EXPSN 
LIZ17 JSB GETCH 
 JMP LIZ19
LIZ50 LDB EXVAL 
 BLS,BLS
 ADB EXVAL
 BLS
 AND .+17B
 ADA 1
 STA EXVAL
 JSB GETCH
 JSB DIGIT
 JMP LIZ50
LIZ51 LDA EXVAL 
 LDB EXPSN
 SZB
 CMA,INA
 ADA EXPF 
 LDB FCNT 
 CMB,INB
 ADA 1
 STA EXVAL
 LDA .+31 
 STA EXPF 
 LDA IDNUM
 LDB IDNU1
 SZA,RSS
 SZB
 JMP LIZ54
LIZ66 DST RNUM
LIZ77 LDA SIGNI 
 SZA,RSS
 JMP *+4
 JSB ..DLC
 DEF RNUM 
 JMP *+3
 DLD RNUM 
 DST INDUM,I
 CCA
 ADA CCGTC
 STA CCGTC
 JMP READR,I
LIZ54 JSB NORML 
 LDA EXVAL
 SZA
 JMP LIZ53
 LDB IDNUM
 LDA IDNU1
 CLE
 ADA .+200B 
 SEZ
 CLE,INB
 ALF,ALF
 AND .377 
 ALF,ALF
 STA TTTT1
 SSB,RSS
 JMP *+3
 RBR
 CLA,INA,RSS
 CLA
 ADA EXPF 
 STB RNUM 
 JSB TWEEQ
 DEC -128 
 DEC 127
 JMP EXPOZ
 SSA,RSS
 JMP LIT5Z
 CLA
 CLB
 JMP LIZ66
EXPOZ RAL 
 AND .377 
 IOR TTTT1
 STA RNUM+1 
 JMP LIZ77
LIZ53 SSA 
 JMP LIZ55
 ADA .-1
 STA EXVAL
 LDA EXPF 
 ADA .+3
 STA EXPF 
 LDA IDNUM
 LDB IDNU1
 CLE,ERA
 ERB
 CLE,ERA
 ERB,CLE
 ADB IDNU1
 SEZ
 INA
 ADA IDNUM
 JMP LIZ54
LIZ55 INA 
 STA EXVAL
 LDA .-3
 ADA EXPF 
 STA EXPF 
 LDA IDNU1
 LDB TENTH
 JSB MPY
 CLE
 ADB 0
 SEZ
 INA
 STA IDNU1
 LDA IDNUM
 LDB TENTH
 JSB MPY
 CLE
 ADB 0
 ADB IDNU1
 SEZ
 INA
 JMP LIZ54
* 
WRCHA NOP 
 LDB INDUM
 CMB,INB
 STB INLOC
WRCHH LDA BLANK 
 ISZ INLOC
 JMP *+4
 LDA INDUP,I
 JSB ADOUT
 JMP WRCHA,I
 JSB ADOUT
 JMP WRCHH
* 
INLOC BSS 1 
* 
WRINT NOP 
 CLA
 STA SIGNI
 LDA INDUP,I
 SSA,RSS
 JMP *+3
 ISZ SIGNI
 CMA,INA
 CLB,INB
 STB INLOC
 LDB INBFA
 STB INBFP
 CLB
 DIV .+10 
 ADB ZEROD
 STB INBFP,I
 SZA,RSS
 JMP *+4
 ISZ INBFP
 ISZ INLOC
 JMP *-9
 LDA SIGNI
 SZA,RSS
 JMP *+5
 ISZ INBFP
 ISZ INLOC
 LDA MINAS
 STA INBFP,I
 LDA INLOC
 INA
 CMA,INA
 ADA INDUM
 SSA
 JMP *+7
 CMA
 STA INLOC
 LDA BLANK
 JSB ADOUT
 ISZ INLOC
 JMP *-3
 LDA INBFP,I
 JSB ADOUT
 LDA INBFP
 CPA INBFA
 JMP WRINT,I
 ADA .-1
 STA INBFP
 JMP *-7
* 
INBFA DEF *+1 
 BSS 6
INBFP BSS 1 
* 
WRREA NOP 
 DLD INDUP,I
 JSB WRREE
 JMP WRREA,I
 LDA FLD
 ADA .+2
 LDA 0,I
 CMA,INA
 ADA INDUM
 SSA,RSS
 JMP *+4
 LDA BLANK
 JSB ADOUT
 JMP *+11 
 CMA
 STA INLOC
 LDA BLANK
 JSB ADOUT
 ISZ INLOC
 JMP *-3
 LDA FLD
 ADA .+2
 LDA 0,I
 STA INDUM
 LDA SIGNI
 JSB ADOUT
 LDA .+10 
 CMA,INA
 ADA INDUM
 LDB .+2
 SSA,RSS
 ADB 0
 CMB,INB
 STB DIGCT
 STB DIGZT
NUMO6 JSB GETDG 
 ADA ZEROD
 STA NMPTR,I
 ISZ NMPTR
 ISZ DIGCT
 JMP NUMO6
 JSB GETDG
 ADA .-5
 SSA
 JMP NUMO9+1
 LDB NMPTR
NUMO7 ADB .-1 
 LDA 1,I
 INA
 CPA NINED+1
 RSS
 JMP NUMO9
 CPB NMBFA
 JMP NUMO8
 LDA ZEROD
 STA 1,I
 JMP NUMO7
NUMO8 ISZ EXPON 
 NOP
 LDA ONEDG
NUMO9 STA 1,I 
 CCB
 STB DIGCT
 ADB DIGZT
 STB DIGZT
 LDB NMBFA
 STB NMPTR
 JMP *+5
NMO15 ISZ DIGCT 
 JMP *+3
 LDA POINT
 JMP *+3
 LDA NMPTR,I
 ISZ NMPTR
 JSB ADOUT
 ISZ DIGZT
 JMP NMO15
 LDA EEEEE
 JSB ADOUT
 LDA MINAS
 LDB EXPON
 SSB
 CMB,INB,RSS
 LDA PLUAS
 STB EXPON
 JSB ADOUT
 LDA ZEROD
 JSB ADOUT
 LDA EXPON
 CLB
 DIV .+10 
 ADA ZEROD
 ADB ZEROD
 STB EXPON
 JSB ADOUT
 LDA EXPON
 JSB ADOUT
 JMP WRREA,I
* 
ZEROD EQU .+60B 
NMBFA DEF NUMBF 
NUMBF BSS 136 
MANT1 BSS 1 
MANT2 BSS 1 
EXPZ BSS 1
EXPON BSS 1 
DIGCT BSS 1 
DIGZT BSS 1 
NMPTR BSS 1 
NINED EQU .+71B 
ONEDG EQU .+61B 
TENT. OCT 63146 
HHMMS OCT 174000
BB377 OCT 377 
FD0 BSS 1 
NT0 BSS 1 
TEMP2 BSS 1 
* 
MBY10 NOP 
 LDA MANT1
 SZA,RSS
 JMP MBY10,I
 LDB EXPZ 
 ADB .+3
 STB EXPZ 
 LDB MANT2
 CLE,ERA
 ERB
 CLE,ERA
 ERB,CLE
 ADB MANT2
 SEZ
 INA
 ADA MANT1
 SSA,RSS
 JMP MBY01
 CLE,ERA
 ERB
 ISZ EXPZ 
 NOP
MBY01 STA MANT1 
 STB MANT2
 JMP MBY10,I
* 
DBY10 NOP     LIKE MBY10, BUT DIVISION BY 10. 
 LDA MANT1
 SZA,RSS
 JMP DBY10,I
 LDB .-2
 ADB EXPZ 
 STB EXPZ 
 LDA MANT2
 CLE,ERA
 MPY TENT.
 CLE,ELA
 ELB,CLE
 ADA 1
 SEZ
 CLE,INB
 STB MANT2
 LDA MANT1
 MPY TENT.
 ADA 1
 ADA MANT2
 SEZ
 INB
 STA INLOC
 LDA 1
 LDB INLOC
 JSB NORM.
 JMP DBY10,I
* 
GETDG NOP 
 JSB MBY10
 LDB EXPZ 
 CMB,INB
 AND HHMMS
 RAL
 SSB,INB
 JMP *-2
 AND BB377
 STA FD0
 LDB EXPZ 
 CMB,INB
 RAR
 SSB,INB
 JMP *-2
 XOR MANT1
 LDB MANT2
 JSB NORM.
 LDA FD0
 JMP GETDG,I
* 
NORM. NOP 
 STA NT0
 CLA
 STA TEMP2
 LDA NT0
 SZA,RSS
 SZB
 JMP NOR2.+1
 STA EXPZ 
 STA MANT1
NOR1. STB MANT2 
 JMP NORM.,I
NOR2. ISZ TEMP2 
 CLE,ELB
 ELA
 SEZ,SSA,RSS
 JMP NOR2.
 SEZ,SSA
 JMP NOR2.
 ERA
 ERB,CLE
 STA MANT1
 LDA TEMP2
 CMA,INA
 ADA EXPZ 
 STA EXPZ 
 LDA MANT1
 JMP NOR1.
* 
WRREE NOP 
 SZA
 JMP NUMO1
 LDA ZEROD
 STA NUMBF
 LDA NMBFA
 STA INDUP
 JSB WRCHA
 JMP WRREE,I
NUMO1 ISZ WRREE 
 STA NUMBF
 SSA,RSS
 JMP *+5
 JSB ..FCM
 STA NUMBF
 LDA MINAS
 RSS
 LDA BLANK
 STA SIGNI
 LDA NUMBF
 STA MANT1
 JSB .FLUN
 STB MANT2
 STA EXPZ 
 CLA
 STA EXPON
 CPA EXPZ 
 JMP NUMO5
NUMO0 JSB MBY10 
 LDA EXPZ 
 CMA,SSA,INA,SZA
 JMP *+3
 ISZ EXPON
 JMP NUMO0
 JSB DBY10
 LDA EXPON
NUMO4 LDB EXPZ
 CMB,INB
 SSB,RSS
 JMP NUMO5
 STA EXPON
 JSB DBY10
 CCA
 ADA EXPON
 JMP NUMO4
NUMO5 CMA 
 STA EXPON
 LDA NMBFA
 STA NMPTR
 JMP WRREE,I
* 
WRBOO NOP 
 LDA INDUM
 ADA .-6
 SSA
 JMP *+7
 CMA
 STA INLOC
 LDA BLANK
 JSB ADOUT
 ISZ INLOC
 JMP *-3
 LDA .TRUE
 LDB INDUP,I
 CLE,SSB,RSS
 LDA .FALS
 LDB .+5
 JSB OUTST
 JMP WRBOO,I
* 
.TRUE DEF *+1 
 ASC 3, TRUE
.FALS DEF *+1 
 ASC 3,FALSE
* 
WRRFX NOP 
 DLD 0,I
 JSB WRREE
 JMP WRRFX,I
 CLB
 LDA INDUP
 SSA
 STB INDUP
 LDA EXPON
 INA
 STA EXPON
 ADA INDUP
 SSA,RSS
 JMP SIGDG
 LDA INDUP
 ADA .+4
 CMA,INA
 ADA INDUM
 SSA
 JMP *+7
 CMA
 STA INLOC
 LDA BLANK
 JSB ADOUT
 ISZ INLOC
 JMP *-3
 LDA SIGNI
 JSB ADOUT
 LDA ZEROD
 JSB ADOUT
 LDA POINT
 JSB ADOUT
 CCA
 ADA INDUP
 SSA
 JMP WRRFX,I
 CMA
 STA INLOC
 LDA ZEROD
 JSB ADOUT
 ISZ INLOC
 JMP *-3
 JMP WRRFX,I
SIGDG LDA EXPON 
 ADA INDUP
 CMA
 STA DIGCT
 LDA NMBFA
 STA NMPTR
NUM.6 JSB GETDG 
 ADA ZEROD
 STA NMPTR,I
 ISZ NMPTR
 ISZ DIGCT
 JMP NUM.6
 JSB GETDG
 ADA .-5
 SSA
 JMP NUM.9+1
 LDB NMPTR
NUM.7 ADB .-1 
 LDA 1,I
 INA
 CPA NINED+1
 RSS
 JMP NUM.9
 CPB NMBFA
 JMP NUM.8
 LDA ZEROD
 STA 1,I
 JMP NUM.7
NUM.8 ISZ EXPON 
 NOP
 LDA ONEDG
NUM.9 STA 1,I 
 CCA
 ADA EXPON
 SSA,RSS
 JMP DGBFS
 LDA INDUP
 ADA .+4
 CMA,INA
 ADA INDUM
 SSA
 JMP *+7
 CMA
 STA INLOC
 LDA BLANK
 JSB ADOUT
 ISZ INLOC
 JMP *-3
 LDA SIGNI
 JSB ADOUT
 LDA ZEROD
 JSB ADOUT
 LDA POINT
 JSB ADOUT
 LDA EXPON
 SSA,RSS
 JMP *+6
 STA INLOC
 LDA ZEROD
 JSB ADOUT
 ISZ INLOC
 JMP *-3
 LDA INDUP
 ADA EXPON
 CMA,INA
 STA INLOC
 LDA NMBFA
 STA NMPTR
 LDA NMPTR,I
 JSB ADOUT
 ISZ NMPTR
 ISZ INLOC
 JMP *-4
 JMP WRRFX,I
DGBFS LDA INDUP 
 ADA EXPON
 ADA .+3
 CMA,INA
 ADA INDUM
 SSA
 JMP *+7
 CMA
 STA INLOC
 LDA BLANK
 JSB ADOUT
 ISZ INLOC
 JMP *-3
 LDA SIGNI
 JSB ADOUT
 LDA NMBFA
 STA NMPTR
 LDA EXPON
 CMA,INA
 STA INLOC
 LDA NMPTR,I
 JSB ADOUT
 ISZ NMPTR
 ISZ INLOC
 JMP *-4
 LDA POINT
 JSB ADOUT
 CCA
 ADA INDUP
 SSA
 JMP WRRFX,I
 CMA
 STA INLOC
 LDA NMPTR,I
 JSB ADOUT
 ISZ NMPTR
 ISZ INLOC
 JMP *-4
 JMP WRRFX,I
* 
 EXT .PACK
RANDM NOP   RANDOM NUMBER GENERATOR 
 CLA
 STA EXPRN
 LDA XHRND
 ALS
 ADA XHRND
 LDB XLRND
 CLE,ERB
 ADA 1
 LDB XLRND
 RBL,CLE,SLB,ERB
 INA
 CLE,ELB
 ADB XLRND
 ELA,CLE,SLA,ERA
 CLE,INA
 ADB FLRND
 SEZ
 INA
 ELA,CLE,ERA
 STA XHRND
 STB XLRND
 JSB .PACK
EXPRN OCT 0 
 JMP RANDM,I
XHRND OCT 0 
XLRND OCT 1 
FLRND OCT 100000
* 
SUB64 NOP 
 LDA SUB64,I
 STA PARNO
 LDA SUB64
 INA
 STA SUB64
 INA
 STA PARAD
 INA
 STA PARAD,I
 STA SUBAD
 CCA
 ADA PARNO
 CMA,INA
 ADA TSI
 ADA 0
 ADA S
 STA PAROT
 LDB PARNO
 CMB
 STB PARNO
SU71 ISZ PARNO
 RSS
 JMP SUB64,I
 LDA SUBAD,I
 LDB PAROT
 SSA,RSS
 JMP DIRCT
 LDB 1,I
 ADB 1
 ADB S
 STB PAROT,I
 LDB PAROT
 ELB
 CCE
 ERB
DIRCT STB SUBAD,I 
 ISZ PAROT
 ISZ PAROT
 ISZ SUBAD
 ISZ PARAD,I
 JMP SU71 
* 
PARNO BSS 1 
PARAD BSS 1 
SUBAD BSS 1 
PAROT BSS 1 
* 
SUB65 NOP 
 STA SUBAD
 LDA BI 
 ADA 0
 ADA S
 STA PARAD
 LDA SUBAD
 DST PARAD,I
 JMP SUB65,I
* 
 END
