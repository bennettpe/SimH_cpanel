
; restore RTE-III disc image with RTE-IV distribution files loaded

copy -q  rtgen/rte3host/RTE-III_rte4_loaded.disc    RTE-III.disc 

; Boot RTE-III system

set cpu 1000-M
set cpu 1024K

set dpc 13210A
set ds0 7925
set ds1 7906

set ptr dev=10
set clk dev=11
set ds  dev=12
set msc dev=13
set mpx dev=15
set tty dev=16
set ptp dev=17
set lpt dev=20
set lps dev=21
set dpc dev=22
set tv      sc=45
set scope   sc=46
set plotter sc=50

att -q dpc0 RTE-III.disc

; Enable used devices
set lps enabled

; Disable unused devices
set dqc disabled
set drc disabled
set mux disabled
set mtc disabled

; Configure simulation environment
set tty noautolf
set console del=177
send after=200k
send delay=30k

; Bootstrap the system.

d s 0
boot dpc0

go until ":\x0d\x0a:"

; load answer file

send "PU,ANSFL\r"
go until ":" 
att -q ptr rtgen/answer_file.txt
send "ST,5,ANSFL::::-1\r"
go until "-1"
go until ":" 
det -q ptr


; relocate & create RT4GN prog (segmented + 19 pages of mem)

send "LG,20\r"
go until ":" 
send "MR,RT4G1\r"
go until ":"
send "MR,RT4G2\r"
go until ":"
send "RU,LOADR,99,1,,19001,2\r"
go until "LOADR:$END"
go until ":"

; run RT4GN 

send "RU,RT4GN\r"
go until "LIST FILE?"
; Send output to LU 7 = EQT 0 = nul device
send "7\r"
go until "ECHO?"
send "YES\r"
go until "TRACKS IN OUTPUT FILE?"
send "TR,ANSFL\r"

go until "RT4GN FINISHED"
go until ":"

; save RTE-III disc image with RTE-IV system & boot files generated

det -q dpc0 

copy -q   RTE-III.disc    rtgen/rte3host/RTE-III_rte4_generated.disc 

