

; restore RTE-III disc image with RTE-IV system files generated

copy -q   rtgen/rte3host/RTE-III_rte4_generated.disc    RTE-III.disc 

; Boot RTE-III system

set cpu 1000-M
set cpu 1024K

set dpc 13210A
set ds0 7925
set ds1 7906

set ptr dev=10
set clk dev=11
set ds  dev=12
set msc dev=13
set mpx dev=15
set tty dev=16
set ptp dev=17
set lpt dev=20
set lps dev=21
set dpc dev=22
set tv      sc=45
set scope   sc=46
set plotter sc=50

att -q dpc0 RTE-III.disc
att -q dpc1 -n RTE-IV.disc

; Enable used devices
set lps enabled

; Disable unused devices
set dqc disabled
set drc disabled
set mux disabled
set mtc disabled

; Configure simulation environment
set tty noautolf
set console del=177
send after=200k
send delay=30k

; Bootstrap the system.

d s 0
boot dpc0

go until "\n:"

send "LG,20\r"
go until ":" 
send "MR,SWTH4\r"
go until ":"
; 8-> replace RTE3 SWTCH with RTE4 one
; needs 18 pages of mem
; 001 because SWTCH is a segmented program (else error SC05)
send "RU,LOADR,99,1,8,18001,2\r"
go until "LOADR:$END"
go until ":"

; run SWITCH 

; 22=disc select code. Use B postfix to state as Octal value (if not, interpreted as decimal value)
send "RU,SWTCH,,22B\r"

go until "FILE NAME OF NEW RTE SYSTEM?" 
go until "\n"
send "!SYSTM\r"

go until "TARGET PLATTER/UNIT FOR NEW SYSTEM?" 
go until "\n"
send "2\r"

go until "INSERT CORRECT CARTRIDGE" 
go until "TARGET PLATTER/UNIT."
go until "\n"
send " \r"

go until "SAVE FILES AT TARGET?" 
go until "\n"
send "N\r"

go until "INITIALIZE SUBCHANNELS  ?" 
go until "\n"
send "Y\r"

go until "INITIALIZE SUBCHANNEL  1?" 
go until "\n"
send "Y\r"

go until "TARGET PLATTER?" 
go until "\n"
send "3\r"

go until "OK TO PROCEED?" 
go until "\n"
send "Y\r"

go until "SWTCH FINISHED"
go until ":"

; discard used host RTE-III disc image 

det dpc0 
det dpc1

del  -q RTE-III.disc 
copy -q RTE-IV.disc     rtgen/rte3host/RTE-IV_unint.disc 

