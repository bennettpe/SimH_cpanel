ASMB,B,R,L,C
      HED PRTN  TO RETURN PRAMETERS TO THE SCHEDULING PROGRAM 
      NAM PRTN,6
      ENT PRTM
      ENT PRTN
      EXT $LIBR,$LIBX 
      SPC 2 
*  THIS ROUTINE IS USED TO PASS FIVE PARAMETERS TO THE PROGRAM
*  THAT SCHEDULED THE CALLER WITH WAIT. 
* 
*  THE SCHEDULING PROGRAM MAY RECOVER THESE PARAMETERS WITH RMPAR.
* 
*  THE WAIT FLAG IS CLEARED SO THE CALLER SHOULD HAVE HIGHER
*  PRIORITY THAN THE SCHEDULER TO PREVENT A SWAP. 
* 
*  CALLING SEQUENCE:
* 
*     JSB PRTN
*     DEF *+2       STANDARD FORTRAN SEQUENCE 
*     DEF PRAM      ADDRESS OF THE FIVE RETURN PRAMATERS
*     JSB EXEC      PROGRAM SHOULD COMPLETE 
*     DEF *+2 
*     DEF SIX 
      SPC 3 
PRTN  NOP           ENTRY POINT 
      JSB $LIBR     GO DO PRIVLEDGE THING 
COUNT NOP 
      LDA PRTN      GET THE ADDRESS OF THE CALL PRAMS 
      LDB A,I       GET RETURN ADDRESS
      STB RTN       SAVE IT 
      INA           STEP TO PRAM ADDRESS
      LDA A,I       GET PRAM ADDRESS
      RAL,CLE,SLA,ERA  REMOVE POSSIBLE INDIRECT 
      JMP *-2       IF INDIRECT TRY AGAIN 
      STA PRTN      SAVE THE PRAM ADDRESS 
      LDA KEYWD     GET HEAD OF THE KEY WORD LIST 
      STA PRTM      SAVE IT LOCALLY 
      JMP NEXT1+1   GO SCAN THE LIST
      SPC 1 
NEXT  CLB,INB       ADD ONE 
      ADB A         TO IT TO GET THE WAIT ID ADDRESS
      STB ID        ALSO THE PRAM SAVE ADDRESS SAVE IT
      LDB B,I       GET THE WORD
      CPB XEQT      THIS THE SCHEDULING PROGRAM?
      JMP FOUND     LOOKS GOOD  GO CHECK THE STATUS 
NEXT1 ISZ PRTM      STEP KEYWORD ADDRESS
      LDA PRTM,I    GET NEXT ENTRY
      SZA           IF END OF LIST EXIT 
      JMP NEXT      NOT END  TRY NEXT ID
      SPC 1 
EXIT  LDA OP1       RESET THE OPTION FOR PRTN ENTRY 
      STA OPTIN 
      JSB $LIBX     EXIT TO THE SYSTEM EXIT ROUTINE 
      DEF RTN       RETURN ADDRESS
      SPC 1 
RTN   NOP 
ID    NOP 
STAT  NOP 
      SPC 2 
FOUND LDB D5        CACULATE LAST PRAM ADDRESS
      ADB A         TO B
      STB LAST      SAVE IT FOR TESTING 
      ADB D10       CALCULATE THE STATUS ADDRESS
      STB STAT      SAVE IT 
      LDB B,I       GET STATUS OF SCHEDULER 
      BLF,SLB       IS HE WAITING?
OPTIN ERB,CLE,SLB,ELB  OR INA,RSS FOR PRTM
      JMP NEXT1     NO TRY NEXT PGM 
      BLF,BLF       ROTATE B THE REST 
      BLF           OF THE WAY AROUND 
      STB STAT,I    SET STATUS TO SCHEDULED 
OVER  INA           SET TO PREM AREA
      LDB PRTN,I    GET FIRST PRAM
      STB A,I       SET PRAM
      ISZ PRTN      STEP ADDRESS
      CPA LAST      LAST PRAMETER?
      CLB,INB,RSS   YES B_1 AND SKIP
      JMP OVER      NO GO DO NEXT ONE 
      ADA D5        YES SET TO B REG ADDRESS
      LDB ID        GET ADDRESS OF PRAM AREA
      STB A,I       SET BREG SAVE TO POINT TO PRAMS 
      JMP EXIT      DONE RETURN TO PGM
      SPC 3 
OP1   ERB,CLE,SLB,ELB 
INARS INA,RSS       PRTM ENTRY A OPTION 
LAST  NOP 
D5    OCT 5 
D10   DEC 10
      SPC 2 
PRTM  NOP           OPTIONAL ENTRY FOR FOUR 
      JSB $LIBR     PRAMETER PASS WITH OUT CLEARING 
      NOP           THE WAIT BIT
      LDA INARS     GET THE OPTIONAL INSTRUCTION
      STA OPTIN     SET IT IN THE CODE
      LDA PRTM      GET THE RETURN ADDRESS
      STA PRTN      SET IT IN THE MAIN ENTRY POINT
      JMP COUNT+1   GO TO MAIN LINE AND DO THE JOB
      SPC 2 
A     EQU 0 
B     EQU A+1 
KEYWD EQU 1657B 
XEQT  EQU 1717B 
      END 
