ASMB,B,R,L,C
      NAM CLOSE,7 
      ENT CLOSE 
      EXT EXEC,.ENTR,R/W$ 
      SUP 
* 
*  THIS IS THE CLOSE SUBROUTINE--A PART OF THE
*  REAL-TIME FILE MANAGEMENT PACKAGE
* 
*  THE ASSEMBLY CALL TO CLOSE A FILE IS:
* 
*     JSB CLOSE 
*     DEF RTN       RETURN ADDRESS
*     DEF IDCB      DATA CONTROL BLOCK ADDRESS
*     DEF IERR      (OPTIONAL) ERROR CODE RETURNED HERE AND IN A REG
*     DEF IRX       (OPTIONAL) NO. OF 128 WORD DOUBLE 
*RTN                SECTORS TO BE DELETED FROM THE FILE 
* 
*     ERRORS ARE: 
*     0       NONE
*    -1       DISC DOWN 
*    -10      NOT ENOUGH PARAMETERS 
*    -11      FILE NOT OPEN 
*    -13      DISC LOCKED 
* 
* 
* 
      SKP 
IDCB  DEF ZERO      DCB ADDRESS 
IERR  DEF IDCB      ERROR CODE ADDRESS
IRX   DEF ZERO      TRUNICATE CODE ADDRESS
      SPC 1 
CLOSE NOP           ENTRY POINT 
      JSB .ENTR     TRANSFER THE ADDRESSES
DM    DEF IDCB
      LDA IDCB      IF NO PARAMETERS
      CPA DZ        THEN
      JMP ER10      ERROR EXIT
      INA           STEP TO WORD TWO
      STA DCB2      SAVE FOR D.RTR CALL 
      ADA .8        ADD 8 TO GET THE THE OPEN FLAG
      STA OPNFL     SAVE THE OPEN FLAG ADDRESS
      LDB A,I       GET THE OPEN FLAG 
      ADA N2        BACK UP TO THE
      STA SC        SAVE THE SECURITY CODE ADDRESS
      CPB XEQT      FILE OPEN?
      CLE,RSS       YES SKIP
      JMP ER11      NO; ERROR EXIT
      LDB IDCB      GET THE DCB ADDRESS 
      JSB R/W$      CALL TO FLUSH THE BUFFER
      JMP EXIT      DISC ERROR EXIT 
      LDB DCB2      GET THE TYPE FLAG 
      INB           TO
      LDA B,I       A 
      SZA           IF ZERO NO TRUNCATE 
      LDA IRX,I     DISC FILE SET TRUNCATE CODE 
      ALS           ADJUST FOR 64 WORD SECTORS
      ADB .13       STEP TO EXTENT WORD 
      LDB B,I       IF NOT
      SZB           FIRST EXTENT
      CLA           DO NOT ALLOW TRUNCATION 
      LDB SC,I      GET THE SECURITY FLAG 
      SSB,RSS       IF BAD SC 
      CLA           DIS ALLOW TRUNCATION
      CMA,INA       SET NEGATIVE
      STA IRX       SAVE
SCHED JSB EXEC      CALL EXEC 
      DEF SCHRT     TO
      DEF .9        SCHEDULE WITH WAIT
      DEF D.RTR     D.RTR 
      DEF XEQT      WITH THE ID 
      DEF IRX       THE TRUNCATE WORD 
      DEF IDCB,I    THE FIRST DCB WORD
DCB2  NOP           THE SECOND DCB WORD 
DZ    DEF ZERO      AND THE CLOSE CODE
SCHRT SZA           SCHEDULE OK 
      JMP SCHED     NO; TRY AGAIN 
      SPC 2 
      STA OPNFL,I   CLEAR THE OPEN FLAG 
      LDA B,I       YES; GET ERROR RETURN 
EXIT  STA IERR,I    SET THE ERROR CODE
      LDB DM        RESET 
      STB IERR      THE CALL WORDS
      LDB DZ        FOR THE 
      STB IRX       NEXT CALL 
      STB IDCB      AND 
      JMP CLOSE,I   EXIT ERROR CODE IN A
      SPC 3 
ER11  CCA           FILE NOT OPEN - ERROR 11
ER10  ADA N10       NOT ENOUGH PRAMS - ERROR 10 
      JMP EXIT      GO EXIT 
      SPC 3 
N10   DEC -10 
N2    DEC -2
.8    DEC 8 
.9    DEC 9 
.13   DEC 13
SC    NOP 
OPNFL NOP 
ZERO  NOP 
D.RTR ASC 3,D.RTR 
      SPC 2 
      SPC 3 
A     EQU 0 
B     EQU 1 
XEQT  EQU 1717B 
      SPC 1 
END   EQU * 
      SPC 1 
      END 
