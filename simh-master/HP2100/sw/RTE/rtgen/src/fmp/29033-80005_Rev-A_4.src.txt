ASMB,B,R,L,Z,C
      IFN 
      NAM P.PAS,6 
      EXT $LIBR,$LIBX 
      XIF 
      IFZ 
      NAM P.PAS,7 
      ENT P.PAS 
      XIF 
*  P.PAS IS USED TO SET UP ADDRESS OR TO MOVE 
*  INFORMATION FROM THE CALL AREA 
* 
*     CALLING SEQUENCE: 
* 
*     E=0     SET UP CALL AREA
*     E=1     MOVE FROM CALL AREA 
*     B=0     SET ADDRESSES ONLY
*     B=100000   MOVE PARAMETERS
*     A   =   ADDRESS OF OTHER AREA OR FIRST ADDRESS
* 
*     JSB P.PAS 
*     DEC -N        N= NO. OF WORDS TO BE SET UP
*     BSS N         CALL AREA BUFFER
*                   IF B IS 0, THIS WILL BE A 
*                   LIST OF ADDRESSES; IF B=100000, 
*                   THIS WILL BE THE WORDS AT THE 
*                   ADDRESS PROVIDED IN A.
* 
* 
P.PAS NOP 
      IFN 
      JSB $LIBR     CALL FOR PRIVILEGE
      NOP 
      XIF 
      ADB LOAD      CONFIGURE THE LOAD
      STB NEXT      AND SET IT
      LDB P.PAS,I   GET THE COUNT 
      STB COUNT     AND SET 
      ISZ P.PAS     STEP TO PRAM AREA 
      LDB P.PAS     ADDRESS TO B
      SEZ           IF FROM 
      SWP           SWAP ADDRESSES
      STB DEST      SAVE THE DESTINATION ADDRESS
NEXT  LDB A         GET ADDRESS OR IF LDB A,I 
      STB DEST,I    A WORD - SET IF 
      ISZ DEST      STEP DESTINATION
      INA           STEP FROM 
      ISZ COUNT     STEP COUNT - DONE?
      JMP NEXT      NO; GET NEXT ONE
      IFZ 
      SEZ           YES; EXIT TO
      JMP A,I       END OF CALL 
      JMP DEST,I    SEQUENCE
      XIF 
      IFN 
      SEZ,RSS       PRIVILEGE - COMPUTE 
      LDA DEST      RETURN ADDRESS AND
      STA P.PAS     SET IT
      JSB $LIBX     CALL SYSTEM 
      DEF P.PAS     TO RETURN 
      XIF 
      SPC 5 
      IFN 
COUNT EQU P.PAS+2 
      XIF 
      IFZ 
COUNT NOP 
      XIF 
DEST  NOP 
LOAD  LDB A 
TEST  NOP 
      SPC 2 
A     EQU 0 
B     EQU 1 
END EQU * 
      END 
