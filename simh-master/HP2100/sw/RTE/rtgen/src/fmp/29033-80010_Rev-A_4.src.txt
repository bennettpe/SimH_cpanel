ASMB,B,R,L,C
      NAM READ.,7 
      ENT READ. 
      EXT %READ,.ENTR 
* 
* 
*     READ.    IS AN INTERFACE ROUTINE TO CALL THE
*              READS ROUTINE FROM FORTRAN.
* 
*     THE CALL IS:
* 
*     CALL READ.(LU,BUF,RQLN,RTNLN) 
* 
*     W H E R E:
* 
*     LN       IS THE LOGICAL UNIT. 
*     BUF      IS THE USER'S BUFFER 
*     RQLN     IS THE REQUEST LENGTH IN WORDS.
*     RTNLN    IS THE RETURN LENGTH IN WORDS. 
* 
*     ON END OF FILE RTNLN IS SET TO -1.
* 
* 
LU    NOP 
BUF   NOP 
LN    NOP 
L     NOP 
READ. NOP 
      JSB .ENTR     GET PARAMETERS
      DEF LU
      LDA LN,I      SET LENGTH
      CMA,INA       TO NEGATIVE 
      ALS           CHARACTERS
      STA LN        AND STORE 
      JSB %READ     CALL READS ROUTINE
      DEF *+5       NORMAL RETURN 
      DEF LU,I      LU
      DEF BUF,I     BUFFER
      DEF LN        LENGTH
      CCB,RSS       EOF RETURN - SET TLOG TO -1 
      INB           NORMAL RETURN ROUND UP
      BRS           CONVERT TO WORDS
      STB L,I       STORE IN USER AREA
      JMP READ.,I   RETURN
      END 
