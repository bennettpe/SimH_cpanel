ASMB,B,R,L,C
      HED FMGR ERROR EXPANDER MODULE  PART OF RTE FMP 
      NAM ??..,8
      ENT ??..
      EXT FM.ER,TMP.,WRITF,O.BUF,.ENTR,.R.E.,.E.R.
      EXT IER.,BUF.,IFLG. 
      EXT FM.AB,OPEN. 
      SUP 
N     NOP 
LST   NOP 
      SPC 1 
??..  NOP           ENTRY POINT 
      JSB .ENTR     GEN PRAMS 
      DEF N 
      SPC 1 
      LDB .R.E.     GET ERROR PRAM
      ASR 16        EXTEND THE SIGN BIT 
      DIV .1000     DIVID 
      LDA B         ERROR CODE TO A 
      LDB LST,I     GET FLAG
      ISZ LST       STEP TO 
      SZB             IF NOT SUPPLIED USE .E.R. 
      LDA LST,I        ELSE USE FIRST PRAM
      CPA .99       IF PRAM=99
      JMP ALL        THEN PRINT ALL CODES ON LIST 
      CPA N101      TREAT 101 
      RSS           AND 
      CPA N102      102 
      JMP ICK       SPECIALLY 
      STA N         SAVE CODE 
      ADA MOSNG     TEST FOR
      SSA             DEFINED  CODE 
      JMP UDF       TOO NEGATIVE
      LDA N 
      ADA NHLP1 
      SSA 
      JMP PRINT     OK - PRINT IT 
      ADA NHLG
      SSA 
      JMP UDF       IN   MID  CODE  GAP - UNDEFINED 
      ADA NHH 
      SSA,RSS 
      JMP UDF       TO  HIGH - UNDEFINED
      LDA N         ADJUST  N 
      ADA NHLG      FOR HIGH GROUP
      STA N         TABLE 
PRINT LDA N         GET  N
      ALS           DOUBLE
      ADA TBAD      ADD TABLE ADDRESS 
PR    LDB A,I       GET MESSAGE ADDRESS 
      STB MSAD      SET AS POINTER
      INA               GET 
      LDB A,I        MESSAGE
      STB A           LENGTH
      CMA,INA       SET FOR 
      STA N          MOVE 
      ADB .2          AND 
      STB LST          OUTPUT 
      DLD FMGR      SET FMGR
BF    DST BUF.       AT 
      LDA BUF.D       HEAD
      RSS 
      LDA A,I          OF 
      RAL,CLE,SLA,ERA   MESSAGE 
      JMP *-2            GET
      ADA .2              BUFFER ADDRESS AND
MOVE  LDB MSAD,I    MOVE
      STB A,I        MESSAGE
      INA             TO
      ISZ MSAD         THE
      ISZ N             BUFFER
      JMP MOVE
      JSB FM.ER     PRINT 
      DEF FMRTN      ON 
      DEF .2          LOG 
BUF.D DEF BUF.          DEVICE
      DEF LST 
FMRTN LDA IFLG.     IF INIT 
      SZA            THEN 
      JMP ??..,I      RETURN
      LDB .R.E.     IF
      STA .R.E. 
      CPB .60        60 
      JMP FM.AB       THE ABORT 
      JMP ??..,I    ELSE, RETURN
      SPC 3 
ICK   SZB           IF NOT REAL 
      JMP UDF       THEN UNDEFINED
      LDB ER1+1     FIX 
      CPA N102      MESSAGE 
      INB           AND 
      STB EM101+1   THEN
      LDA DF101     GO
      JMP PR         PRINT IT 
      SPC 2 
UDF   LDA DFUDF     PICK UN DEFINED 
      JMP PR        AND SEND IT.
      SPC 3 
ALL   LDA IFLG. 
      SZA 
      JMP FMRTN     LIST ALL
      LDA TMP.D 
      RSS            POSSIBLE 
      LDA A,I         ERROR 
      RAL,CLE,SLA,ERA  CODES. 
      JMP *-2       GET PRAMETER
      ADA .3         ADDRESS
      STA LST         AND 
      JSB OPEN.        OPEN 
      DEF OPRTN         LIST
      DEF O.BUF          FILE 
TMP.D DEF TMP.
      DEF LST,I 
      DEF .0
OPRTN DLD PTRS      SET THE 
      DST CPTRS      POINTERS FOR LENGTH/BUFFER AD
WRIT  JSB WRITF     WRITE 
      DEF WRRTN      THE
      DEF O.BUF       MESSAGE 
      DEF .E.R.        ON 
CPTRS NOP               THE 
      NOP                LIST 
WRRTN JSB IER.            DEVICE
      DEF *+1       CHECK FOR ERRORS
      LDA CPTRS+1,I IF CURRENT LENGTH 
      SSA           NEGATIVE THEN 
      JMP FMRTN     DONE - RETURN.
      ISZ CPTRS 
      ISZ CPTRS     STEP THE
      ISZ CPTRS+1 
      ISZ CPTRS+1   BUFFER AND LENGTH POINTERS
      JMP WRIT      ELSE GO WRITE NEXT MESSAGE
      SPC 3 
.1000 DEC 1000
.99   DEC 99
N101  DEC -101
N102  DEC -102
.2    DEC 2 
.3    DEC 3 
.60   DEC 60
      SPC 1 
A     EQU 0 
B     EQU 1 
MSTN  EQU 17        MOST NEGATIVE ERROR CODE
HLOW  EQU 23        MOST POSITIVE OF LOW GROUP
LHIG  EQU 50        LOWEST OF HIGH GROUP
HHIG  EQU 60        HIGHEST OF HIGH GROUP 
      SPC 1 
MOSNG ABS MSTN         MOST NEG. CODE 
NHLP1 ABS -HLOW-1      NEG. OF LOW HIGH BOUND 
NHLG  ABS HLOW+1-LHIG  NEG. OF LOW HIGH GAP 
NHH   ABS LHIG-HHIG-1  NEG. OF HIGH SIZE. 
      SPC 1 
BFPT  NOP 
MSAD  NOP 
DFUDF DEF *+1 
      DEF UDN 
      ABS LUDN
FMGR  ASC 2,FMGR
TBAD  DEF MS00
PTRS  DEF LSHED,I 
      DEF LSHED+1 
ER101 DEF EM101 
      ABS L101
EM101 ASC 19,-10* INTERNAL VALIDITY CHECK FAILED SE 
      ASC  7,ND BUG REPORT! 
L101  EQU *-ER101 
UDN   ASC 6, NOT DEFINED
LUDN  EQU *-UDN 
LSHED DEF HEAD      THIS LIST 
      ABS LHEAD      IS IN
      DEF BLNK        THE 
      ABS LBLNK        ORDER
      DEF HD2           OF
      ABS LHD2          PPRINTING 
      DEF BLNK            AND 
      ABS LBLNK            ALSO 
      DEF ERM17             NUMERICAL 
      ABS LM17               ORDER
      DEF ERM16 
      ABS LM16
      DEF ERM15 
      ABS LM15
      DEF ERM14 
      ABS LM14
      DEF ERM13 
      ABS LM13
      DEF ERM12 
      ABS LM12
      DEF ERM11 
      ABS LM11
      DEF ERM10 
      ABS LM10
      DEF ERM9
      ABS LM9 
      DEF ERM8
      ABS LM8 
      DEF ERM7
      ABS LM7 
      DEF ERM6
      ABS LM6 
      DEF ERM5
      ABS LM5 
      DEF ERM4
      ABS LM4 
      DEF ERM3
      ABS LM3 
      DEF ERM2
      ABS LM2 
      DEF ERM1
      ABS LM1 
MS00  DEF ER0 
      ABS L0
      DEF ER1 
      ABS L1
      DEF ER2 
      ABS L2
      DEF ER3 
      ABS L3
      DEF ER4 
      ABS L4
      DEF ER5 
      ABS L5
      DEF ER6 
      ABS L6
      DEF ER7 
      ABS L7
      DEF ER8 
      ABS L8
      DEF ER9 
      ABS L9
      DEF ER10
      ABS L10 
      DEF ER11
      ABS L11 
      DEF ER12
      ABS L12 
      DEF ER13
      ABS L13 
      DEF ER14
      ABS L14 
      DEF ER15
      ABS L15 
      DEF ER16
      ABS L16 
      DEF ER17
      ABS L17 
      DEF ER18
      ABS L18 
      DEF ER19
      ABS L19 
      DEF ER20
      ABS L20 
      DEF ER21
      ABS L21 
      DEF ER22
      ABS L22 
      DEF ER23
      ABS L23 
      DEF ER50
      ABS L50 
      DEF ER51
      ABS L51 
      DEF ER52
      ABS L52 
      DEF ER53
      ABS L53 
      DEF ER54
      ABS L54 
      DEF ER55
      ABS L55 
      DEF ER56
      ABS L56 
      DEF ER57
      ABS L57 
      DEF ER58
      ABS L58 
      DEF ER59
      ABS L59 
      DEF ER60
      ABS L60 
DF101 DEF ER101     EOF RECORD
      DEC -1
.0    NOP           END OF THE LIST 
*     ERROR TABLE -CODES ARE ENTERED IN ANY ORDER.
ER0   ASC  7, 000 NO ERROR
L0    EQU *-ER0 
ERM1  ASC  8, -01 DISC ERROR
LM1   EQU *-ERM1
ERM2  ASC 12, -02 DUPLICATE FILE NAME 
LM2   EQU *-ERM2
ERM3  ASC 11, -03 BACKSPACE ILLEGAL 
LM3   EQU *-ERM3
ERM4  ASC 19, -04 MORE THAN 32786 RECORDS IN A TYPE 
      ASC  4, 2 FILE
LM4   EQU *-ERM4
ERM5  ASC 13, -05 RECORD LENGTH ILLEGAL 
LM5   EQU *-ERM5
ERM6  ASC 18, -06 CR OR FILE NOT FOUND OR NO ROOM 
LM6   EQU *-ERM6
ERM7  ASC 14, -07 BAD FILE SECURITY CODE
LM7   EQU *-ERM7
ERM8  ASC 16, -08 FILE OPEN OR LOCK REJECTED
LM8   EQU *-ERM8
ERM9  ASC 19, -09 ATTEMPT TO USE APOSN OR FORCE TO
      ASC  8,1 A TYPE 0 FILE
LM9   EQU *-ERM9
ERM10 ASC 13, -10 NOT ENOUGH PARAMETERS 
LM10  EQU *-ERM10 
ERM11 ASC  9, -11 DCB NOT OPEN
LM11  EQU *-ERM11 
ERM12 ASC 11, -12 EOF OR SOF ERROR
LM12  EQU *-ERM12 
ERM13 ASC  8, -13 DISC LOCKED 
LM13  EQU *-ERM13 
ERM14 ASC 10, -14 DIRECTORY FULL
LM14  EQU *-ERM14 
ERM15 ASC  9, -15 ILLEGAL NAME
LM15  EQU *-ERM15 
ERM16 ASC 14, -16 ILLEGAL TYPE OR SIZE=0
LM16  EQU *-ERM16 
ERM17 ASC 19, -17 ILLEGAL READ/WRITE ON TYPE 0 FILE 
LM17  EQU *-ERM17 
      SPC 1 
ER1   ASC 14, 001 DISC ERROR-LU REPORTED
L1    EQU *-ER1 
ER2   ASC 10, 002 INITILIZE LU 2! 
L2    EQU *-ER2 
ER3   ASC 10, 003 INITILIZE LU 3! 
L3    EQU *-ER3 
ER4   ASC 18, 004 ILLEGAL RESPONSE TO 002 OR 003
L4    EQU *-ER4 
ER5   ASC 18, 005 REQUIRED TRACK NOT AVAILABLE -
      ASC 15,RELATIVE TAT POSITION REPORTED 
L5    EQU *-ER5 
ER6   ASC 10, 006 FMGR SUSPENDED
L6    EQU *-ER6 
ER7   ASC 10, 007 CHECKSUM ERROR
L7    EQU *-ER7 
ER8   ASC 11, 008 D.RTR NOT LOADED
L8    EQU *-ER8 
ER9   ASC 13, 009 ID-SEGMENT NOT FOUND
L9    EQU *-ER9 
ER10  ASC  8, 010 INPUT ERROR 
L10   EQU *-ER10
ER11  ASC 18, 011 DO OF,XXXXX,8 ON NAMED PROGRAMS 
L11   EQU *-ER11
ER12  ASC 16, 012 DUPLICATE DISC LABEL OR LU
L12   EQU *-ER12
ER13  ASC 11, 013 TR STACK OVERFLOW 
L13   EQU *-ER13
ER14  ASC 17, 014 REQUIRED ID-SEGMENT NOT FOUND 
L14   EQU *-ER14
ER15  ASC 10, 015 LS TRACK REPORT 
L15   EQU *-ER15
ER16  ASC 20, 016 FILE MUST BE AND IS NOT ON LU 2 OR
      ASC  1,3
L16   EQU *-ER16
ER17  ASC 16, 017 ID SEGMENT NOT SET UP BY RP 
L17   EQU *-ER17
ER18  ASC 12, 018 PROGRAM NOT DORMANT 
L18   EQU *-ER18
ER19  ASC 19, 019 FILE NOT SET UP BY SP ON CURRENT
      ASC  3,SYSTEM 
L19   EQU *-ER19
ER20  ASC 11, 020 ILLEGAL TYPE 0 LU 
L20   EQU *-ER20
ER21  ASC 14, 021 ILLEGAL DISC SPECIFIED
L21   EQU *-ER21
ER22  ASC 10, 022 COPY TERMINATED 
L22   EQU *-ER22
ER23  ASC 14, 023 DUPLICATE PROGRAM NAME. 
L23   EQU *-ER23
      SPC 2 
ER50  ASC 13, 050 NOT ENOUGH PARAMETERS 
L50   EQU *-ER50
ER51  ASC 17, 051 ILLEGAL MASTER SECURITY CODE
L51   EQU *-ER51
ER52  ASC 20, 052 ILLEGAL LU IN RESPONSE TO 002 OR 3
L52   EQU *-ER52
ER53  ASC 14, 053 ILLEGAL LABEL OR ILABEL 
L53   EQU *-ER53
ER54  ASC 11, 054 DISC NOT MOUNTED
L54   EQU *-ER54
ER55  ASC 11, 055 MISSING PARAMETER 
L55   EQU *-ER55
ER56  ASC  9, 056 BAD PARAMETER 
L56   EQU *-ER56
ER57  ASC 16, 057 BAD TRACK NOT IN FILE AREA
L57   EQU *-ER57
ER58  ASC 18, 058 LG AREA EMPTY 
L58   EQU *-ER58
ER59  ASC 16, 059 REPORTED TRACK UNAVAILABLE
L59   EQU *-ER59
ER60  ASC 19, 060 DO YOU REALLY WANT TO PURGE THIS
      ASC  9,DISC? (YES OR NO). 
L60   EQU *-ER60
      SPC 2 
HEAD  ASC  9, FMGR ERROR CODES
LHEAD EQU *-HEAD
HD2   ASC  9, ERROR     MEANING 
LHD2  EQU *-HD2 
BLNK  ASC  1, 
LBLNK EQU *-BLNK
      ORG *         PROGRAM LENGTH
      END 
