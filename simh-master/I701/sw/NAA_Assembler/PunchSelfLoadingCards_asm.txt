                                  Punch Self Loading Binary Card 
  
                                  (c) April 2021 by Roberto Sancho

                                  Written to allow to create self loading card 
                                  from binary cards generated by NAA SpeedEx assembler 
                                   
                                  To be assembled with Ibm701Assembler.exe cross-assembler

LABEL--- S OPCODE--- OPADDR---    COMMENT   *NNNN -> octal number, else decimal number

           HEAD      NAA          Generate NAA Assembler source  
           HEAD      1000

           ORG       0040  
   
START    + R ADD     LSTO1        INIT CARD BUFFER 
         + STORE A   LSTO         X




RDCARD   + READ      2048         START READING BINARY CARDS
         - COPY      CTRLW        STORE AT LOC 000 CONTROL WORD NNAAAA00TTTT WHERE 
         + TR        L10           NN=NUMBER OF FULLWORDS TO LOAD, AAAA=ADDR TO LOAD, TTTT=LOCATION TO TRANSFER TO
         + TR        LGEN         END OF FILE, GO TO GENERATE SELF LOADING CARD
L10      - R ADD     CTRLW        GET CONTROL WORD
         - STORE     CKSUM        INIT CKSUM
         + A RIGHT   0030         GET NUMBER OF WORDS TO LOAD
         + A LEFT    0019
         + SUB       LSTO
         + STORE     LCPEND     

LSTO     - COPY      /   /        READ DATA WORD FROM CARD
         - L LEFT    0035
         - ADD       CKSUM        UPDATE CHECKSUM
         - STORE     CKSUM        X
         + R ADD     LSTO         INCR DESTINATION ADDR
         + SUB       TWO          X
         + STORE     LSTO         X
         + ADD       LCPEND       X
         + TR +      LSTO         ELSE READ NEXT WORD FROM CARD

LCKSUM   - COPY      RCKSUM       READ CHKSUM WORD FROM CARD (IS LAST WORD)
         - R ADD     RCKSUM       GET THE CHECKSUM IN CARD
         - SUB       CKSUM        IS THE SAME AS CALCULATED?
         + TR 0      RDCARD       YES, GO TO LOAd NEXT CARD
TWO      + STOP      0002         NO, CHECKSUM ERROR STOP
ONE      + STOP      0001         CONSTANT

           HEAD      2000

LGEN     + R ADD     LCOPY1       INIT SOURCE ADDR TO START 
         + STORE     LCOPY        OF CARD BUFFER
         + R ADD     LSTO         GET FIRST FREE ADDR FROM BUFFER
         + STORE A   LCOPY2       SET AS END OF READ LOOP VALUE

PHCARD   + WRITE     1024         START PUNCHING SELF LOADING CARD 
         + R ADD     D23          INIT WORD PUNCHED COUNT TO 24
         + STORE     CTRLW        AS THERE ARE 24 FULL WORDS PER CARD
         
LCOPY    - COPY      /   /        PUNCH WORD
         + R ADD     LCOPY        INCR SOURCE ADDR
         + SUB       TWO          X
         + STORE     LCOPY        X
         + SUB       LCOPY2       OF READ CBUF LOOP 
         + TR 0      LOK          X
         + R ADD     CTRLW        DECR PUNCHED COUNT
         + SUB       ONE          IF ZERO GO TO CREATE NEW CARD
         + STORE     CTRLW        ELSE GO TO PUNCH
         + TR +      LCOPY   
         + TR        PHCARD
         
LOK      + WRITE     2052
         + STOP      0000         PROGRAM FINISHED         
         
LSTO1    - STORE     CBUF         INIT STORE IN CARD BUFFER
LSTO2    - STORE     CBUFEND      END OF CARD BUFFER
LCOPY1   - COPY      CBUF         INIT PUNCH CARD BUFFER
LCOPY2   - COPY      0000         PUNCH END ADDR 
D23      + STOP      0023  

           ORG       EVEN 

CBUF       RES       1000
CBUFEND    RES          2

CTRLW      RES          2
RDWORD     RES          2
CKSUM      RES          2
LCPEND     RES          1
RCKSUM     RES          1


                                  Usage:

                                  Used to generate self loading cards. Converts input binary program 
                                  cards (with control word and checksum) to output self loading cards

                                  Input Card Deck:
                                     a) Self Loading Card
                                     b) This program
                                     c) Input binary cards 

                                  Operation: 
                                     - Select load from card 
                                     - set address to 0000
                                     - press load button

                                  Output:
                                     Self loading cards 

   
                                  Normal Stops:
                                  - STOP At IC 0131 on termination (card reader end of file)

                                  Error stops:
                                  - STOP At IC 0104 checksum error reading input program binary cards


