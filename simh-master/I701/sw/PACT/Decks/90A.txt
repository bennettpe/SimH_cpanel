                                                             (c) May 2025 by Roberto Sancho
                           
                                                             PACT I Compiler
                                                              
                                                             To be assembled with Ibm701Assembler.exe cross-assembler
                           
                                    
                           
                                                             SELF-LOADING PROGRAM TO LOAD ABS BIN PROGRAM CARDS 
                                                             THE FORMAT USED IS SPEEDEX FORMAT 
                           
                                                             WHEN RUNNING SELFLOAD, THESE ARE THE ERROR STOPS:
                                                                STOP 0006   ERROR LOADING CARDS: NO CARD, PLACE DECK ON READ HOPPER AND PRESS START
                                                                STOP 2223   ERROR LOADING CARDS: BAD CHECKSUM 
                           
                           
                                                             ABS BINARY CARD LAYOUT. 
                           
                                                              / ........ [ABS MC INSTR    ] [ABS MC INSTR    ] [            CHECK SUM              ]
                                                             |  ........ .................. .................. .................. .................. 
                           
                                                             |  ........ .................. .................. .................. .................. 
                           
                                                             |  ........ ........^......... .................. .................. .................. 
                           
                                                             |  ........ ........|......... .................. .................. .................. 
                           
                                                             |  ........ ........|......... .................. .................. .................. 
                           
                                                             |  ........ ........|......... .................. .................. .................. 
                           
                                                             |  ........ ........|......... .................. .................. .................. 
                           
                                                             |  ........ [ABS MC INSTR    ] [ABS MC INSTR    ] [ABS MC INSTR    ] [ABS MC INSTR    ] 
                                                             |  ........ [ABS MC INSTR    ] [ABS MC INSTR    ] [ABS MC INSTR    ] [ABS MC INSTR    ] 
                                                             |  ........ [ABS MC INSTR    ] [ABS MC INSTR    ] [ABS MC INSTR    ] [ABS MC INSTR    ] 
                                                             |  ........ [ NN ][   AAAA   ] [ 00 ][   TTTT   ] [ABS MC INSTR    ] [ABS MC INSTR    ] 
                                                                          -----------------------------------
                                                                                     CONTROL WORD
                           
                                                                1ST WORD FROM CARD: NNAAAA00TTTT WHERE 
                                                                                       NN=NUMBER OF FULLWORDS IN CARD (01..22)
                                                                                       AAAA=ADDR TO LOAD TO (AAAA > 0036)
                                                                                       TTTT=LOCATION TO TRANSFER TO (=0 TO READ NEXT CARD)
                                                                LAST WORD FROM CARD IS THE CHECKSUM. NOTE THAT THIS CHKSUM IS COMPUTED
                                                                WITH DIFFERENT PROCEDURE AS THE ONE USED FOR PACT RELATIVE BIN CARDS)
                           
                           
                           
                                      OUTDEC
                                      ORG       0
                           
        DEC
        NNNN OP ADDR COMMENTS
        0000-31 0002 SELFLOAD - COPY      0002         -+ LOAD THE WHOLE CARD IN MEM
        0001+10 0003 TTTT     + R ADD     0003          | AT ADDRESSES 0000..0047 (DEC)
        0002+09 0000 SLEND    + ADD       0000          |
        0003-31 0004          - COPY      0004          |
        0004+13 0003 SLSUM    + STORE A   0003          |
        0005+01 0001          + TR        0001         -+
                                                            
        0006+24 2048 SLRD     + READ      2048         START READING BINARY CARD 
        0007-31 0000          - COPY      0000         STORE AT LOC 0000 CONTROL WORD NNAAAA|00TTTT WHERE 
        0008+01 0010          + TR        SL10               NN=NUMBER OF FULLWORDS TO LOAD, AAAA=ADDR TO LOAD, TTTT=LOCATION TO TRANSFER TO
        0009+00 0006          + STOP      0006         END OF FILE STOP: ERROR LOADING CARDS: NO CARD, PLACE DECK ON READ HOPPER AND PRESS START
        0010-10 0000 SL10     - R ADD     0000         GET CONTROL WORD
        0011-12 0004          - STORE     SLSUM        INIT CHKSUM
        0012+13 0017          + STORE A   SLCOPY       SET LOADING ADDRESS
        0013+23 0030          + A RIGHT   0030         GET NUMBER OF WORDS TO LOAD
        0014+22 0019          + A LEFT    0019         
        0015+05 0017          + SUB       SLCOPY       
        0016+12 0002          + STORE     SLEND        
                                                            
        0017-31 0000 SLCOPY   - COPY      /   /        READ DATA WORD FROM CARD
        0018-20 0035          - L LEFT    0035         GET JUST READ WORD
        0019-09 0004          - ADD       SLSUM        UPDATE CHECKSUM
        0020-12 0004          - STORE     SLSUM              
        0021+10 0017          + R ADD     SLCOPY       INCR DESTINATION ADDR
        0022+05 0039          + SUB       TWO                
        0023+12 0017          + STORE     SLCOPY              
        0024+09 0002          + ADD       SLEND             
        0025+03 0017          + TR +      SLCOPY       ELSE READ NEXT WORD FROM CARD
                                                            
        0026-31 0002 SL26     - COPY      SLEND        READ CHKSUM WORD FROM CARD (IS LAST WORD)
        0027+01 0026          + TR        SL26         TR IF THIS IS NOT LAST WORD OF CARD
        0028+08 0000          + NOOP      0000         CONSTANT
        0029-10 0002          - R ADD     SLEND        GET THE CHECKSUM IN CARD
        0030-05 0004          - SUB       SLSUM        TEST IF CHKSUM MATCHES
        0031+04 0033          + TR 0      SLSUMOK      IF =0 -> CHKSUM MATCHES -> TR CARD LOADED OK
        0032+00 2223          + STOP      2223         CHECKSUM ERROR STOP
                                                            
        0033+10 0001 SLSUMOK  + R ADD     TTTT         GET TRANSFER LOC 
        0034+04 0006          + TR 0      SLRD         IF ZERO, GO TO READ NEXT CARD
        0035+13 0037          + STORE A   SLTRA        ELSE SET TO TRANSFER TO TTTT 
        0036+02 0037          + TR OV     SLTRA        CLEAR OV
        0037+01 0000 SLTRA    + TR        /   /        TRANSFER TO TTTT 
                                    
        0038+08 0000          + NOOP      SELFLOAD     NOT NEEDED. THIS INSTR IS JUST TO AVOID A "NOT REFERENCED" WARNING ON ASSEMBLING     
        0039+00 0002 TWO      + STOP      0002         
                           
        0040+00 0000          + STOP      0000         Loc 0040: Spare
        0041+00 0000          + STOP      0000         Loc 0041: Spare
        0042+00 0000          + STOP      0000         Loc 0042: Spare
        0043+00 0000          + STOP      0000         Loc 0043: Spare
        0044+00 0000          + STOP      0000         Loc 0044: Spare
        0045+00 0000          + STOP      0000         Loc 0045: Spare
        0046+00 0000          + STOP      0000         Loc 0046: Spare
        0047+00 0001          + STOP      0001         Loc 0047: Spare
                           
