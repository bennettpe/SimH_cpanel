; SYMBOL TABLE
; Reg Addr   Len
;  I     0    82
;  T     0     0
;    Num  Op.  Addr  Name
; SY   0  DEF     0  PRTNLINE
; SY   1  USE        PRTALF
; SY   2  USE        PRTSTR
; SY   3  USE        PRTCHR
; SY   4  USE        PRTINT
; SY   5  USE        PRTOCT
; SY   6  USE        CLRBUF
; SY   7  USE        PRINTCRD
;  
         0000 +A  I 0075 PRTNLINE + ADD       TWO          PRINT A NEW LINE OF DATA
         0001 +ST I 0078          + STORE     PARAM        SAVE PARAM LIST ADDR
         0002 +RA I 0002          + R ADD     *            CLEAR CARD BUFFER
         0003 +T  S 0006          + TR        CLRBUF 
         0004 +RA I 0078 GPARAM   + R ADD     PARAM        GET PARAM PAIR FMT/DATA
         0005 +SA I 0010          + STORE A   P1
         0006 +A  I 0074          + ADD       ONE
         0007 +SA I 0014          + STORE A   P2
         0008 +A  I 0074          + ADD       ONE
         0009 +ST I 0078          + STORE     PARAM
         0010 +RA A 0000 P1       + R ADD     /   /
         0011 +ST I 0080          + STORE     FMT
         0012 +TP I 0014          + TR +      P2           IF FMT < 0 THEM SKIP THIS PAIR FMT/DATA
         0013 +T  I 0004          + TR        GPARAM
         0014 +RA A 0000 P2       + R ADD     /   /
         0015 +ST I 0040          + STORE     DATALOC      GET DATA ADDRESS PASED AS PARAM
         0016 +SA I 0017          + STORE A   P2A
         0017 +RA A 0000 P2A      + R ADD     /   /
         0018 +ST I 0079          + STORE     DATA         GET EFFECTIVE DATA
         0019 +RA I 0073          + R ADD     ZERO
         0020 +LM I 0080          + LOAD MQ   FMT
         0021 +D  I 0077          + DIV       D100         ACC=REMAINDER=COL NUMBER, MQ=QUOTIEN=TYPE OF DATA
         0022 +ST I 0081          + STORE     COL
         0023 +RA I 0073          + R ADD     ZERO
         0024 +LL A 0018          + L LEFT    0018
         0025 +SM I 0080          + STORE MQ  FMT
         0026 +LM I 0079          + LOAD MQ   DATA
         0027 +RA I 0031          + R ADD     TYPETRAD
         0028 +A  I 0080          + ADD       FMT
         0029 +SA I 0030          + STORE A   TYPETR
         0030 +T  A 0000 TYPETR   + TR        /   /        TR SELECTED ROUTINE
         0031 +H  I 0032 TYPETRAD + STOP      TYPETRT      TYPE TRANSFER TABLE ADDRESS
         0032 +T  I 0061 TYPETRT  + TR        EOL          FMT=0..6 (IE <100) -> END OF PARAM LIST -> END OF LINE 
         0033 +T  I 0038          + TR        TYSTR        FMT=1 -> PRINT A STRINGZ
         0034 +T  I 0043          + TR        TYINT        FMT=2 -> PRINT AN INT
         0035 +T  I 0048          + TR        TYOCT        FMT=3 -> PRINT AN OCT
         0036 +T  I 0053          + TR        TYCHR        FMT=4 -> PRINT A CHAR
         0037 +T  I 0057          + TR        TYALF        FMT=5 -> PRINT A 3-CHR PACKED ALFA STRING
         0038 +RA I 0038 TYSTR    + R ADD     *            PRINT STRINGZ
         0039 +T  S 0002          + TR        PRTSTR
         0040 +H  A 0000 DATALOC  + STOP      /   /
         0041 -H  I 0081          - STOP      COL          COLUMN WHERE TXT STARTS
         0042 +T  I 0004          + TR        GPARAM
         0043 +RA I 0043 TYINT    + R ADD     *            MQ ALREADY LOADED WITH DATA TO PRINT
         0044 +T  S 0004          + TR        PRTINT
         0045 +H  A 0006          + STOP      0006         UP TO 6 DECIMAL DIGITS 
         0046 -H  I 0081          - STOP      COL          COLUMN WHERE TXT STARTS
         0047 +T  I 0004          + TR        GPARAM
         0048 +RA I 0048 TYOCT    + R ADD     *            MQ ALREADY LOADED WITH DATA TO PRINT
         0049 +T  S 0005          + TR        PRTOCT
         0050 +H  A 0006          + STOP      0006         UP TO 6 DECIMAL DIGITS 
         0051 -H  I 0081          - STOP      COL          COLUMN WHERE TXT STARTS
         0052 +T  I 0004          + TR        GPARAM
         0053 +RA I 0053 TYCHR    + R ADD     *            MQ ALREADY LOADED WITH DATA TO PRINT
         0054 +T  S 0003          + TR        PRTCHR
         0055 -H  I 0081          - STOP      COL          COLUMN WHERE TXT STARTS
         0056 +T  I 0004          + TR        GPARAM
         0057 +RA I 0057 TYALF    + R ADD     *            MQ ALREADY LOADED WITH DATA TO PRINT
         0058 +T  S 0001          + TR        PRTALF
         0059 -H  I 0081          - STOP      COL          COLUMN WHERE TXT STARTS
         0060 +T  I 0004          + TR        GPARAM
         0061 +RA I 0081 EOL      + R ADD     COL          SEND CARD BUFFER TO PRINTER AND RETURN TO CALLER
         0062 +TZ I 0067          + TR 0      DOPRINT      =0 -> PRINT ALLWAYS (NO SENSE SW CHECK)
         0063 +A  I 0076          + ADD       D68          =1..6 -> SENSE 69..74 = OPERATOR'S PANEL SENSE SWITCH 1..6
         0064 +SA I 0065          + STORE A   CHKSW
         0065 +SE A 0000 CHKSW    + SENSE     /   /         
         0066 +T  I 0069          + TR        RETURN       SW NOT AT ON POSITION
         0067 +RA I 0067 DOPRINT  + R ADD     *            PRINT CARD BUFFER
         0068 +T  S 0007          + TR        PRINTCRD
         0069 +RA I 0078 RETURN   + R ADD     PARAM        PARAM-1 IS THE RETURN ADDR      
         0070 +S  I 0074          + SUB       ONE
         0071 +SA I 0072          + STORE A   EXIT
         0072 +T  A 0000 EXIT     + TR        /   /        RETURN TO CALLER
         0073 +H  A 0000 ZERO     + STOP      0000
         0074 +H  A 0001 ONE      + STOP      0001
         0075 +H  A 0002 TWO      + STOP      0002
         0076 +H  A 0068 D68      + STOP      0068
         0077 +H  A 0100 D100     + STOP      100
         0078 +H  A 0000 PARAM    + STOP      0000
         0079 +H  A 0000 DATA     + STOP      0000
         0080 +H  A 0000 FMT      + STOP      0000
         0081 +H  A 0000 COL      + STOP      0000
