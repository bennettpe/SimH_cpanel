                                  (c) May 2025 by Roberto Sancho

                                  PACT I Compiler
                                   
                                  To be assembled with Ibm701Assembler.exe cross-assembler

LABEL--- S OPCODE--- OPADDR---    COMMENT   *NNNN -> octal number, else decimal number


           HEAD      PACTREL      USING PACT RELATIVE ASSEMLER SYNTAX

                                  DEFINE SYMBOLS NAME FOR PACT LOADER

           HEAD      DEF:MEMCOPY         COPY MEMORY STORAGE FROM ONE ADDR TO ANOTHER
                                         PARMS: SRCADDR: SOURCE ADDR
                                                DESTADDR: DESTINATION ADDR 
                                                LEN: LOCATION OF LEN=NUMBER OF HALF-WORDS TO CLEAR

MEMCOPY  + ADD       TWO 
         + STORE A   P1           PARM: SRCADDR 
         + ADD       ONE          
         + STORE A   P2           PARM: DESTADDR
         + ADD       ONE          
         + STORE A   P3           PARM: LEN: LOCATION OF LEN=NUMBER OF HALF-WORDS TO CLEAR
         + ADD       ONE          
         + STORE A   EXIT

P1       + R ADD     /   /        GET SOURCE ADDR 
         + STORE A   MEMS
P2       + R ADD     /   /        GET DESTINATION ADDR 
         + STORE A   MEMD
P3       + R ADD     /   /        GET LEN ADDR
         + STORE A   P3A
P3A      + R ADD     /   /        GET LEN           
         + STORE     LEN

         + LOAD MQ   ZERO

LOOP     + R ADD     LEN
EXIT     + TR 0      /   /
         + SUB       ONE
         + STORE     LEN
MEMS     + R ADD     /   /        
MEMD     + STORE     /   /        
         + R ADD     MEMS
         + ADD       ONE
         + STORE A   MEMS
         + R ADD     MEMD
         + ADD       ONE
         + STORE A   MEMD
         + TR        LOOP

ZERO     + STOP      0000
ONE      + STOP      0001         constant 1
TWO      + STOP      0002         constant 2
LEN      + STOP      0000


