                                  (c) May 2025 by Roberto Sancho

                                  PACT I Compiler
                                   
                                  To be assembled with Ibm701Assembler.exe cross-assembler

LABEL--- S OPCODE--- OPADDR---    COMMENT   *NNNN -> octal number, else decimal number


           HEAD      PACTREL      USING PACT RELATIVE ASSEMLER SYNTAX

                                  DEFINE SYMBOLS NAME FOR PACT LOADER

           HEAD      DEF:READTBL2        TAKE VALUES FROM TABLE

                                  READ MULTIPLE VALUES FROM TABLE, AT GIVE ENTRY. STORE IT IN VAL: VAL=TBL[ENT]
                                  RESULT ALSO IN ACC: ACC=TBL[ENT]
                                  PARAMS: TBL: LOCATION OF TABLE START
                                          MAX: LOCATION OF VAR WITH MAX NUMBER OF ENTRIES IN TABLE
                                          ENT: LOCATION OF ENTRY. FIRST ENTRY OF TABLE IS 1 (STORED AT TBL ADDRESS)
                                          VAL1: LOCATION OF VAL=VALUE READ FROM TABLE
                                          VAL2: LOCATION OF VAL=VALUE READ FROM TABLE
                                          ...
                                          VALN: LOCATION OF VAL=VALUE READ FROM TABLE
                                          0000: END OF PARAMS

READTBL2 + ADD       TWO 
         + STORE A   P1           PARM: ADDR OF TABLE
         + ADD       ONE          
         + STORE A   P2           PARM: MAX LEN OF TABLE
         + ADD       ONE          
         + STORE A   P3           PARM: ENT TO GET
         + ADD       ONE          
         + STORE A   PVAL         PARM: VAL TO GET

P1       + R ADD     /   /        GET ADDR OF TABLE
         + STORE A   TBL
        
P2       + R ADD     /   /        GET ADDR OF MAX LEN OF TABLE
         + STORE A   P2A
P2A      + R ADD     /   /        DERREFERENCE -> ACC=MAX
         + STORE     MAX

P3       + R ADD     /   /        GET ADDR OF ENTRY TO GET
         + STORE A   P3A
P3A      + R ADD     /   /        DERREFERENCE -> ACC=ENT
         + TR 0      ENT0         EXIT, CANNOT READ ON ENTRY 0
         + SUB       ONE          
         + ADD       TBL          TABLE BASE ADDR
         + STORE A   RDTBL        TBL[ENT] ADDRESS

PVAL     + R ADD     /   /        GET ADDR OF VAL TO FIND
         + TR 0      PARAMEND     END OF PARAM LIST
         + STORE A   WRVAL
        
RDTBL    + R ADD     /   /        VAL=TBL[ENT]
WRVAL    + STORE     /   /

         + R ADD     PVAL         SELECT NEXT PARAM FROM LIST
         + ADD       ONE
         + STORE A   PVAL

         + R ADD     RDTBL
         + ADD       MAX
         + STORE A   RDTBL
         + TR        PVAL     

ENT0     + STOP      2222         CANNOT READTBL ON ENTRY 0

PARAMEND + R ADD     PVAL         GET RETURN ADDR AND EXIT
         + ADD       ONE
         + STORE A   EXIT
EXIT     + TR        /   /

MAX      + STOP      0000
TBL      + STOP      0000

ONE      + STOP      0001         constant 1
TWO      + STOP      0002         constant 2


