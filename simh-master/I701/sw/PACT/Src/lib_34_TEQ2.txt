                                  (c) May 2025 by Roberto Sancho

                                  PACT I Compiler
                                   
                                  To be assembled with Ibm701Assembler.exe cross-assembler

LABEL--- S OPCODE--- OPADDR---    COMMENT   *NNNN -> octal number, else decimal number


           HEAD      PACTREL      USING PACT RELATIVE ASSEMLER SYNTAX

                                  DEFINE SYMBOLS NAME FOR PACT LOADER

           HEAD      DEF:EQTBL2          STORE VALUES IN TABLE

                                  STORE MULTIPLE VALUES TO TABLE, AT GIVE ENTRY. STORE VAL: TBL[ENT]=VAL
                                  PARAMS: TBL: LOCATION OF TABLE START
                                          MAX: LOCATION OF VAR WITH MAX NUMBER OF ENTRIES IN TABLE
                                          ENT: LOCATION OF ENTRY. FIRST ENTRY OF TABLE IS 1 (STORED AT TBL ADDRESS)
                                          VAL1: LOCATION OF VAL=VALUE READ FROM TABLE
                                          VAL2: LOCATION OF VAL=VALUE READ FROM TABLE
                                          ...
                                          VALN: LOCATION OF VAL=VALUE READ FROM TABLE
                                          0000: END OF PARAMS

EQTBL2   + ADD       TWO 
         + STORE A   P1           PARM: ADDR OF TABLE
         + ADD       ONE          
         + STORE A   P2           PARM: MAX LEN OF TABLE
         + ADD       ONE          
         + STORE A   P3           PARM: ENT TO SET
         + ADD       ONE          
         + STORE A   PVAL         PARM: VAL TO SET

P1       + R ADD     /   /        GET ADDR OF TABLE
         + STORE A   TBL
        
P2       + R ADD     /   /        GET ADDR OF MAX LEN OF TABLE
         + STORE A   P2A
P2A      + R ADD     /   /        DERREFERENCE -> ACC=MAX
         + STORE     MAX

P3       + R ADD     /   /        GET ADDR OF ENTRY TO SET
         + STORE A   P3A
P3A      + R ADD     /   /        DERREFERENCE -> ACC=ENT
         + TR 0      ENT0         EXIT, CANNOT READ ON ENTRY 0
         + SUB       ONE          
         + ADD       TBL          TABLE BASE ADDR
         + STORE A   WRTBL        TBL[ENT] ADDRESS

PVAL     + R ADD     /   /        GET ADDR OF VAL TO SET
         + TR 0      PARAMEND     END OF PARAM LIST
         + STORE A   RDVAL
        
RDVAL    + R ADD     /   /        TBL[ENT]=VAL
WRTBL    + STORE     /   /

         + R ADD     PVAL         SELECT NEXT PARAM FROM LIST
         + ADD       ONE
         + STORE A   PVAL

         + R ADD     WRTBL
         + ADD       MAX
         + STORE A   WRTBL
         + TR        PVAL     

ENT0     + STOP      2222         CANNOT EQTBL ON ENTRY 0

PARAMEND + R ADD     PVAL         GET RETURN ADDR AND EXIT
         + ADD       ONE
         + STORE A   EXIT
EXIT     + TR        /   /

MAX      + STOP      0000
TBL      + STOP      0000

ONE      + STOP      0001         constant 1
TWO      + STOP      0002         constant 2


