                                  (c) May 2025 by Roberto Sancho

                                  PACT I Compiler
                                   
                                  To be assembled with Ibm701Assembler.exe cross-assembler

LABEL--- S OPCODE--- OPADDR---    COMMENT   *NNNN -> octal number, else decimal number


           HEAD      PACTREL      USING PACT RELATIVE ASSEMLER SYNTAX

                                  DEFINE SYMBOLS NAME FOR PACT LOADER

           HEAD      DEF:CHKSUM          CALCULATE CARD CHECKSUM FOR PACT REL BINARY FORMAT
                                         

                                         PARAMS: 
                                             CARDADDR: CARD IMAGE ADDRESS
                                             RETURNS IN ACC FULLWORD CHECKSUM OF CARD WORDS 00..45
              

CHKSUM   + ADD       TWO          CALC CHEKSUM 
         + STORE A   CKP1
         + ADD       ONE
         + STORE A   CHKSUMEX     RETURN ADDR

CKP1     + R ADD     /   /        GET CARD IMAGE ADDRESS
         + STORE A   RDDATA
         + ADD       D46
         + STORE A   RDEND

         + R ADD     ZERO
         - STORE     SUM
         - STORE     DATA
                  
RDDATA   + R ADD     /   /
         + STORE     DATA2
         - R ADD     DATA
         - ADD       SUM
         - STORE     SUM

         + R ADD     RDDATA
         + ADD       ONE
         + STORE A   RDDATA
         + SUB       RDEND
         + TR 0      CHKSUM2
         + TR        RDDATA
RDEND    + R ADD     0000         
         
CHKSUM2  - R ADD     SUM
         - ADD       SUM          ADD TWICE -> CHKSUM IS DOUBLED -> CHKSUM IS SHIFTED TO LEFT ONE BIT -> RIGHTMOST BIT IS ZERO
CHKSUMEX + TR        /   /

           ORG       EVEN

DATA       DEF       0            CHECKSUM CALCULATION WORDS
DATA2      DEF       0
SUM        DEF       0            
           DEF       0

                                  VARS/CONST HALF-WORD

ZERO       DEF       0
ONE        DEF       1
TWO        DEF       2
D46        DEF       46

