;2345678901234567890123456789012345678901234567890123456789012345678901234567890
; Compute primes using Sieve of Eratosthenes
; 
;       [R][ST][OP][FAC][S1[S2[Q+[NUMBER   ][Notes                             ] 
        RA0  1 DO   RA1                      SIEVE OF ERATOSTHENES PRIMES
             2 DO   RA2                      compute primes
             3 DO   RA4                      prepare prime list 
             4 DO   RA5                      print prime list

        RA1  1 SET       I   1               clear NUM vector
             2 CL   NUM  I
             3 TEST      I 500

             4        1                      NUM[1]=1 mark 1 as non-prime
             5 EQ   NUM   1                  

        RA2  1        2                      for N=2 up to 500           
             2 EQ     N 
             3 DO   RA3                      call RA3 to mark multipes of N as non-prime
             4 DUP  LPN                      duplicate region LPN (loop on N): incr N, acc<0 if N<500 
             5 TN     3                      loop to proceed with next N value

        LPN  1        N                      Loop on N region 
             2 +      1                      N=N+1
             3 EQ     N                      check N with end of loop value
             4 -    500

        RA3  1 USE       I   N
             2      NUM  I                   take NUM[N]
             3 TZ     5                      if =0 -> N is not marked -> N is prime
             4 EXIT                          if<>0 -> N is not prime -> exit to try next N

             5        N                      mark all multiples of N as non-primes
             6 EQ     M                      M = N 

            10        M                      M = M + N
            11 +      N                      if M > 500 then return to caller
            12 EQ     M
            13 -    500
            14 TN    16
            15 EXIT

            16 USE       I   M               NUM[M]=1 -> mark M as non-prime
            17        1
            18 EQ   NUM  I                   
            19 T     10                      proceed with next multiple of N

        RA4  1 CL     P                      P=0 -> number of primes found 
             2        2
             3 EQ     N                      N=2 -> first prime to check 

            10 USE       I   N
            11      NUM  I                   take NUM[N]
            12 TP    20                      if>0 -> N is marked -> N is not prime -> proceed with next N value

            13        P                      if=0 -> N is prime -> incr P = number of primes found
            14 +      1
            15 EQ     P
            16 USE       I   P               store P-th prime number as NUM[P]=N
            17        N
            18 EQ   NUM  I                   
            
            20 DUP  LPN                      duplicate region LPN (loop on N): incr N, acc<0 if N<500 
            21 TN    10

        RA5  1 CL     P
             2        1
             3 EQ     M                      M=1 -> Flag print values

             5 SET       J   1

            10        P                      incr P = number of primes found
            11 +      1
            12 EQ     P
            13 USE       I   P               PRI[J]=NUM[P]
            14      NUM  I                   
            15 EQ   PRI  J
            16 -      1
            17 TP    20                      if NUM[J]=1 -> NUM[J]=0
            18 CL   PRI  J
            19 CL     M                      M=0 -> exit print loop

            20 TEST      J   6

            30 LIST
            31 ID   PRI   1
            32 ID   PRI   2
            33 ID   PRI   3
            34 ID   PRI   4
            35 ID   PRI   5
            36 ID   PRI   6

            40        M                      if M=1 -> keep printing values
            41 TP     5

           999 SE       519                  blank line                        +
;       FAC[1][2]+QQ D1 D2FAC[1][2]TYP LOC[notes                               L
          N       17
          M       17
          P       17
        PRI       17     6
        NUM       17   500                                                     +





