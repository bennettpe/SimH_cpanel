
if "%CPANEL%"=="1" goto chk_cp_ok1

echo
echo ERROR!
echo i701 compiled without control panel support. Build again 
echo with CPANEL, USE_SIM_VIDEO and HAVE_LIBSDL symbols defined 
echo
goto end

:chk_cp_ok1

if exist ../cpanel/ibm701.ini goto chk_cp_ok2

echo
echo ERROR!
echo cannot find the control panel definition file
echo "../cpanel/ibm701.ini" 
echo
goto end

:chk_cp_ok2

; start control panel

echo *** 
echo *** Start Control Panel GUI
echo *** 
echo *** HotKeys on GUI: +/-  Zoom In/Zoom Out (also ^+ and ^-)
echo ***                 ^Y   Toggle zoom 100%% <-> 50%%
echo ***                 ^T   Toggle flash on clickable controls
echo ***                 ^I   Toggle info panel (readable at 100%% and 50%% zoom)      
echo ***                 ^F   Fast mode: while pressed, accelerates cpu to max speed
echo *** 
echo ***           Click on control panel image and drag mouse to move the window
echo ***           Right click mouse to show a tooltip with image at 100% scale
echo ***           Click on control panel image and drag mouse to move the window
echo *** 
echo ***           click card read input hopper/stacker or punched cards stacker
echo ***           to open the Punched card View panel. Hotkeys available:
echo *** 
echo ***                 <-/-> cursor keys   show previous/next card
echo ***                 Home/End            show 1st/last card
echo *** 

; Display main cpu panel
att cpanel ../cpanel/ibm701.ini
set cpanel scale=25
set cpanel pos=50/70
set cpanel option=ShowInfo
set cpanel on

; Display CardViewer cpanel. By default shows Read Stacker
set cpanel select=CardView
set cpanel scale=50
set cpanel pos=990/470
set cpanel on

set cpu speed=1.0
set realtime

echo
echo Use SW6 to toggle PACT compiler log
echo

cd PACT
do run_PACT_production.ini  Validation/pact_Test_SC.txt  NOLOG  Validation/pact_Test_SC_Data.txt

set cpanel interactive

goto end


:end