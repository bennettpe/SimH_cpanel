
; set console -n log=console.txt 
; set debug -n debug.txt
; set debug stdout

; set cpu debug=cmd;data;detail
; set cdr debug=cmd;data;detail
; set cdp debug=cmd;data;detail
; set lp debug=cmd;data;detail
; set mt debug=cmd;data;detail;exp


; if control panel available while assembling, tape MT2 gets mounted 
; to load position, and tape MT3 gets rewinded.
; mt3 tape length set to 1000 feet (instead of default 1200) 
; so it gets rewinded just before assembling finishes (at x1.0 speed)

if (CPANELGUI == 0) goto nocp
set mt3 length=1000
set cpanel option=MT3/R
att -q -n mt3 mt3.tap
att -q -n mt2 mt2.tap
:nocp

echo ***
echo *** Assemble HelloWorld/HelloWorld_naa.txt
echo *** using NAA SpeedEx assembler
echo ***
echo 

do run_NAA_Assembler.ini  HelloWorld/HelloWorld_naa.txt NONE NONE

echo ***
echo *** Assemble HelloWorld/HelloWorld_assembly.txt
echo *** using NR9003 Symbolic Assembly from IBM 
echo ***
echo 

do run_NR9003_Assembly.ini  HelloWorld/HelloWorld_assembly.txt 0310 

echo ***
echo *** Assemble HelloWorld/HelloWorld_regional.txt
echo *** using SO2 Regional Assembler from IBM 
echo ***
echo 

do run_SO2_Regional_Assembler.ini  HelloWorld/HelloWorld_regional.txt 0200

del -q deck_out.dck

echo 
echo ***
echo *** Assemble HelloWorld/HelloWorld_asm.txt
echo *** using Win32 cross assembler
echo ***
echo 

!asm\Ibm701Assembler.exe HelloWorld\HelloWorld_asm.txt

if exist prog.txt goto ok1
echo 
echo *** Assembling error
echo
goto end

:ok1

; output of cross assembler is a prog.txt file
; suitable to be loaded in simh
; move and rename the file 

copy -q prog.txt HelloWorld/Hello_prog.txt
del -q prog.txt

; the load it into simulator

load HelloWorld/Hello_prog.txt

echo 
echo ***
echo *** Run the program
echo ***
echo 

; program starts at location 0040 (decimal)

dep ic 0040
go 

goto end

:end

; clean up

det all
del -q mt2.tap
del -q mt3.tap
