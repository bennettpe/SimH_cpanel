
; set console -n -q log=console.txt 
; set debug -n -q debug.txt

if "%CPANEL%"=="1" goto chk_cp_ok1

echo
echo ERROR!
echo IBM360 compiled without control panel support. Build again 
echo with CPANEL, USE_SIM_VIDEO and HAVE_LIBSDL symbols defined 
echo
goto end

:chk_cp_ok1

if exist ../cpanel/ibm360.ini goto chk_cp_ok2

echo
echo ERROR!
echo cannot find the control panel definition file
echo "../cpanel/ibm360.ini" 
echo
goto end

:chk_cp_ok2

; start control panel

echo *** 
echo *** Start Control Panel GUI
echo *** 
echo *** HotKeys on GUI: +/-  Zoom In/Zoom Out (also ^+ and ^-)
echo ***                 ^Y   Toggle zoom 100%% <-> 50%%
echo ***                 ^T   Toggle flash on clickable controls
echo ***                 ^I   Toggle info panel (readable at 100%% and 50%% zoom)      
echo ***                 ^F   Fast mode: while pressed, accelerates cpu to max speed
echo *** 
echo ***           Click on control panel image and drag mouse to move the window
echo ***           Right click mouse to show a tooltip with image at 100% scale
echo *** 

att cpanel ../cpanel/ibm360.ini

set cpanel option=AllPanels
set cpanel option=DasdType/3330
set cpanel option=tapes/1
set cpanel option=console/1
set cpanel option=CpuType/2065

; set main cpanel window
set cpanel scale=15
set cpanel option=ShowInfo
set cpanel on
set cpanel pos=20/50

; set console cpanel window
set cpanel select=ibm1052
set cpanel scale=70
set cpanel on 
set cpanel pos=20/450
set cpanel select=0

set realtime

set cpanel option=mt0/gg
set cpanel option=mt1/gg
set cpanel option=mt2/rr
set cpanel option=mt3/rr

; first parameter is the working directory
if "%1"=="" goto end
cd %1

; execute script given as parameter
if "%2"=="" goto end

do %2 %3 %4 %5 %6 %7 %8 %9


:end