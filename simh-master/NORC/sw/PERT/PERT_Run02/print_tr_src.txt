
                            Print transactions program
                            by Roberto Sancho Nov 2020

                            tapes input:
                                          09 program
                                          01 transactions tape created in Run 02/03/04/05
                                  output
                                          02 tansactions to print on ctc
                                  on line print:
                                             (only 4 words filled with data)
                                             label of input tape(s) 
                                             66 0..0 NNN | A 0..0 predecessor code | sucesor code
                                             55                                    | pred code    | successor code   

                                             NNN=number of transaction
                                             A=card code

                            stops: IC: 01693 (00 61 0000 0000 1696) -> unrecoverable read error
                                   IC: 01718 (00 61 1721 0000 1727) -> unrecoverable write error

                                   IC: 00001 (00 61 0000 0000 0000) -> finished ok. 

                            Switch 74 set to off      -> output is printed on online printer 1
                                             transfer -> output is written in tape 2

                            Input tape tape transaction record (as written by Run02) for tape 02 successors

                            w1 = A RS PR 0 PPPPPPPPP 0      A=card code PPP=predecessor code, RS=Res.Rate, PR=Pref.Rate
                            w2 = TE
                            w3 = SIGMA
                            w4 = 55 00 00 SSSSSSSSS R       R=report code
                            w5 
                            w6 
                            w7 = SCHEDL 000000 TSTS         TSTS=Schedulled completition time, SCHEDL=Schedulled date (format mm dd yy)

                            Input tape transaction record (as written by Run02) for tape 03 predecessors

                            w1 = A RS PR 0 PPPPPPPPP 0      A=card code PPP=predecessor code, RS=Res.Rate, PR=Pref.Rate
                            w2 
                            w3 
                            w4 = 66 xx xx SSSSSSSSS R       R=report code
                            w5 = TE
                            w6 = SIGMA
                            w7 = SCHEDL xx xxxx TSTS        TSTS=Schedulled completition time, SCHEDL=Schedulled date (format mm dd yy)

                            in input tape transaction are grouped in blocks. Max 28 transaction (7 words each)
                            per block -> max block size = 7*28=196 words
 
LABEL--- PP OPCODE-- RRRR---- MM SSSS---- MM TTTT---- MM COMMENT


                                                         init

0008        57                                           Clear M4 M6 M8
         09 98                                           rewind tape 09 program
            TR       ZERO        TRCOUNT
         01 94       TEMP1       TEMP1       0000        read tape 01 label
            68       *                       REREAD      if tape check Call Re-read
            80       TEMP1       0001                    print label for tape 1
            80       ZERO        0002
            80                   0003
            80                   0004
            80                   0005
            80                   0006
            81       ZERO        0007                    print

                                                         read block from input tape

RB-INBUF 01 94       INBUF       0000                    read input block buffer  
            68       *                       REREAD      if tape check Call Re-read
            67                               END         if end of file transfer to end of input transactions       
            TR                   TEMP1                   Temp1=start of block xx xx aaaa bbbb xxxx
         04 MASK                 FIELD-S     TEMP2       Temp2=bbbb=02 90 aaaa bbbb nnnn maked with 0000 0000 1111 0000 -> 0000 0000 bbbb 0000, then shift 4 right to get 0..0 bbbb
         08 MASK     TEMP1       FIELD-R                 REG1=02 90 aaaa bbbb nnnn maked with 0000 1111 0000 0000 -> 0000 aaaa 0000 0000, then shift 8 right to get 0..0 aaaa 
            SUB      TEMP2             
            ADD                  ONE         BL-INBUF    BL-INBUF=Block Len from Input Transaction=bbbb-aaaa+1
            SUB      N196                                check Block Len from Input Transaction
            TR SGN   LSPL3       LSPL3                   transfer if 196 or less
            STOP                                         stop because input transaction in tape 02 or 03 too big (max 196w, that holds 28 transactions of 7w
LSPL3       TR       ZERO        NW-INBUF                NW-INBUF=Number of words already processed from Input Transaction block
            TR                               RR-INBUF    goto process first transaction record in block
                                                         end of split phase

                                                         read transaction record from from input tape block 

RR-INBUF    SUB      BL-INBUF    NW-INBUF                REG1=words left to be read in input transact block. 
            TR SGN   LRR1        RB-INBUF                If no words left, go to read next block
            STOP                                         Safety: stop because overflowing block rec
LRR1     96 TR       NW-INBUF                            Shift num words read into field S
            ADD      SETM-OP                 LRR2        set instr to ...
LRR2        00       0000        0000        0000        ... clear modif and set M6=NW-INBUF, M8=NW-OUTBUF
LRR3        TR       INBUF   +M6 W1      +M4             copy from input tranasction block to rec w1..w7
            50       0000        0001        0000        ... incr M6
            58       0001        0007        LRR3        ... incr M4, loop
            ADD      NW-INBUF    N7          NW-INBUF    Number of words read from block += 7

                                                         get transaction successor/predecessor code to print
            
            ADD      TRCOUNT     ONE         TRCOUNT     incr total count of transactions 
            TR       ZERO        CODE1
            TR       ZERO        CODE2
            MASK     W4          E55         TEMP1
            ADD                  TRCOUNT     TRSSPP
            TR EQ    TEMP1       E55         LSSCODE   
LPPCODE     MASK     W1          TR-FIELD    CODE1       
            MASK     W4          TR-FIELD    CODE        predecessor transasc: code to sort
            TR                               LPPQQ0
LSSCODE     MASK     W1          TR-FIELD    CODE        successor transasc: code to sort
            MASK     W4          TR-FIELD    CODE2   
LPPQQ0      MASK     W1          E10                     get card code
            ADD      CODE1                   CODE1       put card code in code1
LPPQQ1      TR NEQ   CODE        ZERO        LPPQQ2
            TR       E10         CODE 
LPPQQ2      TR NEQ   CODE2       ZERO        LPPQQ3
            TR       E10         CODE2 
LPPQQ3      TR                               PRT-TR
            
                                                         print transaction 

PRT-TR      80       TRSSPP      0001
            80       CODE1       0002
            80       CODE        0003
            80       CODE2       0004  
            80       ZERO        0005
            74                               SAVE-TAP    if switch 74 set to transfer, goto save to output tape
            81       ZERO        0006
            TR                               RR-INBUF

SAVE-TAP 02 91       0001        0004                    write to tape
         08 60       *                       CHKWRT      Call check Write
            TR                               RR-INBUF

END-TAP  02 91       0000        0000                    write end of tape   
         08 60       *                       CHKWRT      Call check Write
            TR       STOP-OP     0001        0001        set stop in 0001, transfer to 0001: sucessfull program end                             

END         74                               END-TAP     if switch 74 set to transfer, goto save to output tape
            TR       STOP-OP     0001        0001        set stop in 0001, transfer to 0001: sucessfull program end                             


1000                                                     variables


INBUF       RES      200                                 block read from input tape 02 or 03

BL-INBUF                                                 block length
NW-INBUF                                                 Number of words read from block

CODE                                                     predecessor/succesor code as control word for sort
CODE1
CODE2

TRCOUNT                                                  count of transactions to sort
TRSSPP

W1                                                       input transaction record                                                   
W2
W3
W4
W5
W6
W7

TEMP1
TEMP2

ZERO     00 00       0000        0000        0000
ONE      00 00       0000        0000        0001
N7       00 00       0000        0000        0007
N196     00 00       0000        0000        0196

E10      10 00       0000        0000        0000
E55      55 00       0000        0000        0000

FIELD-R  00 00       1111        0000        0000
FIELD-S  00 00       0000        1111        0000  
FIELD-T  00 00       0000        0000        1111
ONE-S    00 00       0000        0001        0000
TR-FIELD 00 00       0011        1111        1110               

STOP-OP  00 61       0000        0000        0000        stop opcode
SETM-OP  00 57       0000        0000        0000        clear M468 and set M468 opcode
NOP-OP   00 60       0000        0000        0000        

1990                                                     Library

                                                         Re-read routine. if error persist then stop 
                              
REREAD      STOP                             REREAD      if read error, stop    

                                                         Check write routine. 

CHKWRT      ADD      MK-TR60                 LCHKEX      set and save exit transfer
            68                               LCHK1       if tape check goto lchk1
LCHKEX      00                                           if no tape chek, return to caller
LCHK1       STOP                             LCHK1       if write error, stop 
MK-TR60  00 59       9999        9140        0001   
     
