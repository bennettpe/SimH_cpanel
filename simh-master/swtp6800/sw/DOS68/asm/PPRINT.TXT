 TTL PRINT SYSTEM
 OPT PAG,NOG
*
* GENERATE PRINTER DRIVER FOR USE BY THE P COMMAND
* THIS VERSION FOR PARALLEL PRINTER ON PORT #6
*
* RUN FILE NAMED 'PPRINT.SYS' MUSTED BE PLACED ON
* SYSTEM/BOOT DISK TO BE RUN BY START.UP FILE
*
 ORG YPPORT PATCH PARAMETER TABLE PRINTER PORT
 FDB $8018 PORT #6
*
* PATCH PARAMETER TABLE HARD COPY INITIALIZATION
*
 ORG ZHCINT
 JMP PINIT INITIALIZATION

* PATCH PARAMETER TABLE HARD COPY CHARACTER OUTPUT

 ORG ZHCOUT
 JMP PCHAR PRINTER CHARACTER OUTPUT
*
 ORG $A016 INTO SCRATCH PAD

PCHAR STX SAVEX SAVE IX & B
 PSHB
 LDX YPPORT GET PORT ADDRESS
 STAA 0,X GIVE DATA TO PIA
 LDAB #$36 GENERATE READY STROBE
 STAB 1,X
 LDAB #$3E
 STAB 1,X
LOOP TST 1,X WAIT FOR DATA ACCEPTED
 BPL LOOP
 LDAB 0,X READ TO CLEAR IRQ
SAVEX EQU *+1
 LDX #SAVEX
 PULB
 RTS
*
 ORG $A04A SPLIT TO TOP OF SCRATCH

PINIT LDX YPPORT
 LDAB #$FF
 STAB 0,X
 LDAB #$3E
 STAB 1,X
 RTS
*
 END PINIT
