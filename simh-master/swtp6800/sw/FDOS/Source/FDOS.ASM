 NAM DOS
******** VERSION 1.00 ***********
* WRITTEN BY R. H. UITERWYK

COMREG EQU $8018
DATREG EQU $801B
RESTRT EQU $2403
READ EQU $242B
READ0 EQU $242E
PRELIM EQU $2523
WRITE EQU $255F
WRITE0 EQU $2561
DRIVE EQU $25AC
TRACK EQU $0000
SECTOR EQU $0001
BMEMH EQU $0004
EMEMH EQU $0006
SAVEX EQU $0009
PROGX EQU $000B
NDRIVE EQU $000D

 ORG $3000
BUFNXT RMB 2
ENDBUF RMB 2
BUFFER RMB 80
CREFLG RMB 1
DECPNT RMB 1
LIBSEC RMB 1
NEEDNO RMB 1
TYPE RMB 1
STARTX RMB 2
ENDX RMB 2
GOX RMB 2
HILINE RMB 2
TEMPX1 RMB 2
TEMPX2 RMB 2
TEMPX3 RMB 2
NAME RMB 8
PASSWD RMB 8
DBUFFR RMB 256
OVERFL RMB 32

 ORG $2600

DOS LDX #COPMSG
 LDAA #$FF
 STAA DECPNT
 JSR OUTPUT
DOS1 LDS #$A049
 JSR DOSTRT
 BRA DOS1
OUTCHA JMP OUTEEE
OUTEEE EQU $E1D1
INCH JMP INEEE
INEEE EQU $E1AC
PRNCHA JMP OUTPIA
OUTPIA STX SAVEX
 LDX #$801C
 STAA 0,X
 LDAA #$36
 STAA 1,X
 LDAA #$3E
 STAA 1,X
OUTPI3 TST 1,X
 BPL OUTPI3
 LDAA 0,X
 LDX SAVEX
 RTS
OUTCH TST DECPNT
 BNE OUTCHA
 BRA PRNCHA
PRINT CLR DECPNT
 JMP READY
****************
RLIB LDAA #3
 BRA LIB1
WLIB LDAA #2
 CLR NEEDNO
 CLR CREFLG
LIB1 PSHA
 LDAA #2
 STAA TRACK
 CLRA
 STAA SECTOR
 JSR PRELIM
LIB2 LDAA SECTOR
 STAA LIBSEC
 LDX #DBUFFR+255
 STX EMEMH
 LDX #DBUFFR
 JSR READ0
 LDX #DBUFFR
 SEI
 STS SAVEX
LIB3 LDAB #16
 STX TEMPX2
 LDS #NAME-1
 LDAA 0,X
 CMPA #$FF
 BEQ LIB7
LIB4 PULA
 CMPA 0,X
 BNE LIB5
 INX
 DECB
 BNE LIB4
 LDS SAVEX
 NOP
 CLI
 JMP LIB8
LIB5 CMPB #9
 BCC LIB55
 LDX #PASERR
 BRA LIB65
LIB55 INX
 DECB
 BNE LIB55
 LDAB #16
LIB57 INX
 DECB
 BNE LIB57
 DEX
 CPX EMEMH
 BEQ LIB58
 INX
 BRA LIB3
LIB58 LDS SAVEX
 NOP
 CLI
 LDAA SECTOR
 CMPA #9
 BNE LIB2
LIB6 LDX #DSKFUL
 PULA
LIB65 JSR CRLF
 JSR OUTPUT
 JMP RESTRT
LIB7 LDS SAVEX
 NOP
 CLI
 PULB
 CMPB #3 * IS IT READ
 BNE LIB75
 LDX #NOTFND
 BRA LIB65
LIB75 PSHB
 INC EMEMH
 STX TEMPX2
 SEI
 STS SAVEX
 LDS #NAME-1
 LDAB #16
LIB77 PULA
 STAA 0,X
 INX
 DECB
 BNE LIB77
 LDS SAVEX
 NOP
 CLI
 TST NEEDNO
 BNE LIB78
 LDAA ENDX
 SUBA STARTX
 INCA
 TAB
 LSRB
 LSRB
 INCB
 ABA
 STAA NEEDNO
LIB78 LDAA 2,X
 LDAB 3,X
 SUBB NEEDNO
 SBCA #00
 BMI LIB6
 STAA 34,X
 STAB 35,X
 CLR 2,X
 LDAB NEEDNO
 STAB 3,X
 ADDB 3,X
 ADDB 1,X
 CLRA
LIB79 INCA
 SUBB #10
 BCC LIB79
 DECA
 ADDB #10
 STAB 33,X
 ADDA 0,X
 STAA 32,X
 LDAA #$FF
 STAA 16,X
 LDAA TYPE
 STAA 4,X
 PULA
 BRA LIB82
LIB8 PULA
 CMPA #2 * ARE WE WRITING
 BNE LIB9
 TST CREFLG
 BEQ LIB82
 LDX #NOTCRE
 JMP LIB65
LIB82 LDAB ENDX
 SUBB STARTX
 INCB
 CMPB 3,X
 BLS LIB88
 LDX #FLFULL
 JMP LIB65
LIB88 LDAB #8
 SEI
 STS SAVEX
 LDS #STARTX-1
LIB89 PULA
 STAA 5,X
 INX
 DECB
 BNE LIB89
 LDS SAVEX
 NOP
 CLI
 LDX TEMPX2
 LDAB TYPE
 LDAA 20,X
 BEQ *+5
 CBA
 BNE LIB91
 STAB 20,X
 LDAA LIBSEC
 STAA SECTOR
LIB895 LDX #DBUFFR
 JSR WRITE0
LIB9 LDX TEMPX2
 LDAA TYPE
 CMPA #$FF
 BEQ LIB95
 TST 20,X
 BEQ LIB95
 CMPA 20,X
 BEQ LIB95
LIB91 LDX #TYPERR
 JMP LIB65
LIB95 LDX 27,X
 STX HILINE
 LDX TEMPX2
 LDX 25,X
 STX GOX
 LDX TEMPX2
 LDX 23,X
 STX EMEMH
 STX ENDX
 LDX TEMPX2
 LDAA 16,X
 STAA TRACK
 LDAA 17,X
 STAA SECTOR
 LDAA 20,X
 STAA TYPE
 LDX 21,X
 STX STARTX
 STX BMEMH
 LDX TEMPX2
RETURN RTS

DSKFUL FCC /DISK FULL - PACK/
 FCB $0D,$0A,$15,4
NOTFND FCC /FILE NOT FOUND/
 FCB $0D,$0A,$15,4
FLFULL FCC /FILE FULL - RENAME/
 FCB $0D,$0A,$15,4
TYPERR FCC /FILE TYPE MISMATCH/
 FCB $0D,$0A,$15,4
PASERR FCC /PASSWORD INVALID/
 FCB $0D,$0A,$15,4
NOTCRE FCC /FILE ALREADY EXISTS/
 FCB $0D,$0A,$15,4

DEL LDX #DELMSG
 JSR OUTPUT
 BRA KEYBD0
KEYBD LDAA #'?
 JSR OUTCH
 LDAA #$20
 JSR OUTCH
KEYBD0 LDX #BUFFER
KEYBD1 JSR INCH
 TSTA
 BEQ KEYBD1
 CMPA #$0A
 BEQ KEYBD1
 CMPA #$03
 BNE *+5
 JMP DOSTRT
 CMPA #$18 * CONTROL X
 BEQ DEL
 CMPA #$0D * IS IT C/R
 BEQ IEXIT
 CMPA #$0F * CONTROL O
 BNE KEYBD2
 LDAA #$5F
 JSR OUTCH
 CPX #BUFFER
 BEQ KEYBD1
 DEX
 BRA KEYBD1
KEYBD2 CPX #BUFFER+72
 BEQ KEYBD1
 STAA 0,X
 INX
 BRA KEYBD1
IEXIT LDAA #$04
 STAA 0,X
 STX ENDBUF
 JSR CRLF
 RTS

DELMSG FCC / DELETED/
 FCB 4

* UNIVERSAL OUTPUT ROUTINE

OUTPUT BSR OUTNCR
 BRA CRLF
OUTPU1 JSR OUTCH
 INX
OUTNCR LDAA 0,X
 CMPA #$04
 BNE OUTPU1
 RTS
CRLF STX CRLFXX
 LDX #CRLFST
 BSR OUTNCR
 LDX CRLFXX
 RTS

CRLFST FCB $0D,$0A,$15
 FCB $FF,$FF,$FF,$FF
 FCB $04
 FCB $FF,$FF,$FF,$FF
CRLFXX RMB 2

SKIPSP LDAA 0,X
 CMPA #$20
 BNE *+5
INXSKP INX
 BRA SKIPSP
 TSTA
 RTS
CCODE BSR SKIPSP
 STX BUFNXT
 SEI
 STS SAVEX
 LDX #DOSLIS-1
LOOP3 LDS BUFNXT
 DES
LOOP4 INX
 PULA
 CMPA #$20
 BEQ *-3
 LDAB 0,X
 BEQ OKCODE
 CBA
 BEQ LOOP4
LOOP5 INX
 CPX #COMEND
 BEQ NOCODE
 LDAB 0,X
 BNE LOOP5
 INX
 INX
 BRA LOOP3
OKCODE INX
 STS BUFNXT
NOCODE LDS SAVEX
 NOP
 CLI
 RTS

DOSLIS FCC /LOAD/
 FCB 0
 FDB LOAD
 FCC /RUN/
 FCB 0
 FDB RUN
 FCC /SAVE/
 FCB 0
 FDB SAVE
 FCC /EXIT/
 FCB 0
 FDB $E0D0
 FCC /DOS/
 FCB 0
 FDB DOS
 FCC /PACK/
 FCB 0
 FDB PACK
 FCC /INIT/
 FCB 0
 FDB INIT
 FCC /TEST/
 FCB 0
 FDB TEST
 FCC /CATALOG/
 FCB 0
 FDB CATAL
 FCC /CAT/
 FCB 0
 FDB CATAL
 FCC /FILES/
 FCB 0
 FDB FILES
 FCC /HOME/
 FCB 0
 FDB HOME
 FCC /CREATE/
 FCB 0
 FDB CREATE
 FCC /PRINT/
 FCB 0
 FDB PRINT
 FCC /RENAME/
 FCB 0
 FDB RENAME
 FCC /DELETE/
 FCB 0
 FDB PURGE
 FCC /PURGE/
COMEND FCB 0
 FDB PURGE

COPMSG FCC /SWTPC V1.0 (C) 1977/
 FCB 4
RDYMSG FCB $0D,$0A,$15,$FF,$FF
 FCC /FDOS READY/
 FCB 4
NAMMSG FCC /FILENAME/
 FCB 4
FIRADD FCC /FIRST ADDRESS/
 FCB 4
LASADD FCC / LAST ADDRESS/
 FCB 4
PSTART FCC /PROGRAM START/
 FCB 4
SECNUM FCC /# OF SECTORS (HEX)/
 FCB 4
SURMSG FCC /ARE YOU SURE/
 FCB 4
TRKMSG FCC /BAD TRACK & SECTOR #/
 FCB 4
NEWMSG FCC /NEW/
 FCB 4
TSTDON FCC /TEST DONE/
 FCB 4

DOSTRT LDS #$A045
 LDX #DOSTRT
 STX PROGX
 LDX #$801C
 LDAB #$FF
 STAB 0,X
 LDAB #$3E
 STAB 1,X
 LDAA #$FF
 STAA DECPNT
READY LDX #RDYMSG
 JSR OUTPUT
DOSTR2 JSR KEYBD0
 LDX #BUFFER
 JSR CCODE
 CPX #COMEND
 BEQ RUNCOM
 LDX 0,X
 JMP 0,X
PACK JSR SELECT
RUNCOM LDX #BUFFER
 JSR SKIPSP
 CMPA #$04
 BEQ DOSTR2
 DEX
 LDAA #'$
 STAA 0,X
 STX BUFNXT
 JSR CLRPAS
 LDAA #$11
 STAA TYPE
 JMP RUN0
SELECT LDX BUFNXT
 JSR SKIPSP
 CMPA #$30
 BMI NOSEL
 CMPA #$39
 BLE OKSEL
NOSEL CLRA
 JSR DRIVE
 RTS
OKSEL SUBA #$30
 CMPA #$03
 BLE OKSEL1
 LDX #DRVERR
 JMP LIB65
OKSEL1 JSR DRIVE
 INX
 JSR SKIPSP
 CMPA #',
 BNE SELEX
 INX
SELEX STX BUFNXT
 RTS

DRVERR FCC /DRIVE ERROR/
 FCB 4

GETPAS LDX #PASSWD
 BRA GETNA0
GETNAM LDX #NAME
GETNA0 STX TEMPX1
 LDAB #8
 LDAA #$20
GETN05 STAA 0,X
 INX
 DECB
 BNE GETN05
 LDX BUFNXT
GETNA1 LDAA 0,X
 INX
 STX BUFNXT
 CMPA #$20
 BEQ GETNEX
 CMPA #$04
 BEQ GETNEX
 CMPA #',
 BEQ GETNEX
 LDX TEMPX1
 STAA 0,X
 INX
 CPX #NAME+8
 BEQ GETNEX
 CPX #PASSWD+8
 BEQ GETNEX
 STX TEMPX1
 LDX BUFNXT
 BRA GETNA1
GETNEX RTS
CLRPAS PSHA
 LDAB #8
 STX TEMPX1
 LDX #PASSWD
 LDAA #$20
CLRPA1 STAA 0,X
 INX
 DECB
 BNE CLRPA1
 LDX TEMPX1
 PULA
 RTS

MAKNAM JSR SELECT
MAKN JSR SKIPSP
 CMPA #$04
 BEQ MAKNA0
 CMPA #$30
 BCS MAKNU1
 CMPA #$3A
 BCC MAKNU1
 BRA MAKNAM
MAKNA0 LDX #NAMMSG
 JSR OUTNCR
 JSR KEYBD
 LDX #BUFFER
 STX BUFNXT
 BRA MAKN
MAKNU1 JSR GETNAM
 JSR CLRPAS
 CMPA #',
 BNE *+5
 JSR GETPAS
 RTS

INIT JSR SELECT
 LDX #SURMSG
 JSR OUTNCR
 JSR KEYBD
 LDX #BUFFER
 JSR SKIPSP
 CMPA #'Y
 BEQ *+5
 JMP DOSTRT
 JSR PRELIM
 CLRA
 STAA TRACK
 STAA SECTOR
INIT1 LDAB TRACK
 STAB DATREG
 LDAB #$1B * SEEK, LOAD, NOVERIFY
 STAB COMREG
 JSR RETURN
 JSR RETURN
INIT11 LDAB COMREG
 BITB #1
 BNE INIT11
 LDAB #$F4 * WRITE TRACK
 STAB COMREG
 LDAB #46
 CLRA
INIT2 JSR UNPACK
 DECB
 BNE INIT2
 LDAA #$FC
 JSR UNPACK
 LDAB #11
 CLRA
INIT22 JSR UNPACK
 DECB
 BNE INIT22
INIT3 LDAB #6
 CLRA
INIT31 JSR UNPACK
 DECB
 BNE INIT31
 LDAA #$FE
 JSR UNPACK
 LDAA TRACK
 JSR UNPACK
 CLRA
 JSR UNPACK
 LDAA SECTOR
 JSR UNPACK
 LDAA #$01
 JSR UNPACK
 LDAA #$F7
 JSR UNPACK
 LDAB #17
 CLRA
INIT33 JSR UNPACK
 DECB
 BNE INIT33
 LDAA #$FB
 JSR UNPACK
 CLRB
 LDAA #$E5
INIT35 JSR UNPACK
 DECB
 BNE INIT35
 LDAA #$F7
 JSR UNPACK
 LDAB #11
 CLRA
INIT37 JSR UNPACK
 DECB
 BNE INIT37
 LDAB SECTOR
 INCB
 STAB SECTOR
 CMPB #10
 BNE INIT3
INIT4 LDAB COMREG
 BITB #1
 BEQ INIT45
 BRA INIT4
INIT45 CLRA
 STAA SECTOR
 LDAA TRACK
 INCA
 STAA TRACK
 CMPA #35
 BEQ INIT5
 JMP INIT1
INIT5 CLRA
 STAA TRACK
 STAA SECTOR
 LDX #$2FFF
 STX EMEMH
 LDX #$2400
 JSR WRITE
 CLRA
 STAA SECTOR
 LDAA #2
 STAA TRACK
 LDX #DBUFFR+255
 STX EMEMH
 LDX #DBUFFR
 LDAA #'$
 STAA 0,X
 INX
 LDAA #'D
 STAA 0,X
 INX
 LDAA #'O
 STAA 0,X
 INX
 LDAA #'S
 STAA 0,X
 LDAB #12
 LDAA #$20
 INX
INIT51 STAA 0,X
 INX
 DECB
 BNE INIT51
 CLR 1,X
 CLR 2,X
 CLR 4,X
 LDAA #20
 STAA 3,X
 LDAA #$FF
 STAA 16,X
 LDAA #$03
 STAA 32,X
 CLRA
 STAA 33,X
 STAA 36,X
 CLR 0,X
 STS SAVEX
 LDS #$2400
 STS 5,X
 LDS #$2FFF
 STS 7,X
 LDS #$2600
 STS 9,X
 LDS #$014A * NUMBER OF SECOTRS
 STS 34,X
 LDS SAVEX
 LDX #DBUFFR
 JSR WRITE0
 JMP DOSTRT
UNPACK PSHB
UNPAC1 LDAB COMREG
 BITB #2
 BEQ UNPAC1
 STAA DATREG
 PULB
 RTS
CREATE CLRA
 STAA TYPE
 JSR MAKNAM
CREAT2 LDX #SECNUM
 JSR OUTNCR
 JSR KEYBD
 LDX #BUFFER
 JSR BYTE
 BCS CREAT2
 STAA NEEDNO
 LDX #$0000
 STX STARTX
 STX ENDX
 LDX #DOS
 STX GOX
 STX HILINE
 LDAA #2
 STAA CREFLG
 JSR LIB1
 JMP DOSTRT
HOME JSR SELECT
 JSR PRELIM
 JMP DOSTRT
RENAME LDAA #$FF
 STAA TYPE
 JSR MAKNAM
 JSR RLIB
 LDAA #$02
 STAA TRACK
 LDAA LIBSEC
 STAA SECTOR
 STX TEMPX2
 LDX #NEWMSG
 JSR OUTNCR
 JSR MAKNA0
 LDAB #16
 LDX TEMPX2
 STS SAVEX
 SEI
 LDS #NAME-1
REN2 PULA
 STAA 0,X
 INX
 DECB
 BNE REN2
 LDS SAVEX
 NOP
 CLI
 BRA PURGE3

PURGE LDAA #$FF
 STAA TYPE
 JSR MAKNAM
PURGE1 LDAA NAME
 CMPA #'$
 BNE *+5
 JMP GOAWAY
 JSR RLIB
 LDAA #$02
 STAA TRACK
 LDAA LIBSEC
 STAA SECTOR
 CLR 20,X
 LDAB #16
 LDAA #$20
PURGE2 STAA 0,X
 INX
 DECB
 BNE PURGE2
PURGE3 LDX PROGX
 STX TEMPX3
 LDX #PURGE4
 STX PROGX
PURGE4 LDX #DBUFFR+255
 STX EMEMH
 LDX #DBUFFR
 JSR WRITE
 LDX TEMPX3
 STX PROGX
 JMP DOSTRT
LOAD LDAA #$22
 STAA TYPE
 BSR LOAD0
 JMP DOSTRT
LOAD0 JSR MAKNAM
 JSR RLIB
LOAD3 LDX BMEMH
 JSR READ0
 LDX GOX
 STX $A048
 RTS
RUN LDAA #$22
 STAA TYPE
RUN0 BSR LOAD0
 LDS #$A049
 JMP 0,X
SAVE LDAA #$22
 STAA TYPE
 JSR MAKNAM
SAVE2 LDX #FIRADD
 JSR GET4
 STX STARTX
 LDX #LASADD
 JSR GET4
 STX ENDX
 LDX #PSTART
 JSR GET4
 STX GOX
 LDAA NAME
 CMPA #'$
 BNE SAVE3
 LDAA #$11
 STAA TYPE
 JSR KEYBD
 LDAA BUFFER
 CMPA #'$
 BNE GOAWAY
 LDAA BUFFER+1
 CMPA #'$
 BEQ SAVE3
GOAWAY LDX #NOSIR
 JSR OUTPUT
 JMP DOSTRT
SAVE3 JSR WLIB
 LDX BMEMH
 JSR WRITE0
 JMP DOSTRT

NOSIR FCC /SYSTEM FILE - ACCESS DENIED/
 FCB 4

GET4 STX TEMPX1
GET40 LDX TEMPX1
 JSR OUTNCR
 JSR KEYBD
 LDX #BUFFER
 JSR SKIPSP
 BSR BYTE
 BCS GET40
 STAA XHI
 BSR BYTE
 BCS GET40
 STAA XLO
 LDX XHI
 RTS

XHI RMB 1
XLO RMB 1

BYTE BSR INHEX
 BCS OUTBYT
 ASLA
 ASLA
 ASLA
 ASLA
 TAB
 BSR INHEX
 BCS OUTBYT
 ABA
 CLC
OUTBYT RTS
INHEX LDAA 0,X
 INX
 SUBA #$30
 BMI NOTHEX
 CMPA #$09
 BLE YESHEX
 CMPA #$11
 BMI NOTHEX
 CMPA #$16
 BGT NOTHEX
 SUBA #7
YESHEX CLC
 RTS
NOTHEX SEC
 RTS
FILES LDAB #$FF
 STAB CREFLG
 BRA CATAL0
CATAL CLR CREFLG
CATAL0 JSR SELECT
 LDAA #2
 STAA TRACK
 CLRA
 STAA SECTOR
 JSR PRELIM
 LDAA #$10
 JSR OUTCH
 LDAA #$16
 JSR OUTCH
 LDAB #3
 PSHB
CATAL1 LDX #DBUFFR+255
 STX EMEMH
 LDX #DBUFFR
 JSR READ0
 LDX #DBUFFR
CATAL2 LDAA 0,X
 CMPA #$FF
 BEQ CATEX
 LDAB #8
CATAL3 LDAA 0,X
 JSR OUTCH
 INX
 DECB
 BNE CATAL3
 LDAB #8
CATAL4 INX
 DECB
 BNE CATAL4
 LDAA #$20
 JSR OUTCH
 TST CREFLG
 BEQ CATA45
 BSR OUT2HS
 BSR OUT2HS
 INX
 BSR OUT2HS
 BSR OUT2HS
 BSR OUT4HS
 BSR OUT4HS
 BSR OUT4HS
 JSR CRLF
 LDAB #5
 BRA CATAL6
CATA45 PULB
 DECB
 BNE CATA46
 JSR CRLF
 LDAB #3
CATA46 PSHB
 LDAB #16
 BRA CATAL6
CATAL5 LDAB #32
CATAL6 INX
 DECB
 BNE CATAL6
 DEX
 CPX EMEMH
 BEQ CATAL1
 INX
 BRA CATAL2
CATEX STX TEMPX1
 PULB
 JSR CRLF
 LDX #REMMSG
 JSR OUTNCR
 LDX TEMPX1
 LDAB #18
CATEX1 INX
 DECB
 BNE CATEX1
 JSR OUT4HS
 JSR CRLF
 JMP DOSTRT

REMMSG FCC /SECTORS REMAINING (HEX)  /
 FCB 4

OUT2H LDAA 0,X
OUT2HA BSR OUTHL
 LDAA 0,X
 INX
 BRA OUTHR
OUT4HS BSR OUT2H
OUT2HS BSR OUT2H
OUTS LDAA #$20
 JMP OUTCH
OUTHL LSRA
 LSRA
 LSRA
 LSRA
OUTHR ANDA #$F
 ADDA #$30
 CMPA #$39
 BLS OUTHHH
 ADDA #$7
OUTHHH JMP OUTCH
TEST JSR SELECT
 JSR PRELIM
 LDX #TEST2
 STX PROGX
 LDAA #0
 STAA TRACK
 STAA SECTOR
 LDX #$19FF
 STX EMEMH
TEST1 LDX #$1000
 JSR READ0
 LDAA TRACK
 CMPA #35
 BNE TEST1
 LDX #TSTDON
 JSR OUTPUT
 JMP DOSTRT
TEST2 LDAA SECTOR
 INCA
 CMPA #10
 BNE TEST3
 CLRA
 INC TRACK
TEST3 STAA SECTOR
 BRA TEST1
 END
