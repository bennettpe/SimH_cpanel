!
!	MERGE TWO BINARIES
!
!	Jun 2022 - Roberto Sancho
!	Used to build ASM14, BASIC and MetaCompiler
!	Easier to use in SimH scripts than MNB14

!       COPY file1.680 + file2.680 -> OutFile.680 
!       convert type 03 record from file1.680 to type 02 record 
!       skip type 01 record (5 first bytes from file) of file2.680 before coping it
!       keep type 03 record from file2.680 

	DIM	BIGBLOCK$[128]
	DIM	TWOB$/0,0/, REC1$/0,0,0,0,0/, BYTE$/0/, I
	DIM	FILE$[100], FILE1$[100], FILE2$[100], OUTFILE$[100]
	DIM	CURPOS, ADDRESS, COUNT, OPT

	PRINT	"*** MERGE BINARY PROGRAM ***"

10	INPUT	"Input File1= " FILE1$
	INPUT	"Input File2= " FILE2$
	INPUT	"Output File= " OUTFILE$

	IF OUTFILE$="" THEN GOTO 10
	
	CREATE	#2,OUTFILE$

	OPT=1
	FILE$=FILE1$
	GOSUB 100
	OPT=2
	FILE$=FILE2$
	GOSUB 100
	
	PRINT "Generated File: "; OUTFILE$
	GOTO 9999

100	! LOAD FILE AND APPEND TO OUTFILE

	PRINT "Loading File: "; FILE$

	OPEN	#1, FILE$
	CURPOS	=0
	! IF OPT=1 -> KEEP TYPE 1 RECORD HEADER (5 BYTES), 
	!             CHANGE TYPE 3 RECORD TO TYPE 2
	! IF OPT=2 -> SKIP TYPE 1 RECORD HEADER, 
	!             KEEP TYPE 3 RECORD TO TYPE 2
	
	IF OPT=1 THEN 
	   ! READ THEN WRITE TYPE 1 RECORD HEADER (5 BYTES)
	   READ #1, REC1$
	   WRITE #2, REC1$
	ELSE
	   ! READ AND DISCARD TYPE 1 RECORD HEADER (5 BYTES)
	   READ #1, REC1$
	FI
	CURPOS = CURPOS+5
	
110	RESTORE	#1, CURPOS
	READ	#1, BYTE$
	ON	BYTE$[1]+1 GOTO 120, 121, 122, 123

	PRINT	"UNEXPECTED LOAD RECORD AT";CURPOS;" (";BYTE$[1];")"
	CLOSE	#2
	STOP

120	WRITE 	#2, BYTE$ 
	READ	#1, TWOB$
	WRITE 	#2, TWOB$ 
	! TYPE 0 RECORD: SKIP RECORD
	COUNT = TWOB$[1]**8+TWOB$[2]
	CURPOS = CURPOS+3+COUNT
	
	PRINT	"   SKIP ";HEX$(COUNT)
	
	IF COUNT=0 THEN 110
	FOR I=1 TO COUNT
	   READ #1, BYTE$
	   WRITE #2, BYTE$
	NEXT I
	GOTO	110

121	PRINT	"UNEXPECTED TYPE 1 RECORD RECORD AT";CURPOS;" (";BYTE$[1];")"
	CLOSE	#2
	STOP

122	GOSUB	124
	GOTO	110

123	GOSUB	124
	! LAST LOAD RECORD PROCESSED. CLOSE INPUT FILE
	CLOSE	#1
	RETURN

124	! COPY LOAD RECORDS

	IF OPT=1 THEN 
	   ! CHANGE TYPE 3 RECORD TO TYPE 2
	   BYTE$[1]=:02
	FI
	WRITE #2, BYTE$ 

	READ	#1, TWOB$
	ADDRESS=TWOB$[1]**8+TWOB$[2]
	WRITE 	#2, TWOB$ 
	READ	#1, TWOB$
	COUNT	=TWOB$[1]**8+TWOB$[2]
	WRITE 	#2, TWOB$ 
	CURPOS = CURPOS+5+COUNT

	PRINT	"   ADDR ";HEX$(ADDRESS);", COUNT ";HEX$(COUNT)

	IF COUNT=0 THEN RETURN

	IF COUNT >= MAXLEN(BIGBLOCK$) THEN
	   FOR I=1 TO INT(COUNT/MAXLEN(BIGBLOCK$))
	      READ #1, BIGBLOCK$
	      WRITE #2, BIGBLOCK$
	      COUNT=COUNT-MAXLEN(BIGBLOCK$)
	   NEXT	I
	FI

	IF COUNT=0 THEN RETURN

	FOR I=1 TO COUNT
	   READ #1, BYTE$
	   WRITE #2, BYTE$
	NEXT I
	RETURN

9999	END

