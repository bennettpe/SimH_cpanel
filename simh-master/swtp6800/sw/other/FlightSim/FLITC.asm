;*	NAM FLITC
	ORG  $7000
SCREEN	CMPA #01       
	BEQ  FIRST     
	CMPA #02       
	BEQ  JINIT     
	CMPA #03       
	BEQ  ERASE     
	CMPA #04
	BEQ  PIXEL
	CMPA #05
	BEQ  SHOW2
	CMPA #06
	BEQ  START
	RTS
;* PIA INITIALIZATION SECTION
FIRST	CLRA
	STAA $800C     
	LDAA #$FF
	STAA $800C
	LDAA #$3F
	STAA $800D
	RTS
;* ERASE SCREEN SECTION
ERASE	LDAA #63
	STAA HPOS
HSET	LDAA HPOS
	BSR  SENDA
VBAR	LDAA #128
CLR	BSR  SENDA
	INCA
MOD	CMPA #224
	BNE  CLR
	DEC  HPOS
	BGE  HSET
	LDAA 3192
	STAA MOD+1
	RTS
;*SCREEN DRIVE SUBROUTINES
SENDA	TAB
SENDB	STAB $800C
	LDAB #$37
	STAB $800D
	LDAB 0,X
	LDAB #$3F
	STAB $800D
	RTS
;* PIXEL OUTPUT
PIXEL	LDAB X1
	ADDB #96
	BSR SENDB
	LDAB #160
	SUBB Y1
	BSR SENDB
	RTS
SHOW2	BRA SHOW
;* INITIALIZE PIA INTERFACE TO JOYSTICK
JINIT	LDAA #$3D
	LDX #$800C
	LDAB #$00
	STAB 2,X
	STAA 3,X
;* SAVE MACHINE REGISTERS ON INPUT
START	STAA ASAVE
	STAB BSAVE
	STX ISAVE
;* GET HORIZONTAL STICK POSITION
	LDX #$800C
LOOP1	LDA 3,X
	ANDA #$80
	BEQ LOOP1
	LDAA 2,X
	TAB
	ANDA #$80
	BNE LOOP1
	ANDB #$3F
	SUBB #$20
	BGE RTN1
	NEGB
	ADDB #64
RTN1	STAB JHPOS
LOOP2	LDAA 3,X
	ANDA #$80 
	BEQ LOOP2
	LDAA 2,X
	TAB
	ANDA #$80
	BNE GOBACK
	BRA LOOP1
GOBACK	ANDB #$3F
	SUBB #$20
	BGE RTN2
	NEGB
	ADDB #64
RTN2	STAB JVPOS
	LDAA ASAVE
	LDAB BSAVE
	LDX ISAVE
	RTS
;* ROUTINE TO DRAW LINE FROM (X1,Y1) TO (X2,Y2)
SHOW	CLRA
	LDAB #1
	STAB M
	STAB N
	LDAB X2 
	SUBB X1
	STAB D
	BGE BP1
	NEG M
	NEG D
BP1	BNE BP2
	LDAA #$FF
BP2	LDAB Y2
	SUBB Y1
	STAB E
	BGE B8963
	NEG N
	NEG E
B8963	JSR PIXEL
	LDAB X1
	CMPB X2
	BEQ B8990
B8969	TSTA
	BLT B8981
	LDAB X1
	ADDB M
	STAB X1
	SUBA E
	BRA B8963
B8981	LDAB Y1
	ADDB N
	STAB Y1
	ADDA D
	BRA B8963
B8990	LDAB Y1
	CMPB Y2
	BNE B8969
	RTS
X1	EQU $0050
X2	EQU $0052
Y1	EQU $0051
Y2	EQU $0053
M	EQU $0054
N	EQU $0055
D	EQU $0056
E	EQU $0057
HPOS	EQU $0058
JHPOS	EQU $0059
JVPOS	EQU $005A
ASAVE	EQU $005B
BSAVE	EQU $005C
ISAVE	EQU $005D

	ORG $7200
	
WELCOM	FCC '     WELCOME TO THE M6800 FLIGHT SIMULATOR PROGRAM'
	FCC 'THIS PROGRAM IS DESIGNED TO ALLOW THE USER TO'
	FCC 'SET UP THE AIRCRAFT`S INITIAL POSITION COORDINAES'
	FCC 'FROM USER PROMPTS, ENGAGE THE FLIGHT SIMULATOR'
	FCC 'AND PRESENT AN UPDATED GRAPHICS DISPLAY AS'
	FCC 'STUDENT PILOT ATTEMPTS TO MANUEVER THE PLANE FOR'
	FCC 'LANDING ON AN AIRSTRIP. TO USE THE PROGRAM YOU'
	FCC 'FIRST MUST CHOOSE AN AIRPORT DATABASE.'
	FCC 'PLEASE DEPRESS THE SPACEBAR FOR MORE INTRUCTIONS'
	FCC '     '
	FCB $04

	ORG $7700

MSGL	FCC '	THANKYOU FOR FLYING WITH US TODAY ON PARSONS'
	FCC 'PIPER AIRWAY`S. PLEASE REMAIN SEATED '
	FCC 'UNTIL THE AIRCRAFT HAS COME TO A COMPLETE STOP.'
	FCC 'HAVE A GOOD DAY.'
	FCB $04
	
	ORG $7800
	
X3	RMB 2
Y3	RMB 2
Z3	RMB 2
PITCH	RMB 2
BANK	RMB 2
HEAD	RMB 2
M1	RMB 2
N1	RMB 2
N2	RMB 2
N3	RMB 2
N4	RMB 2
N5	RMB 2
GDOPT	RMB 1
NEWDAT	RMB 1
ENABLE	RMB 1
IDXREG	RMB 2
FLGDUN	RMB 1

	ORG $78F0
	
COUNT	RMB 1
STKSAV	RMB 2

	ORG $7900
	
MSGA	FCC '   CHOOSE THE AIRPORT DATABASE AS FOLLOWS: '
	FCC ' '		
	FCC ' 1 = PARSON`S FIELD AT TETERBORO, NEW JERSEY.'	
	FCC ' 2 = ARTWICK`S FIELD AT TETERBORO, NEW JERSEY.'	
	FCB $04
;* THIS CONCLUDES ALL OF THE PROGRAMMING FOR THE M6800 FLIGHT
;* SIMULATOR PROGRAM	
	END	